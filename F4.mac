	.symlen 6
	SUBTTL	Common file for BASIC interpreter
	.SALL

CONTO	defl 15	; CHARACTER TO SUPRESS OUTPUT (USUALLY CONTROL-O)
DBLTRN	defl 0	; FOR DOUBLE PRECISION TRANSCENDENTALS
	IF2

	.printx	/EXTENDED/


	.printx	/LPT/

	.printx	/CPM DISK/


	.printx	/Z80/

	.printx	/FAST/

	.printx	/5.0 FEATURES/

	.printx	/ANSI COMPATIBLE/
	ENDIF

CLMWID	defl 14	; MAKE COMMA COLUMNS FOURTEEN CHARACTERS
DATPSC	defl 128	; NUMBER OF DATA BYTES IN DISK SECTOR
LINLN	defl 80	; TERMINAL LINE LENGTH
LPTLEN	defl 132
BUFLEN	defl 255	; LONG LINES
NAMLEN	defl 40	; MAXIMUM LENGTH NAME -- 3 TO 127

NUMLEV	defl 0*20+19+2*5	; NUMBER OF STACK LEVELS RESERVED
			; BY AN EXPLICIT CALL TO GETSTK

STRSIZ	defl 4

STRSIZ	defl 3
NUMTMP	defl 3	; NUMBER OF STRING TEMPORARIES

NUMTMP	defl 10

MD.RND	defl 3	; THE MODE NUMBER FOR RANDOM FILES
MD.SQI	defl 1	; THE MODE NUMBER FOR SEQUENTIAL INPUT FILES
			; NEVER WRITTEN INTO A FILE
MD.SQO	defl 2	; THE MODE FOR SEQUENTIAL OUTPUT FILES
			; AND PROGRAM FILES
CPMWRM	defl 0	; CP/M WARM BOOT ADDR
CPMENT	defl CPMWRM+5	; CP/M BDOS CALL ADDR
	CSEG
TRUROM	defl 0
	PAGE
	TITLE	MATHPK FOR BASIC MCS 8080 GATES/ALLEN/DAVIDOFF
	SUBTTL	FLOATING POINT MATH PACKAGE CONFIGURATION

CURNCY	defl 24h	; USE DOLLAR SIGN AS DEFAULT

	PUBLIC	RND,SIN,FOUT,INPRT,LINPRT
	PUBLIC	ZERO,FLOAT,FLOATR,MOVE,FADD,FADDS,FSUB,FMULT,FDIV,FIN
	EXTRN	INTXT,SNERR,BSERR,OVERR,MINPLS
	PUBLIC	NORMAL,UNPACK
	PUBLIC	PUSHF,ABS,INT,QINT,SGN,FCOMP,SIGNC,POPHRT
	PUBLIC	SQR
	PUBLIC	MOVFM,MOVMF,MOVFR,MOVRF,MOVRM,NEG,INRART,INXHRT
	EXTRN	CNSGET
	PUBLIC	UMULT,SIGNS
	PUBLIC	FPWR,EXP,LOG,COS,TAN,ATN,FONE
	PUBLIC	PUFOUT
	PUBLIC	VMOVMF,VMOVFM,FRCINT,FRCSNG,FRCDBL,VNEG,IADD
	PUBLIC	ISUB,IMULT,ICOMP,INEG,DADD,DSUB,DMULT,DDIV,DCOMP,VINT
	PUBLIC	VMOVE,VALINT,VALSNG,FRCSTR,CHKSTR,MAKINT,ISIGN
	PUBLIC	DCOMPD,DNORML,DINT
	PUBLIC	FDIVT,CONSIH,CONIA,VMOVFA,VMOVAF,GETBCD,VSIGN,VDFACS

	PUBLIC	IMOD,IDIV

	EXTRN	FANSII,OVERRI,STRPRN,FLGOVC
	EXTRN	TTYPOS
	EXTRN	DIVMSG,OVRMSG
	EXTRN	CALTTY
	EXTRN	FAC,FACLO,FBUFFR,MINUTK,PLUSTK,ERROR,FCERR
	EXTRN	CHRGTR,OUTDO,DV0ERR,STROUT
	PUBLIC	SIGN
	PUBLIC	SINCON,ATNCON

	EXTRN	GETYPR
	EXTRN	DCOMPR,SYNCHR
	EXTRN	TMERR
	EXTRN	ERRFLG,DFACLO,ARG,ARGLO,VALTYP,TEMP2,TEMP3



;
;	EXTERNAL LOCATIONS USED BY THE MATH-PACKAGE
;	;THE FLOATING ACCUMULATOR
;IFN	LENGTH&2,<
;	BLOCK	1		;[TEMPORARY LEAST SIGNIFICANT BYTE]
;DFACLO:	BLOCK	4>		;[FOUR LOWEST ORDERS FOR DOUBLE PRECISION]
;FACLO:	BLOCK	3		;[LOW ORDER OF MANTISSA (LO)]
;				;[MIDDLE ORDER OF MANTISSA (MO)]
;				;[HIGH ORDER OF MANTISSA (HO)]
;FAC:	BLOCK	2		;[EXPONENT]
;				;[TEMPORARY COMPLEMENT OF SIGN IN MSB]
;IFN	LENGTH&2,<
;	BLOCK	1		;[TEMPORARY LEAST SIGNIFICANT BYTE]
;ARGLO:	BLOCK	7		;[LOCATION OF SECOND ARGUMENT FOR DOUBLE
;ARG:	BLOCK	1>		; PRECISION]
;FBUFFR:	BLOCK	^D13		;BUFFER FOR FOUT
;IFN	LENGTH&2,<BLOCK	^D<35-13>>
;
;
;THE FLOATING POINT FORMAT IS AS FOLLOWS:
;
;THE SIGN IS THE FIRST BIT OF THE MANTISSA
;THE MANTISSA IS 24 BITS LONG
;THE BINARY POINT IS TO THE LEFT OF THE MSB
;NUMBER = MANTISSA * 2 ^ EXPONENT
;THE MANTISSA IS POSITIVE, WITH A ONE ASSUMED TO BE WHERE THE SIGN BIT IS
;THE SIGN OF THE EXPONENT IS THE FIRST BIT OF THE EXPONENT
;THE EXPONENT IS STORED IN EXCESS 200 I.E. WITH A BIAS OF 200
;SO, THE EXPONENT IS A SIGNED 8-BIT NUMBER WITH 200 ADDED TO IT
;AN EXPONENT OF ZERO MEANS THE NUMBER IS ZERO, THE OTHER BYTES ARE IGNORED
;TO KEEP THE SAME NUMBER IN THE FAC WHILE SHIFTING:
;	TO SHIFT RIGHT,	EXP:=EXP+1
;	TO SHIFT LEFT,	EXP:=EXP-1
;
;SO, IN MEMORY THE NUMBER LOOKS LIKE THIS:
;	[BITS 17-24 OF THE MANTISSA]
;	[BITS 9-16 OF THE MANTISSA]
;	[THE SIGN IN BIT 7, BITS 2-8 OF THE MANTISSA ARE IN BITS 6-0]
;	[THE EXPONENT AS A SIGNED NUMBER + 200]
;(REMEMBER THAT BIT 1 OF THE MANTISSA IS ALWAYS A ONE)
;
;ARITHMETIC ROUTINE CALLING CONVENTIONS:
;
;FOR ONE ARGUMENT FUNCTIONS:
;	THE ARGUMENT IS IN THE FAC, THE RESULT IS LEFT IN THE FAC
;FOR TWO ARGUMENT OPERATIONS:
;	THE FIRST ARGUMENT IS IN B,C,D,E I.E. THE "REGISTERS"
;	THE SECOND ARGUMENT IS IN THE FAC
;	THE RESULT IS LEFT IN THE FAC
;
;THE "S" ENTRY POINTS TO THE TWO ARGUMENT OPERATIONS HAVE (HL) POINTING TO
;THE FIRST ARGUMENT INSTEAD OF THE FIRST ARGUMENT BEING IN THE REGISTERS.
;MOVRM IS CALLED TO GET THE ARGUMENT IN THE REGISTERS.
;THE "T" ENTRY POINTS ASSUME THE FIRST ARGUMENT IS ON THE STACK.
;POPR IS USED TO GET THE ARGUMENT IN THE REGISTERS.
;NOTE: THE "T" ENTRY POINTS SHOULD ALWAYS BE JUMPED TO AND NEVER CALLED
;BECAUSE THE RETURN ADDRESS ON THE STACK WILL BE CONFUSED WITH THE NUMBER.
;
;ON THE STACK, THE TWO LO'S ARE PUSHED ON FIRST AND THEN THE HO AND SIGN.
;THIS IS DONE SO IF A NUMBER IS STORED IN MEMORY, IT CAN BE PUSHED ON THE
;STACK WITH TWO PUSHM'S.  THE LOWER BYTE OF EACH PART IS IN THE LOWER
;MEMORY ADDRESS SO WHEN THE NUMBER IS POPPED INTO THE REGISTERS, THE HIGHER
;ORDER BYTE WILL BE IN THE HIGHER ORDER REGISTER OF THE REGISTER PAIR, I.E.
;THE HIGHER ORDER BYTE WILL BE POPPED INTO B, D OR H.
;%
	PAGE

	SUBTTL	FLOATING POINT ADDITION AND SUBTRACTION
			; ENTRY TO FADD WITH POINTER TO ARG IN (HL)

	PUBLIC	FADDH
FADDH:	ld	hl,FHALF	; ENTRY TO ADD 1/2
FADDS:	call	MOVRM	; GET ARGUMENT INTO THE REGISTERS
	jp	FADD	; DO THE ADDITION


			; SUBTRACTION FAC:=ARG-FAC
FSUBS:	call	MOVRM	; ENTRY IF POINTER TO ARG IS IN (HL)
FSUB:	call	NEG	; NEGATE SECOND ARGUMENT
			; FALL INTO FADD


			; ADDITION FAC:=ARG+FAC
			; ALTERS A,B,C,D,E,H,L
;*****************************************************************
;IF INTFSW=1 THE FORMAT OF FLOATING POINT NUMBERS WILL BE:
;REG B:SIGN AND BITS 1-7 OF EXPONENT,REG C:BIT 8 OF EXPONENT
;AND BITS 2-8 OF MANTISSA,REG D:BITS 9-16 OF MANTISSA,
;REG E:BITS 17-24 OF MANTISSA, AND LIKEWISE FOR THE FAC FORMAT
;FURTHERMORE, THE EXPONENT FOR INTEL WILL BE BIAS 177 OCTAL
;******************************************************************
FADD:	ld	a,b	; CHECK IF FIRST ARGUMENT IS ZERO
	or	a	; GET EXPONENT
	ret	z		; IT IS, RESULT IS NUMBER IN FAC
	ld	a,(FAC)	; GET EXPONENT
	or	a	; SEE IF THE NUMBER IS ZERO
	jp	z,MOVFR	; IT IS, ANSWER IS IN REGISTERS

;WE WANT TO GET THE SMALLER NUMBER IN THE REGISTERS SO WE CAN SHIFT IT RIGHT
;AND ALIGN THE BINARY POINTS OF THE TWO NUMBERS.  THEN WE CAN JUST ADD OR
;SUBTRACT THEM (DEPENDING ON THEIR SIGNS) BYTEWISE.
	sub	b	; CHECK RELATIVE SIZES
	jp	nc,FADD1	; IS FAC SMALLER?
	cpl		; YES, NEGATE SHIFT COUNT
	inc	a
	ex	de,hl		; SWITCH FAC AND REGISTERS, SAVE (DE)
	call	PUSHF	; PUT FAC ON STACK
	ex	de,hl		; GET (DE) BACK WHERE IT BELONGS
	call	MOVFR	; PUT REGISTERS IN THE FAC
	pop	bc
	pop	de
			; GET THE OLD FAC IN THE REGISTERS
FADD1:	
	cp	19h	; ARE WE WITHIN 24 BITS?
	ret	nc
	push	af	; SAVE SHIFT COUNT
	call	UNPACK	; UNPACK THE NUMBERS
	ld	h,a	; SAVE SUBTRACTION FLAG
	pop	af	; GET SHIFT COUNT BACK
	call	SHIFTR	; SHIFT REGISTERS RIGHT THE RIGHT AMOUNT

;IF THE NUMBERS HAVE THE SAME SIGN, THEN WE ADD THEM.  IF THE SIGNS ARE
;DIFFERENT, THEN WE HAVE TO SUBTRACT THEM.  WE HAVE TO DO THIS BECAUSE THE
;MANTISSAS ARE POSITIVE.  JUDGING BY THE EXPONENTS, THE LARGER NUMBER IS IN
;THE FAC, SO IF WE SUBTRACT, THE SIGN OF THE RESULT SHOULD BE THE SIGN OF THE
;FAC; HOWEVER, IF THE EXPONENTS ARE THE SAME, THE NUMBER IN THE REGISTERS
;COULD BE BIGGER, SO AFTER WE SUBTRACT THEM, WE HAVE TO CHECK IF THE RESULT
;WAS NEGATIVE.  IF IT WAS, WE NEGATE THE NUMBER IN THE REGISTERS AND
;COMPLEMENT THE SIGN OF THE FAC.  (HERE THE FAC IS UNPACKED)
;IF WE HAVE TO ADD THE NUMBERS, THE SIGN OF THE RESULT IS THE SIGN OF THE
;FAC.  SO, IN EITHER CASE, WHEN WE ARE ALL DONE, THE SIGN OF THE RESULT
;WILL BE THE SIGN OF THE FAC.
	ld	a,h	; GET SUBTRACTION FLAG
	or	a
	ld	hl,FACLO	; SET POINTER TO LO'S
	jp	p,FADD3	; SUBTRACT IF THE SIGNS WERE DIFFERENT
	call	FADDA	; ADD THE NUMBERS
	jp	nc,ROUND	; ROUND RESULT IF THERE WAS NO OVERFLOW
			; THE MOST IT CAN OVERFLOW IS ONE BIT
	inc	hl	; THERE WAS OVERFLOW
	inc	(hl)	; INCREMENT EXPONENT
	jp	z,OVFIN4
	ld	l,1	; SHIFT RESULT RIGHT ONE, SHIFT CARRY IN
	call	SHRADD
	jp	ROUND	; ROUND RESULT AND WE ARE DONE
			; HERE TO SUBTRACT C,D,E,B FROM ((HL)+0,1,2),0
FADD3:	xor	a	; SUBTRACT NUMBERS, NEGATE UNDERFLOW BYTE
	sub	b
	ld	b,a	; SAVE IT
	ld	a,(hl)	; SUBTRACT LOW ORDERS
	sbc	a,e
	ld	e,a
	inc	hl	; UPDATE POINTER TO NEXT BYTE
	ld	a,(hl)	; SUBTRACT MIDDLE ORDERS
	sbc	a,d
	ld	d,a
	inc	hl	; UPDATE POINTER TO HIGH ORDERS
	ld	a,(hl)	; SUBTRACT HIGH ORDERS
	sbc	a,c
	ld	c,a
			; BECAUSE WE WANT A POSITIVE MANTISSA, CHECK IF WE HAVE TO NEGATE THE
			;  NUMBER
FADFLT:	call	c,NEGR	; ENTRY FROM FLOATR, INT: NEGATE NUMBER IF IT
			;  WAS NEGATIVE, FALL INTO NORMALIZE


			; NORMALIZE C,D,E,B
			; ALTERS A,B,C,D,E,H,L
			; HERE WE SHIFT THE MANTISSA LEFT UNTIL THE MSB IS A ONE.
			; EXCEPT IN 4K, THE IDEA IS TO SHIFT LEFT BY 8 AS MANY TIMES AS
			; POSSIBLE.
NORMAL:	
	ld	l,b	; PUT LOWEST 2 BYTES IN (HL)
	ld	h,e
	xor	a	; ZERO SHIFT COUNT
NORM1:	ld	b,a	; SAVE SHIFT COUNT
	ld	a,c	; DO WE HAVE 1 BYTE OF ZEROS
	or	a
	jp	nz,NORM3	; NO, SHIFT ONE PLACE AT A TIME
			; THIS LOOP SPEEDS THINGS UP BY SHIFTING 8 PLACES AT ONE TIME
	ld	c,d	; YES, SHIFT OVER 1 BYTE
	ld	d,h
	ld	h,l
	ld	l,a	; SHIFT IN 8 ZEROS FOR THE LOW ORDER
	ld	a,b	; UPDATE SHIFT COUNT
	sub	08h
	cp	0e0h	; DID WE SHIFT IN 4 BYTES OF ZEROS?
	jp	nz,NORM1	; NO, TRY TO SHIFT OVER 8 MORE
			; YES, NUMBER WAS ZERO. FALL INTO ZERO


			; ZERO FAC
			; ALTERS A ONLY
			; EXITS WITH A=0
			; BY OUR FLOATING POINT FORMAT, THE NUMBER IS ZERO IF THE EXPONENT IS
			;  ZERO
ZERO:	xor	a	; ZERO A
ZERO0:	ld	(FAC),a	; ZERO THE FAC'S EXPONENT, ENTRY IF A=0
	ret		; ALL DONE


NORM2:	
	ld	a,h	; CHECK FOR CASE OF NORMALIZING A SMALL INT
	or	l
	or	d
	jp	nz,NORM2U	; DO USUAL THING
	ld	a,c	; GET BYTE TO SHIFT
NORM2F:	dec	b	; DECRMENT SHIFT COUNT
	rla		; SHIFT LEFT
	jp	nc,NORM2F	; NORMALIZE LIKE SOB
	inc	b	; CORRECT SHIFT COUNT
	rra		; WE DID IT ONE TOO MANY TIMES
	ld	c,a	; RESULT TO [C]
	jp	NORM3A	; ALL DONE
NORM2U:	dec	b	; DECREMENT SHIFT COUNT
	add	hl,hl	; ROTATE (HL) LEFT ONE, SHIFT IN A ZERO
	ld	a,d	; ROTATE NEXT HIGHER ORDER LEFT ONE
	rla
	ld	d,a
	ld	a,c	; ROTATE HIGH ORDER LEFT ONE
	adc	a,a	; SET CONDITION CODES
	ld	c,a
NORM3:	jp	p,NORM2	; WE HAVE MORE NORMALIZATION TO DO
NORM3A:	ld	a,b	; ALL NORMALIZED, GET SHIFT COUNT
	ld	e,h	; PUT LO'S BACK IN E,B
	ld	b,l
	or	a	; CHECK IF WE DID NO SHIFTING
	jp	z,ROUND
	ld	hl,FAC	; LOOK AT FAC'S EXPONENT
	add	a,(hl)	; UPDATE EXPONENT
	ld	(hl),a
	jp	nc,ZERO	; CHECK FOR UNDERFLOW
	jp	z,ZERO	; NUMBER IS ZERO, ALL DONE
			; FALL INTO ROUND AND WE ARE DONE


			; ROUND RESULT IN C,D,E,B AND PUT NUMBER IN THE FAC
			; ALTERS A,B,C,D,E,H,L
			; WE ROUND C,D,E UP OR DOWN DEPENDING UPON THE MSB OF B
ROUND:	ld	a,b	; SEE IF WE SHOULD ROUND UP
ROUNDB:	ld	hl,FAC	; ENTRY FROM FDIV, GET POINTER TO EXPONENT

			; INTEL FLOATING SOFTWARE FLAG
	or	a
	call	m,ROUNDA	; DO IT IF NECESSARY
	ld	b,(hl)	; PUT EXPONENT IN B
			; HERE WE PACK THE HO AND SIGN
	inc	hl	; POINT TO SIGN
	ld	a,(hl)	; GET SIGN
	and	80h	; GET RID OF UNWANTED BITS
	xor	c	; PACK SIGN AND HO
	ld	c,a	; SAVE IT IN C
	jp	MOVFR	; SAVE NUMBER IN FAC




			; SUBROUTNE FOR ROUND: ADD ONE TO C,D,E
ROUNDA:	inc	e	; ADD ONE TO THE LOW ORDER, ENTRY FROM QINT
	ret	nz		; ALL DONE IF IT IS NOT ZERO
	inc	d	; ADD ONE TO NEXT HIGHER ORDER
	ret	nz		; ALL DONE IF NO OVERFLOW
	inc	c	; ADD ONE TO THE HIGHEST ORDER
	ret	nz		; RETURN IF NO OVEFLOW
	ld	c,80h	; THE NUMBER OVERFLOWED, SET NEW HIGH ORDER
	inc	(hl)	; UPDATE EXPONENT
	ret	nz		; RETURN IF IT DID NOT OVERFLOW
	jp	OVFIN8	; OVERFLOW AND CONTINUE



			; ADD (HL)+2,1,0 TO C,D,E
			; THIS CODE IS USED BY FADD, FOUT
FADDA:	ld	a,(hl)	; GET LOWEST ORDER
	add	a,e	; ADD IN OTHER LOWEST ORDER
	ld	e,a	; SAVE IT
	inc	hl	; UPDATE POINTER TO NEXT BYTE
	ld	a,(hl)	; ADD MIDDLE ORDERS
	adc	a,d
	ld	d,a
	inc	hl	; UPDATE POINTER TO HIGH ORDER
	ld	a,(hl)	; ADD HIGH ORDERS
	adc	a,c
	ld	c,a
	ret		; ALL DONE


			; NEGATE NUMBER IN C,D,E,B
			; THIS CODE IS USED BY FADD, QINT
			; ALTERS A,B,C,D,E,L
NEGR:	ld	hl,FAC+1	; NEGATE FAC
	ld	a,(hl)	; GET SIGN
	cpl		; COMPLEMENT IT
	ld	(hl),a	; SAVE IT AGAIN
	xor	a	; ZERO A
	ld	l,a	; SAVE ZERO IN L
	sub	b	; NEGATE LOWEST ORDER
	ld	b,a	; SAVE IT
	ld	a,l	; GET A ZERO
	sbc	a,e	; NEGATE NEXT HIGHEST ORDER
	ld	e,a	; SAVE IT
	ld	a,l	; GET A ZERO
	sbc	a,d	; NEGATE NEXT HIGHEST ORDER
	ld	d,a	; SAVE IT
	ld	a,l	; GET ZERO BACK
	sbc	a,c	; NEGATE HIGHEST ORDER
	ld	c,a	; SAVE IT
	ret		; ALL DONE


			; SHIFT C,D,E RIGHT
			; A = SHIFT COUNT
			; ALTERS A,B,C,D,E,L
			; THE IDEA (EXCEPT IN 4K) IS TO SHIFT RIGHT 8 PLACES AS MANY TIMES AS
			;  POSSIBLE
SHIFTR:	ld	b,0	; ZERO OVERFLOW BYTE
SHFTR1:	sub	08h	; CAN WE SHIFT IT 8 RIGHT?
	jp	c,SHFTR2	; NO, SHIFT IT ONE PLACE AT A TIME
			; THIS LOOP SPEEDS THINGS UP BY SHIFTING 8 PLACES AT ONE TIME
	ld	b,e	; SHIFT NUMBER 1 BYTE RIGHT
	ld	e,d
	ld	d,c
	ld	c,0	; PUT 0 IN HO
	jp	SHFTR1	; TRY TO SHIFT 8 RIGHT AGAIN
SHFTR2:	add	a,09h	; CORRECT SHIFT COUNT
	ld	l,a	; SAVE SHIFT COUNT
;TEST FOR CASE (VERY COMMON) WHERE SHIFTING SMALL INTEGER RIGHT.
;THIS HAPPENS IN FOR LOOPS, ETC.
	ld	a,d	; SEE IF THREE LOWS ARE ZERO.
	or	e
	or	b
	jp	nz,SHFTR3	; IF SO, DO USUAL.
	ld	a,c	; GET HIGH BYTE TO SHIFT
SHFTRF:	dec	l	; DONE SHIFTING?
	ret	z		; YES, DONE
	rra		; ROTATE ONE RIGHT
	ld	c,a	; SAVE RESULT
	jp	nc,SHFTRF	; ZAP BACK AND DO NEXT ONE IF NONE
	jp	SHFTC	; CONTINUE SHIFTING
SHFTR3:	xor	a	; CLEAR CARRY
	dec	l	; ARE WE DONE SHIFTING?
	ret	z		; RETURN IF WE ARE
	ld	a,c	; GET HO
SHRADD:	rra		; ENTRY FROM FADD, SHIFT IT RIGHT
	ld	c,a	; SAVE IT
SHFTC:	ld	a,d	; SHIFT NEXT BYTE RIGHT
	rra
	ld	d,a
	ld	a,e	; SHIFT LOW ORDER RIGHT
	rra
	ld	e,a
	ld	a,b	; SHIFT OVERFLOW BYTE RIGHT
	rra
	ld	b,a
	jp	SHFTR3	; SEE IF WE ARE DONE


	PAGE
	SUBTTL	NATURAL LOG FUNCTION
			; CALCULATION IS BY:
			;  LN(F*2^N)=(N+LOG2(F))*LN(2)
			; AN APPROXIMATION POLYNOMIAL IS USED TO CALCULATE LOG2(F)

			; CONSTANTS USED BY LOG
FONE:	defb	000	;  1
	defb	000
	defb	000
	defb	81h
LOGP:	defb	004	; HART 2524 COEFFICIENTS
	defb	9ah	; 4.8114746
	defb	0f7h
	defb	19h
	defb	83h
	defb	24h	; 6.105852
	defb	63h
	defb	43h
	defb	83h
	defb	75h	; -8.86266
	defb	0cdh
	defb	8dh
	defb	84h
	defb	0a9h	; -2.054667
	defb	7fh
	defb	83h
	defb	82h
LOGQ:	defb	004
	defb	000	; 1.0
	defb	000
	defb	000
	defb	81h
	defb	0e2h	; 6.427842
	defb	0b0h
	defb	4dh
	defb	83h
	defb	0ah	; 4.545171
	defb	72h
	defb	11h
	defb	83h
	defb	0f4h	; .3535534
	defb	004
	defb	35h
	defb	7fh

LOG:	call	SIGN	; CHECK FOR A NEGATIVE OR ZERO ARGUMENT
	or	a	; SET CC'S PROPERLY
	jp	pe,FCERR	; FAC .LE. 0, BLOW HIM OUT OF THE WATER
			; FSIGN ONLY RETURNS 0,1 OR 377 IN A
			; THE PARITY WILL BE EVEN IF A HAS 0 OR 377

	call	LOG2
	ld	bc,0080h*256+0031h
	ld	de,0072h*256+0018h	; GET LN(2)
	jp	FMULT	; COMPLETE LOG CALCULATION
LOG2:			; USE HART 2524 CALCULATION
	call	MOVRF	; MOVE FAC TO REGISTERS TOO
	ld	a,80h
	ld	(FAC),a	; ZERO THE EXPONENT
	xor	b	; REMOVE 200 EXCESS FROM X
	push	af	; SAVE EXPONENT
	call	PUSHF	; SAVE THE FAC (X)
	ld	hl,LOGP	; POINT TO P CONSTANTS
	call	POLY	; CALCULATE P(X)
	pop	bc	; FETCH X
	pop	hl	; PUSHF WOULD ALTER DE
	call	PUSHF	; PUSH P(X) ON THE STACK
	ex	de,hl		; GET LOW BYTES OF X TO (DE)
	call	MOVFR	; AND MOVE TO FAC
	ld	hl,LOGQ	; POINT TO Q COEFFICIENTS
	call	POLY	; COMPUTE Q(X)
	pop	bc	; FETCH P(X) TO REGISTERS
	pop	de
	call	FDIV	; CALCULATE P(X)/Q(X)
	pop	af	; RE-FETCH EXPONENT
	call	PUSHF	; SAVE EVALUATION
	call	FLOAT	; FLOAT THE EXPONENT
	pop	bc
	pop	de
			; GET EVAL. BACK
	jp	FADD

	PAGE
;	JMP	FMULT		;MULTIPLY BY LN(2)
	SUBTTL	FLOATING MULTIPLICATION AND DIVISION
			; MULTIPLICATION FAC:=ARG*FAC
			; ALTERS A,B,C,D,E,H,L
FMULT:	call	SIGN	; CHECK IF FAC IS ZERO
	ret	z		; IF IT IS, RESULT IS ZERO
	ld	l,0	; ADD THE TWO EXPONENTS, L IS A FLAG
	call	MULDIV	; FIX UP THE EXPONENTS
			; SAVE THE NUMBER IN THE REGISTERS SO WE CAN ADD IT FAST
	ld	a,c	; GET HO
	ld	(FMULTA+1),a	; STORE HO OF REGISTERS
	ex	de,hl		; STORE THE TWO LO'S OF THE REGISTERS
	ld	(FMULTB+1),hl
	ld	bc,0	; ZERO THE PRODUCT REGISTERS
	ld	d,b
	ld	e,b
	ld	hl,NORMAL
	push	hl	;  ON THE STACK
	ld	hl,FMULT2	; PUT FMULT2 ON THE STACK TWICE, SO AFTER
	push	hl	;  WE MULTIPLY BY THE LO BYTE, WE WILL
	push	hl	;  MULTIPLY BY THE MO AND HO
	ld	hl,FACLO	; GET ADDRESS OF LO OF FAC
FMULT2:	ld	a,(hl)	; GET BYTE TO MULTIPLY BY
	inc	hl	; MOVE POINTER TO NEXT BYTE
	or	a
	jp	z,FMULT3	; ARE WE MULTIPLYING BY ZERO?
	push	hl	; SAVE POINTER
	ex	de,hl		; GET LO'S IN (HL)
	ld	e,08h	; SET UP A COUNT

;THE PRODUCT WILL BE FORMED IN C,D,E,B. THIS WILL BE IN C,H,L,B PART OF THE
;TIME IN ORDER TO USE THE "DAD" INSTRUCTION.  AT FMULT2, WE GET THE NEXT
;BYTE OF THE MANTISSA IN THE FAC TO MULTIPLY BY.  ((HL) POINTS TO IT)
;(THE FMULT2 SUBROUTINE PRESERVES (HL))  IN 8K, IF THE BYTE IS ZERO, WE JUST
;SHIFT THE PRODUCT 8 RIGHT.  THIS BYTE IS THEN SHIFTED RIGHT AND SAVED IN D
;(H IN 4K).  THE CARRY DETERMINES IF WE SHOULD ADD IN THE SECOND FACTOR
;IF WE DO, WE ADD IT TO C,H,L.  B IS ONLY USED TO DETERMINE WHICH WAY WE
;ROUND.  WE THEN SHIFT C,H,L,B (C,D,E,B) IN 4K RIGHT ONE TO GET READY FOR THE
;NEXT TIME THROUGH THE LOOP.  NOTE THAT THE CARRY IS SHIFTED INTO THE MSB OF
;C.  E HAS A COUNT (L IN 4K) TO DETERMINE WHEN WE HAVE LOOKED AT ALL THE BITS
;OF D (H IN 4K).
FMULT4:	rra		; ROTATE BYTE RIGHT
	ld	d,a	; SAVE IT
	ld	a,c	; GET HO
	jp	nc,FMULT5	; DON'T ADD IN NUMBER IF BIT WAS ZERO
	push	de	; SAVE COUNTERS
FMULTB:	ld	de,0	; GET LO'S OF NUMBER TO ADD, THIS IS SET ABOVE
	add	hl,de	; ADD THEM IN
	pop	de	; GET COUNTERS BACK
FMULTA:	adc	a,0	; ADD IN HO, THIS IS SET UP ABOVE
FMULT5:	rra		; ROTATE RESULT RIGHT ONE
	ld	c,a
	ld	a,h	; ROTATE NEXT BYTE
	rra
	ld	h,a
	ld	a,l	; ROTATE NEXT LOWER ORDER
	rra
	ld	l,a
	ld	a,b	; ROTATE LO
	rra
	ld	b,a
	and	10h	; SEE IF WE ROTATED THRU ST
	jp	z,FML5B1	; IF NOT DON'T WORRY
	ld	a,b	; RE FETCH LO
	or	20h	; "OR" IN STICKY
	ld	b,a	; BACK TO LO
FML5B1:	
FMLT5B:	
	dec	e	; ARE WE DONE?
	ld	a,d	; GET NUMBER WE ARE MULTIPLYING BY
	jp	nz,FMULT4	; MULTIPLY AGAIN IF WE ARE NOT DONE
	ex	de,hl		; GET LO'S IN (DE)
POPHRT:	pop	hl	; GET POINTER TO NUMBER TO MULTIPLY BY
	ret		; ALL DONE
FMULT3:	ld	b,e	; MULTIPLY BY ZERO: SHIFT EVERYTHING 8 RIGHT
	ld	e,d
	ld	d,c
	ld	c,a	; SHIFT IN 8 ZEROS ON THE LEFT
	ret		; ALL DONE


			; DIVIDE FAC BY 10
			; ALTERS A,B,C,D,E,H,L
DIV10:	call	PUSHF	; SAVE NUMBER
	ld	hl,FTEN	; GET POINTER TO THE CONSTANT '10'
	call	MOVFM	; MOVE TEN INTO THE FAC
FDIVT:	pop	bc
	pop	de
			; GET NUMBER BACK IN REGISTERS
			; FALL INTO DIVIDE AND WE ARE DONE


			; DIVISION FAC:=ARG/FAC
			; ALTERS A,B,C,D,E,H,L
FDIV:	call	SIGN	; CHECK FOR DIVISION BY ZERO
	jp	z,INTDV1	; DON'T ALLOW DIVIDE BY ZERO
	ld	l,0ffh	; SUBTRACT THE TWO EXPONENTS, L IS A FLAG
	call	MULDIV	; FIX UP THE EXPONENTS AND THINGS
	inc	(hl)
	inc	(hl)
			; HERE WE SAVE THE FAC IN MEMORY SO WE CAN SUBTRACT IT FROM THE NUMBER
			; IN THE REGISTERS QUICKLY.
	dec	hl	; POINT TO HO
	ld	a,(hl)	; GET HO
	ld	(FDIVA+1),a	; SAVE IT
	dec	hl	; SAVE MIDDLE ORDER
	ld	a,(hl)
	ld	(FDIVB+1),a	; PUT IT WHERE NOTHING WILL HURT IT
	dec	hl	; SAVE LO
	ld	a,(hl)
	ld	(FDIVC+1),a

;THE NUMERATOR WILL BE KEPT IN B,H,L.  THE QUOTIENT WILL BE FORMED IN C,D,E.
;TO GET A BIT OF THE QUOTIENT, WE FIRST SAVE B,H,L ON THE STACK, THEN
;SUBTRACT THE DENOMINATOR THAT WE SAVED IN MEMORY.  THE CARRY INDICATES
;WHETHER OR NOT B,H,L WAS BIGGER THAN THE DENOMINATOR.  IF B,H,L WAS BIGGER,
;THE NEXT BIT OF THE QUOTIENT IS A ONE.  TO GET THE OLD B,H,L OFF THE STACK,
;WE POP THEM INTO THE PSW.  IF THE DENOMINATOR WAS BIGGER, THE NEXT BIT OF
;THE QUOTIENT IS ZERO, AND WE GET THE OLD B,H,L BACK BY POPPING IT OFF THE
;STACK.  WE HAVE TO KEEP AN EXTRA BIT OF THE QUOTIENT IN FDIVG+1 IN CASE THE
;DENOMINATOR WAS BIGGER,  THEN B,H,L WILL GET SHIFTED LEFT.  IF THE MSB  OF
;B WAS ONE, IT HAS TO BE STORED SOMEWHERE, SO WE STORE IT IN FDIVG+1.  THEN
;THE NEXT TIME THROUGH THE LOOP B,H,L WILL LOOK BIGGER BECAUSE IT HAS AN
;EXTRA HO BIT IN FDIVG+1. WE ARE DONE DIVIDING WHEN THE MSB OF C IS A ONE.
;THIS OCCURS WHEN WE HAVE CALCULATED 24 BITS OF THE QUOTIENT.  WHEN WE JUMP
;TO ROUND, THE 25TH BIT OF THE QUOTIENT DETERMINES WHETHER WE ROUND OR NOT.
;IT IS IN THE MSB OF A.  IF INITIALLY THE DENOMINATOR IS BIGGER THAN THE
;NUMERATOR, THE FIRST BIT OF THE QUOTIENT WILL BE ZERO.  THIS MEANS WE
;WILL GO THROUGH THE DIVIDE LOOP 26 TIMES, SINCE IT STOPS ON THE 25TH BIT
;AFTER THE FIRST NON-ZERO BIT OF THE QUOTIENT.  SO, THIS QUOTIENT WILL LOOK
;SHIFTED LEFT ONE FROM THE QUOTIENT OF TWO NUMBERS IN WHICH THE NUMERATOR IS
;BIGGER.  THIS CAN ONLY OCCUR ON THE FIRST TIME THROUGH THE LOOP, SO C,D,E
;ARE ALL ZERO.  SO, IF WE FINISH THE LOOP AND C,D,E ARE ALL ZERO, THEN WE
;MUST DECREMENT THE EXPONENT TO CORRECT FOR THIS.
	ld	b,c	; GET NUMBER IN B,H,L
	ex	de,hl
	xor	a	; ZERO C,D,E AND HIGHEST ORDER
	ld	c,a
	ld	d,a
	ld	e,a
	ld	(FDIVG+1),a
FDIV1:	push	hl	; SAVE LO'S OF NUMBER
	push	bc	; SAVE HO OF NUMBER
	ld	a,l	; SUBTRACT NUMBER THAT WAS IN FAC
FDIVC:	sub	0	; SUBTRACT LO
	ld	l,a	; SAVE IT
	ld	a,h	; SUBTRACT MIDDLE ORDER
FDIVB:	sbc	a,0
	ld	h,a
	ld	a,b	; SUBTRACT HO
FDIVA:	sbc	a,0
	ld	b,a
FDIVG:	ld	a,0	; GET HIGHEST ORDER
			; WE COULD DO THIS WITH NO CODE IN RAM, BUT
			;  IT WOULD BE MUCH SLOWER.
	sbc	a,0	; SUBTRACT THE CARRY FROM IT
	ccf		; SET CARRY TO CORESPOND TO NEXT QUOTIENT BIT
	jp	nc,FDIV2	; GET OLD NUMBER BACK IF WE SUBTRACTED TOO MUCH
	ld	(FDIVG+1),a	; UPDATE HIGHEST ORDER
	pop	af	; THE SUBTRACTION WAS GOOD
	pop	af	; GET PREVIOUS NUMBER OFF STACK
	scf		; NEXT BIT IN QUOTIENT IS A ONE
	defb	0d2h	; "JNC" AROUND NEXT 2 BYTES
FDIV2:	pop	bc	; WE SUBTRACTED TOO MUCH
	pop	hl	; GET OLD NUMBER BACK
	ld	a,c	; ARE WE DONE?
	inc	a	; SET SIGN FLAG WITHOUT AFFECTING CARRY
	dec	a
	rra		; PUT CARRY IN MSB
	jp	p,DIV2A	; NOT READY TO ROUND YET
	rla		; BIT BACK TO CARRY
	ld	a,(FDIVG+1)	; FETCH EXTRA BIT
	rra		; BOTH NOW IN A
	and	0c0h	; CLEAR SUPERFLUOUS BITS
	push	af	; SAVE FOR LATER
	ld	a,b	; FETCH HO OF REMAINDER
	or	h	; FETCH HO
	or	l	; SEE IF OTHER REMAINDER BITS
			; AND IF SO SET ST
	jp	z,DIV2AA	; IF NOT IGNORE
	ld	a,20h	; ST BIT
DIV2AA:	pop	hl	; AND THE REST OF REMAINDER
	or	h	; "OR" IN REST
	jp	ROUNDB	; USE REMAINDER
DIV2A:	
	rla		; WE AREN'T, GET OLD CARRY BACK
	ld	a,e	; ROTATE EVERYTHING LEFT ONE
	rla		; ROTATE NEXT BIT OF QUOTIENT IN
	ld	e,a
	ld	a,d
	rla
	ld	d,a
	ld	a,c
	rla
	ld	c,a
	add	hl,hl	; ROTATE A ZERO INTO RIGHT END OF NUMBER
	ld	a,b	; THE HO BYTE, FINALLY!
FDIV2A:	rla
	ld	b,a
FDIV2B:	ld	a,(FDIVG+1)	; ROTATE THE HIGHEST ORDER
	rla
	ld	(FDIVG+1),a
	ld	a,c	; ADD ONE TO EXPONENT IF THE FIRST SUBTRACTION
	or	d	;  DID NOT WORK
	or	e
	jp	nz,FDIV1	; THIS ISN'T THE CASE
	push	hl	; SAVE PART OF NUMBER
	ld	hl,FAC	; GET POINTER TO FAC
	dec	(hl)	; DECREMENT EXPONENT
	pop	hl	; GET NUMBER BACK
	jp	nz,FDIV1	; DIVIDE MORE IF NO OVERFLOW OCCURED
	jp	ZERO	; UNDERFLOW!!


			; CHECK SPECIAL CASES AND ADD EXPONENTS FOR FMULT, FDIV
			; ALTERS A,B,H,L
MULDVS:	ld	a,0ffh	; ENTRY FROM DDIV, SUBTRACT EXPONENTS
	defb	2eh	; "MVI L" AROUND NEXT BYTE
MULDVA:	xor	a	; ENTRY FROM DMULT, ADD EXPONENTS
	ld	hl,ARG-1	; GET POINTER TO SIGN AND HO OF ARG
	ld	c,(hl)	; GET HO AND SIGN FOR UNPACKING
	inc	hl	; INCREMENT POINTER TO EXPONENT
	xor	(hl)	; GET EXPONENT
	ld	b,a	; SAVE IT IN B FOR BELOW
	ld	l,0	; SET FLAG TO ADD THE EXPONENTS BELOW
MULDIV:	ld	a,b	; IS NUMBER IN REGISTERS ZERO?
	or	a
	jp	z,MULDV2	; IT IS, ZERO FAC AND WE ARE DONE
	ld	a,l	; GET ADD OR SUBTRACT FLAG
	ld	hl,FAC	; GET POINTER TO EXPONENT
	xor	(hl)	; GET EXPONENT
	add	a,b	; ADD IN REGISTER EXPONENT
	ld	b,a	; SAVE IT
	rra		; CHECK FOR OVERFLOW
	xor	b	; OVERFLOW IF SIGN IS THE SAME AS CARRY
	ld	a,b	; GET SUM
	jp	p,MULDV1	; WE HAVE OVERFLOW!!
	add	a,80h	; PUT EXPONENT IN EXCESS 200
	ld	(hl),a	; SAVE IT IN THE FAC
	jp	z,POPHRT	; WE HAVE UNDEFLOW!! RETURN.
	call	UNPACK	; UNPACK THE ARGUMENTS
	ld	(hl),a	; SAVE THE NEW SIGN
DCXHRT:	dec	hl	; POINT TO EXPONENT
	ret		; ALL DONE, LEAVE HO IN A
MLDVEX:	call	SIGN	; ENTRY FROM EXP, PICK UNDERFLOW IF NEGATIVE
	cpl		; PICK OVERFLOW IF POSITIVE
	pop	hl	; DON'T SCREW UP STACK
MULDV1:	or	a	; IS ERROR OVERFLOW OR UNDEFLOW?
MULDV2:	pop	hl	; GET OLD RETURN ADDRESS OFF STACK



	jp	p,ZERO
	jp	OVFIN2


			; MULTIPLY FAC BY 10
			; ALTERS A,B,C,D,E,H,L
MUL10:	call	MOVRF	; GET NUMBER IN REGISTERS
	ld	a,b	; GET EXPONENT
	or	a	; RESULT IS ZERO IF ARG IS ZERO
	ret	z		; IT IS
	add	a,2	; MULTIPLY BY 4 BY ADDING 2 TO EXPONENT
	jp	c,OVFIN3
	ld	b,a	; RESTORE EXPONENT
	call	FADD	; ADD IN ORIGINAL NUMBER TO GET 5 TIMES IT
	ld	hl,FAC	; ADD 1 TO EXPONENT TO MULTIPLY NUMBER BY
	inc	(hl)	;  2 TO GET 10 TIMES ORIGINAL NUMBER
	ret	nz		; ALL DONE IF NO OVERFLOW
	jp	OVFIN3
	PAGE
	SUBTTL	SIGN, SGN, FLOAT, NEG AND ABS
			; PUT SIGN OF FAC IN A
			; ALTERS A ONLY
			; LEAVES FAC ALONE
			; NOTE: TO TAKE ADVANTAGE OF THE RST INSTRUCTIONS TO SAVE BYTES,
			; FSIGN IS DEFINED TO BE AN RST. "FSIGN" IS EQUIVALENT TO "CALL SIGN"
			; THE FIRST FEW INSTRUCTIONS OF SIGN (THE ONES BEFORE SIGNC) ARE DONE
			; IN THE 8 BYTES AT THE RST LOCATION.

			; INTEL FLOATING SOFTWARE FLAG

			; FSIGN IS USUALLY AN RST
SIGN:	ld	a,(FAC)	; CHECK IF THE NUMBER IS ZERO
	or	a
	ret	z		; IT IS, A IS ZERO
SIGNC:	ld	a,(FAC-1)	; GET SIGN OF FAC, IT IS NON-ZERO
	defb	0feh	; "CPI" AROUND NEXT BYTE
FCOMPS:	cpl		; ENTRY FROM FCOMP, COMPLEMENT SIGN
ICOMPS:	rla		; ENTRY FROM ICOMP, PUT SIGN BIT IN CARRY
SIGNS:	sbc	a,a	; A=0 IF CARRY WAS 0, A=377 IF CARRY WAS 1
	ret	nz		; RETURN IF NUMBER WAS NEGATIVE
INRART:	inc	a	; PUT ONE IN A IF NUMBER WAS POSITIVE
	ret		; ALL DONE


			; SGN FUNCTION
			; ALTERS A,B,C,D,E,H,L
			; FALL INTO FLOAT


			; FLOAT THE SIGNED INTEGER IN A
			; ALTERS A,B,C,D,E,H,L

			; USE MICROSOFT FORMAT IF NOT INTEL
FLOAT:	ld	b,88h	; SET EXPONENT CORRECTLY
	ld	de,0	; ZERO D,E
			; FALL INTO FLOATR


			; FLOAT THE SIGNED NUMBER IN B,A,D,E
			; ALTERS A,B,C,D,E,H,L
FLOATR:	ld	hl,FAC	; GET POINTER TO FAC
	ld	c,a	; PUT HO IN C
	ld	(hl),b	; PUT EXPONENT IN THE FAC
	ld	b,0	; ZERO OVERFLOW BYTE
	inc	hl	; POINT TO SIGN
	ld	(hl),80h	; ASSUME A POSITIVE NUMBER
	rla		; PUT SIGN IN CARRY
	jp	FADFLT	; GO AND FLOAT THE NUMBER


			; FALL INTO NEG


;
;	;GET THE VALTYP AND SET CONDITION CODES AS FOLLOWS:
;;CONDITION CODE		TRUE SET	FALSE SET
;;SIGN			INT=2		STR,SNG,DBL
;;ZERO			STR=3		INT,SNG,DBL
;;ODD PARITY		SNG=4		INT,STR,DBL
;;NO CARRY		DBL=10		INT,STR,SNG
;GETYPE:	LDA	VALTYP		;GET THE VALTYP
;	CPI	10		;SET CARRY CORRECTLY
;	DCR	A		;SET THE OTHER CONDITION CODES CORRECTLY
;	DCR	A		; WITHOUT AFFECTING CARRY
;	DCR	A
;	RET	*			;ALL DONE


			; ABSOLUTE VALUE OF FAC
			; ALTERS A,B,C,D,E,H,L
ABS:	call	VSIGN	; GET THE SIGN OF THE FAC IN A
	ret	p		; IF IT IS POSITIVE, WE ARE DONE


			; NEGATE ANY TYPE VALUE IN THE FAC
			; ALTERS A,B,C,D,E,H,L
VNEG:	call	GETYPR	; SEE WHAT KIND OF NUMBER WE HAVE
	jp	m,INEG	; WE HAVE AN INTEGER, NEGATE IT THAT WAY
	jp	z,TMERR	; BLOW UP ON STRINGS
			; FALL INTO NEG TO NEGATE A SNG OR DBL


			; NEGATE NUMBER IN THE FAC
			; ALTERS A,H,L
			; NOTE: THE NUMBER MUST BE PACKED

			; IF INTFSW=0 DO NOT USE INTEL FORMAT
NEG:	ld	hl,FAC-1	; GET POINTER TO SIGN
	ld	a,(hl)	; GET SIGN
	xor	80h	; COMPLEMENT SIGN BIT
	ld	(hl),a	; SAVE IT
	ret		; ALL DONE


			; SGN FUNCTION
			; ALTERS A,H,L
SGN:	call	VSIGN	; GET THE SIGN OF THE FAC IN A
			; ENTRY TO CONVERT A SIGNED NUMBER IN A TO AN INTEGER
CONIA:	ld	l,a	; PUT IT IN THE LO POSITION
	rla		; EXTEND THE SIGN TO THE HO
	sbc	a,a
	ld	h,a
	jp	MAKINT	; RETURN THE RESULT AND SET VALTYP


			; GET THE SIGN OF THE VALUE IN THE FAC IN A
			; ALTERS A,H,L
VSIGN:	call	GETYPR	; SEE WHAT KIND OF A NUMBER WE HAVE
	jp	z,TMERR	; BLOW UP ON STRINGS
	jp	p,SIGN	; SINGLE AND DOUBLE PREC. WORK THE SAME
	ld	hl,(FACLO)	; GET THE INTEGER ARGUMENT

			; ENTRY TO FIND THE SIGN OF (HL)
			; ALTERS A ONLY
ISIGN:	ld	a,h	; GET ITS SIGN
	or	l	; CHECK IF THE NUMBER IS ZERO
	ret	z		; IT IS, WE ARE DONE
	ld	a,h	; IT ISN'T, SIGN IS THE SIGN OF H
	jp	ICOMPS	; GO SET A CORRECTLY
	PAGE
	SUBTTL	FLOATING POINT MOVEMENT ROUTINES
			; PUT FAC ON STACK
			; ALTERS D,E
PUSHF:	ex	de,hl		; SAVE (HL)
	ld	hl,(FACLO)	; GET LO'S
	ex	(sp),hl		; SWITCH LO'S AND RET ADDR
	push	hl	; PUT RET ADDR BACK ON STACK
	ld	hl,(FAC-1)	; GET HO'S
	ex	(sp),hl		; SWITCH HO'S AND RET ADDR
	push	hl	; PUT RET ADDR BACK ON STACK
	ex	de,hl		; GET OLD (HL) BACK
	ret		; ALL DONE


			; MOVE NUMBER FROM MEMORY [(HL)] TO FAC
			; ALTERS B,C,D,E,H,L
			; AT EXIT NUMBER IS IN B,C,D,E
			; AT EXIT (HL):=(HL)+4
MOVFM:	call	MOVRM	; GET NUMBER IN REGISTERS
			; FALL INTO MOVFR AND PUT IT IN FAC


			; MOVE REGISTERS (B,C,D,E) TO FAC
			; ALTERS D,E
MOVFR:	ex	de,hl		; GET LO'S IN (HL)
	ld	(FACLO),hl	; PUT THEM WHERE THEY BELONG
	ld	h,b	; GET HO'S IN (HL)
	ld	l,c
	ld	(FAC-1),hl	; PUT HO'S WHERE THEY BELONG
	ex	de,hl		; GET OLD (HL) BACK
	ret		; ALL DONE


			; MOVE FAC TO REGISTERS (B,C,D,E)
			; ALTERS B,C,D,E,H,L
MOVRF:	ld	hl,FACLO	; GET POINTER TO FAC
			; FALL INTO MOVRM


			; GET NUMBER IN REGISTERS (B,C,D,E) FROM MEMORY [(HL)]
			; ALTERS B,C,D,E,H,L
			; AT EXIT (HL):=(HL)+4
MOVRM:	ld	e,(hl)	; GET LO
	inc	hl	; POINT TO MO
GETBCD:	ld	d,(hl)	; GET MO, ENTRY FOR BILL
	inc	hl	; POINT TO HO
	ld	c,(hl)	; GET HO
	inc	hl	; POINT TO EXPONENT
	ld	b,(hl)	; GET EXPONENT
INXHRT:	inc	hl	; INC POINTER TO BEGINNING OF NEXT NUMBER
	ret		; ALL DONE


			; MOVE NUMBER FROM FAC TO MEMORY [(HL)]
			; ALTERS A,B,D,E,H,L
MOVMF:	ld	de,FACLO	; GET POINTER TO FAC
			; FALL INTO MOVE


			; MOVE NUMBER FROM (DE) TO (HL)
			; ALTERS A,B,D,E,H,L
			; EXITS WITH (DE):=(DE)+4, (HL):=(HL)+4
MOVE:	ld	b,4	; SET COUNTER
	jp	MOVE1	; CONTINUE WITH THE MOVE


			; MOVE ANY TYPE VALUE (AS INDICATED BY VALTYP) FROM (DE) TO (HL)
			; ALTERS A,B,D,E,H,L
MOVVFM:	ex	de,hl		; ENTRY TO SWITCH (DE) AND (HL)
VMOVE:	ld	a,(VALTYP)	; GET THE LENGTH OF THE NUMBER
	ld	b,a	; SAVE IT AWAY
	PUBLIC	MOVE1
MOVE1:	ld	a,(de)	; GET WORD, ENTRY FROM VMOVMF
	ld	(hl),a	; PUT IT WHERE IT BELONGS
	inc	de	; INCREMENT POINTERS TO NEXT WORD
	inc	hl
	dec	b
	jp	nz,MOVE1
	ret


			; UNPACK THE FAC AND THE REGISTERS
			; ALTERS A,C,H,L
			; WHEN THE NUMBER IN THE FAC IS UNACKED, THE ASSUMED ONE IN THE
			; MANTISSA IS RESTORED, AND THE COMPLEMENT OF THE SIGN IS PLACED
			; IN FAC+1

			; INTEL FLOATING SOFTWARE FLAG
UNPACK:	ld	hl,FAC-1	; POINT TO HO AND SIGN
	ld	a,(hl)	; GET HO AND SIGN
	rlca		; DUPLICATE THE SIGN IN CARRY AND THE LSB
	scf		; RESTORE THE HIDDEN ONE
	rra		; RESTORE THE NUMBER IN A
	ld	(hl),a	; SAVE HO
	ccf		; GET THE COMPLEMENT OF THE SIGN
	rra		; GET IT IN THE SIGN BIT
	inc	hl	; POINT TO TEMPORARY SIGN BYTE
	inc	hl
	ld	(hl),a	; SAVE COMPLEMENT OF SIGN
	ld	a,c	; GET HO AND SIGN OF THE REGISTERS
	rlca		; DUPLICATE THE SIGN IN CARRY AND THE LSB
	scf		; RESTORE THE HIDDEN ONE
	rra		; RESTORE THE HO IN A
	ld	c,a	; SAVE THE HO
	rra		; GET THE SIGN BACK
	xor	(hl)	; COMPARE SIGN OF FAC AND SIGN OF REGISTERS
	ret		; ALL DONE


			; MOVE ANY TYPE VALUE FROM MEMORY [(HL)] TO FAC
			; ALTERS A,B,D,E,H,L
VMOVFA:	ld	hl,ARGLO	; ENTRY FROM DADD, MOVE ARG TO FAC
VMOVFM:	ld	de,MOVVFM	; GET ADDRESS OF LOCATION THAT DOES
	jp	VMVVFM	;  AN "XCHG" AND FALLS INTO MOVE1


			; MOVE ANY TYPE VALUE FROM FAC TO MEMORY [(HL)]
			; ALTERS A,B,D,E,H,L
VMOVAF:	ld	hl,ARGLO	; ENTRY FROM FIN, DMUL10, DDIV10
			; MOVE FAC TO ARG
VMOVMF:	ld	de,VMOVE	; GET ADDRESS OF MOVE SUBROUTINE
VMVVFM:	push	de	; SHOVE IT ON THE STACK
VDFACS:	ld	de,FACLO	; GET FIRST ADDRESS FOR INT, STR, SNG
	call	GETYPR	; GET THE VALUE TYPE
	ret	c		; GO MOVE IT IF WE DO NOT HAVE A DBL
	ld	de,DFACLO	; WE DO, GET LO ADDR OF THE DBL NUMBER
	ret		; GO DO THE MOVE
	PAGE
	SUBTTL	COMPARE TWO NUMBERS
			; COMPARE TWO SINGLE PRECISION NUMBERS
			; A=1 IF ARG .LT. FAC
			; A=0 IF ARG=FAC
			; A=-1 IF ARG .GT. FAC
			; DOREL DEPENDS UPON THE FACT THAT FCOMP RETURNS WITH CARRY ON
			;  IFF A HAS 377
			; ALTERS A,H,L
FCOMP:	ld	a,b	; CHECK IF ARG IS ZERO
	or	a
	jp	z,SIGN
	ld	hl,FCOMPS	; WE JUMP TO FCOMPS WHEN WE ARE DONE
	push	hl	; PUT THE ADDRESS ON THE STACK
	call	SIGN	; CHECK IF FAC IS ZERO
	ld	a,c	; IF IT IS, RESULT IS MINUS THE SIGN OF ARG
	ret	z		; IT IS
	ld	hl,FAC-1	; POINT TO SIGN OF FAC
	xor	(hl)	; SEE IF THE SIGNS ARE THE SAME
	ld	a,c	; IF THEY ARE DIFFERENT, RESULT IS SIGN OF ARG
	ret	m		; THEY ARE DIFFERENT
	call	FCOMP2	; CHECK THE REST OF THE NUMBER
FCOMPD:	rra		; NUMBERS ARE DIFFERENT, CHANGE SIGN IF
	xor	c	;  BOTH NUMBERS ARE NEGATIVE
	ret		; GO SET UP A

FCOMP2:	inc	hl	; POINT TO EXPONENT
	ld	a,b	; GET EXPONENT OF ARG
	cp	(hl)	; COMPARE THE TWO
	ret	nz		; NUMBERS ARE DIFFERENT
	dec	hl	; POINT TO HO
	ld	a,c	; GET HO OF ARG
	cp	(hl)	; COMPARE WITH HO OF FAC
	ret	nz		; THEY ARE DIFFERENT
	dec	hl	; POINT TO MO OF FAC
	ld	a,d	; GET MO OF ARG
	cp	(hl)	; COMPARE WITH MO OF FAC
	ret	nz		; THE NUMBERS ARE DIFFERENT
	dec	hl	; POINT TO LO OF FAC
	ld	a,e	; GET LO OF ARG
	sub	(hl)	; SUBTRACT LO OF FAC
	ret	nz		; NUMBERS ARE DIFFERENT
	pop	hl	; NUMBERS ARE THE SAME, DON'T SCREW UP STACK
	pop	hl
	ret		; ALL DONE


			; COMPARE TWO INTEGERS
			; A=1 IF (DE) .LT. (HL)
			; A=0 IF (DE)=(HL)
			; A=-1 IF (DE) .GT. (HL)
			; ALTERS A ONLY
ICOMP:	ld	a,d	; ARE THE SIGNS THE SAME?
	xor	h
	ld	a,h	; IF NOT, ANSWER IS THE SIGN OF (HL)
	jp	m,ICOMPS	; THEY ARE DIFFERENT
	cp	d	; THEY ARE THE SAME, COMPARE THE HO'S
	jp	nz,SIGNS	; GO SET UP A
	ld	a,l	; COMPARE THE LO'S
	sub	e
	jp	nz,SIGNS	; GO SET UP A
	ret		; ALL DONE, THEY ARE THE SAME


			; COMPARE TWO DOUBLE PRECISION NUMBERS
			; A=1 IF ARG .LT. FAC
			; A=0 IF ARG=FAC
			; A=-1 IF ARG .GT. FAC
			; ALTERS A,B,C,D,E,H,L
DCOMPD:	ld	hl,ARGLO	; ENTRY WITH POINTER TO ARG IN (DE)
	call	VMOVE	; MOVE THE ARGUMENT INTO ARG
	PUBLIC	XDCOMP
XDCOMP:	ld	de,ARG	; GET POINTER TO ARG
	ld	a,(de)	; SEE IF ARG=0
	or	a
	jp	z,SIGN	; ARG=0, GO SET UP A
	ld	hl,FCOMPS	; PUSH FCOMPS ON STACK SO WE WILL RETURN TO
	push	hl	;  TO IT AND SET UP A
	call	SIGN	; SEE IF FAC=0
	dec	de	; POINT TO SIGN OF ARGUMENT
	ld	a,(de)	; GET SIGN OF ARG
	ld	c,a	; SAVE IT FOR LATER
	ret	z		; FAC=0, SIGN OF RESULT IS SIGN OF ARG
	ld	hl,FAC-1	; POINT TO SIGN OF FAC
	xor	(hl)	; SEE IF THE SIGNS ARE THE SAME
	ld	a,c	; IF THEY ARE, GET THE SIGN OF THE NUMBERS
	ret	m		; THE SIGNS ARE DIFFERENT, GO SET A
	inc	de	; POINT BACK TO EXPONENT OF ARG
	inc	hl	; POINT TO EXPONENT OF FAC
	ld	b,08h	; SET UP A COUNT
DCOMP1:	ld	a,(de)	; GET A BYTE FROM ARG
	sub	(hl)	; COMPARE IT WITH THE FAC
	jp	nz,FCOMPD	; THEY ARE DIFFERENT, GO SET UP A
	dec	de	; THEY ARE THE SAME, EXAMINE THE NEXT LOWER
	dec	hl	;  ORDER BYTES
	dec	b	; ARE WE DONE?
	jp	nz,DCOMP1	; NO, COMPARE THE NEXT BYTES
	pop	bc	; THEY ARE THE SAME, GET FCOMPS OFF STACK
	ret		; ALL DONE


			; COMPARE TWO DOUBLE PRECISION NUMBERS
			; A=1 IF ARG .GT. FAC
			; A=0 IF ARG=FAC
			; A=-1 IF ARG .LT. FAC
			; NOTE: THIS IS THE REVERSE OF ICOMP, FCOMP AND XDCOMP
			; ALTERS A,B,C,D,E,H,L
DCOMP:	call	XDCOMP	; COMPARE THE TWO NUMBERS
	jp	nz,FCOMPS	; NEGATE THE ANSWER, MAKE SURE THE CARRY COMES
	ret		;  OUT CORRECT FOR DOCMP
	PAGE
	SUBTTL	CONVERSION ROUTINES BETWEEN INTEGER, SINGLE AND DOUBLE PRECISION
			; FORCE THE FAC TO BE AN INTEGER
			; ALTERS A,B,C,D,E,H,L
FRCINT:	call	GETYPR	; SEE WHAT WE HAVE
	ld	hl,(FACLO)	; GET FACLO+0,1 IN CASE WE HAVE AN INTEGER
	ret	m		; WE HAVE AN INTEGER, ALL DONE
	jp	z,TMERR	; WE HAVE A STRING, THAT IS A "NO-NO"
	jp	po,FRCIN2	; GO DO S.P.
FDBINT:	call	VMOVAF	; ADD D.P. .5
	ld	hl,DHALF
	call	VMOVFM
	call	DADD
FRDBIN:	call	CONSD
	jp	FRCIN3
FRCIN2:	call	FADDH
FRCIN3:	ld	a,(FAC-1)	; GET SIGN BYTE
	or	a	; SET CONDITION CODES CORRECTLY
	push	af
	and	7fh	; CLEAR SIGN
	ld	(FAC-1),a	; MAKE FAC POSITIVE
	ld	a,(FAC)	; GET EXPONENT
	cp	90h	; SEE IF TOO LARGE
	jp	nc,OVERR
	call	QINT	; CONVERT TO INTEGER
	ld	a,(FAC)
	or	a
	jp	nz,FRCIAA
	pop	af
	ex	de,hl
	jp	FRCI3A
FRCIAA:	
	pop	af
	ex	de,hl		; MOVE INTEGER TO (HL)
	jp	p,FRCIN4
FRCI3A:	ld	a,h
	cpl
	ld	h,a	; COMPLEMENT (HL)
	ld	a,l
	cpl
	ld	l,a
FRCIN4:	jp	MAKINT
	ld	hl,OVERR	; PUT OVERR ON THE STACK SO WE WILL GET ERROR
	push	hl	;  IF NUMBER IS TOO BIG
			; FALL INTO CONIS


			; CONVERT SINGLE PRECISION NUMBER TO INTEGER
			; ALTERS A,B,C,D,E,H,L
	PUBLIC	CONIS
CONIS:	ld	a,(FAC)	; GET THE EXPONENT
	cp	90h	; SEE IF IT IS TOO BIG
	jp	nc,CONIS2	; IT IS, BUT IT MIGHT BE -32768
	call	QINT	; IT ISN'T, CONVERT IT TO AN INTEGER
	ex	de,hl		; PUT IT IN (HL)
			; ENTRY FROM IADD
CONIS1:	pop	de	; GET ERROR ADDRESS OFF STACK

			; PUT (HL) IN FACLO, SET VALTYP TO INT
			; ALTERS A ONLY
MAKINT:	ld	(FACLO),hl	; STORE THE NUMBER IN FACLO
VALINT:	ld	a,2	; SET VALTYP TO "INTEGER"
CONISD:	ld	(VALTYP),a	; ENTRY FROM CONDS
	ret		; ALL DONE

CONIS2:	ld	bc,0090h*256+0080h
	ld	de,0000h*256+0000h	; CHECK IF NUMBER IS -32768, ENTRY FROM FIN
	call	FCOMP
	ret	nz		; ERROR: IT CAN'T BE CONVERTED TO AN INTEGER
	ld	h,c	; IT IS -32768, PUT IT IN (HL)

	ld	l,d
	jp	CONIS1	; STORE IT IN THE FAC AND SET VALTYP


			; FORCE THE FAC TO BE A SINGLE PRECISION NUMBER
			; ALTERS A,B,C,D,E,H,L
FRCSNG:	call	GETYPR	; SEE WHAT KIND OF NUMBER WE HAVE
	ret	po		; WE ALREADY HAVE A SNG, ALL DONE
	jp	m,CONSI	; WE HAVE AN INTEGER, CONVERT IT
	jp	z,TMERR	; STRINGS!! -- ERROR!!
			; DBL PREC -- FALL INTO CONSD


			; CONVERT DOUBLE PRECISION NUMBER TO A SINGLE PRECISON ONE
			; ALTERS A,B,C,D,E,H,L
	PUBLIC	CONSD
CONSD:	call	MOVRF	; GET THE HO'S IN THE REGISTERS
	call	VALSNG	; SET VALTYP TO "SINGLE PRECISON"
	ld	a,b	; CHECK IF THE NUMBER IS ZERO
	or	a
	ret	z		; IF IT IS, WE ARE DONE
	call	UNPACK	; UNPACK THE NUMBER
	ld	hl,FACLO-1	; GET FIRST BYTE BELOW A SNG NUMBER
	ld	b,(hl)	; PUT IT IN B FOR ROUND
	jp	ROUND	; ROUND THE DBL NUMBER UP AND WE ARE DONE


			; CONVERT AN INTEGER TO A SINGLE PRECISION NUMBER
			; ALTERS A,B,C,D,E,H,L
	PUBLIC	CONSI
CONSI:	ld	hl,(FACLO)	; GET THE INTEGER
CONSIH:	call	VALSNG	; SET VALTYP TO "SINGLE PRECISION"
	ld	a,h	; SET UP REGISTERS FOR FLOATR
	ld	d,l
	ld	e,0
	ld	b,90h
	jp	FLOATR	; GO FLOAT THE NUMBER


			; FORCE THE FAC TO BE A DOUBLE PRECISION NUMBER
			; ALTERS A,B,C,D,E,H,L
FRCDBL:	call	GETYPR	; SEE WHAT KIND OF NUMBER WE HAVE
	ret	nc		; WE ALREADY HAVE A DBL, WE ARE DONE
	jp	z,TMERR	; GIVE AN ERROR IF WE HAVE A STRING
	call	m,CONSI	; CONVERT TO SNG IF WE HAVE AN INT
			; FALL INTO CONDS AND CONVERT TO DBL


			; CONVERT A SINGLE PRECISION NUMBER TO A DOUBLE PRECISION ONE
			; ALTERS A,H,L
	PUBLIC	CONDS
CONDS:	ld	hl,0	; ZERO H,L
	ld	(DFACLO),hl	; CLEAR THE FOUR LOWER BYTES IN THE DOUBLE
	ld	(DFACLO+2),hl	;  PRECISION NUMBER
VALDBL:	ld	a,08h	; SET VALTYP TO "DOUBLE PRECISION"
	defb	001	; "LXI B" OVER THE NEXT 2 BYTES
VALSNG:	ld	a,4	; SET VALTYP TO "SINGLE PRECISION"
	jp	CONISD	; GO TO IT


			; FORCE THE FAC TO BE A STRING
			; ALTERS A ONLY
CHKSTR:	
FRCSTR:	call	GETYPR	; SEE WHAT KIND OF VALUE WE HAVE
	ret	z		; WE HAVE A STRING, EVERYTHING IS OK
	jp	TMERR	; WE DON'T HAVE A STRING, FALL INTO TMERR

	PAGE
	SUBTTL	GREATEST INTEGER FUNCTION
			; QUICK GREATEST INTEGER FUNCTION
			; LEAVES INT(FAC) IN C,D,E (SIGNED)
			; ASSUMES FAC .LT. 2^23 = 8388608
			; ASSUMES THE EXPONENT OF FAC IS IN A
			; ALTERS A,B,C,D,E
QINT:	ld	b,a	; ZERO B,C,D,E IN CASE THE NUMBER IS ZERO
	ld	c,a
	ld	d,a
	ld	e,a
	or	a	; SET CONDITION CODES
	ret	z		; IT IS ZERO, WE ARE DONE

;THE HARD CASE IN QINT IS NEGATIVE NON-INTEGERS.  TO HANDLE THIS, IF THE
;NUMBER IS NEGATIVE, WE REGARD THE 3-BYTE MANTISSA AS A 3-BYTE INTEGER AND
;SUBTARCT ONE.  THEN ALL THE FRACTIONAL BITS ARE SHIFTED OUT BY SHIFTING THE
;MANTISSA RIGHT.  THEN, IF THE NUMBER WAS NEGATIVE, WE ADD ONE.  SO, IF WE
;HAD A NEGATIVE INTEGER, ALL THE BITS TO THE RIGHT OF THE BINARY POINT WERE
;ZERO.  SO THE NET EFFECT IS WE HAVE THE ORIGINAL NUMBER IN C,D,E.  IF THE
;NUMBER WAS A NEGATIVE NON-INTEGER, THERE IS AT LEAST ONE NON-ZERO BIT TO THE
;RIGHT OF THE BINARY POINT.  SO THE NET EFFECT IS THAT WE GET THE ABSOLUTE
;VALUE OF INT(FAC) IN C,D,E.  C,D,E IS THEN NEGATED IF THE ORIGINAL NUMBER WAS
;NEGATIVE SO THE RESULT WILL BE SIGNED.
	push	hl	; SAVE (HL)
	call	MOVRF	; GET NUMBER IN THE REGISTERS
	call	UNPACK	; UNPACK THE NUMBER
	xor	(hl)	; GET SIGN OF NUMBER
	ld	h,a	; DON'T LOSE IT
	call	m,QINTA	; SUBTRACT 1 FROM LO IF NUMBER IS NEGATIVE
	ld	a,98h	; SEE HOW MANY WE HAVE TO SHIFT TO CHANGE
	sub	b	;  NUMBER TO AN INTEGER
	call	SHIFTR	; SHIFT NUMBER TO GET RID OF FRACTIONAL BITS
	ld	a,h	; GET SIGN
	rla		; PUT SIGN IN CARRY SO IT WILL NOT BE CHANGED
	call	c,ROUNDA	; IF NUMBER WAS NEGATIVE, ADD ONE
	ld	b,0	; FORGET THE BITS WE SHIFTED OUT
	call	c,NEGR	; NEGATE NUMBER IF IT WAS NEGATIVE BECAUSE WE
			;  WANT A SIGNED MANTISSA
	pop	hl	; GET OLD (HL) BACK
	ret		; ALL DONE

QINTA:	dec	de	; SUBTRACT ONE FROM C,D,E
	ld	a,d	; WE HAVE TO SUBTRACT ONE FROM C IF
	and	e	;  D AND E ARE BOTH ALL ONES
	inc	a	; SEE IF BOTH WERE -1
	ret	nz		; THEY WERE NOT, WE ARE DONE
	PUBLIC	DCXBRT
DCXBRT:	dec	bc	; THIS IS FOR BILL. C WILL NEVER BE ZERO
			;  (THE MSB WILL ALWAYS BE ONE) SO "DCX B"
			;  AND "DCR C" ARE FUNCTIONALLY EQUIVALENT
	ret		; ALL DONE
			;  THIS IS THE FIX (X) FUNCTION. IT RETURNS
			;  FIX(X)=SGN(X)*INT(ABS(X))
	PUBLIC	FIXER
FIXER:	call	GETYPR	; GET VALTYPE OF ARG
	ret	m		; INT, DONE
	call	SIGN	; GET SIGN
	jp	p,VINT	; IF POSITIVE, JUST CALL REGULAR INT CODE
	call	NEG	; NEGATE IT
	call	VINT	; GET THE INTEGER OF IT
	jp	VNEG	; NOW RE-NEGATE IT

			; GREATEST INTEGER FUNCTION
			; ALTERS A,B,C,D,E,H,L
VINT:	call	GETYPR	; SEE WHAT TYPE OF A NUMBER WE HAVE
	ret	m		; IT IS AN INTEGER, ALL DONE
	jp	nc,DINT	; CONVERT THE DOUBLE PRECISION NUMBER
	jp	z,TMERR	; BLOW UP ON STRINGS
	call	CONIS	; TRY TO CONVERT THE NUMBER TO AN INTEGER
			; IF WE CAN'T, WE WILL RETURN HERE TO GIVE A
			;  SINGLE PRECISION RESULT
INT:	ld	hl,FAC	; GET EXPONENT
	ld	a,(hl)
	cp	98h	; SEE IF NUMBER HAS ANY FRACTIONAL BITS

			; THE ONLY GUY WHO NEEDS THIS DOESN'T CARE
	ld	a,(FACLO)	;  ABOUT THE SIGN
	ret	nc		; IT DOES NOT
	ld	a,(hl)	; GET EXPONENT BACK
	call	QINT	; IT DOES, SHIFT THEM OUT
	ld	(hl),98h	; CHANGE EXPONENT SO IT WILL BE CORRECT
			; NOTE:QINT UNPACKED THE NUMBER!!!!
			;  AFTER NORMALIZATION
	ld	a,e	; GET LO
	push	af	; SAVE IT
	ld	a,c	; NEGATE NUMBER IF IT IS NEGATIVE
	rla		; PUT SIGN IN CARRY
	call	FADFLT	; REFLOAT NUMBER
	pop	af	; GET LO BACK
	ret		; ALL DONE


			; GREATEST INTEGER FUNCTION FOR DOUBLE PRECISION NUMBERS
			; ALTERS A,B,C,D,E,H,L
DINT:	ld	hl,FAC	; GET POINTER TO FAC
	ld	a,(hl)	; GET EXPONENT
	cp	90h	; CAN WE CONVERT IT TO AN INTEGER?
DIN00:	jp	nz,DINT2	; CHECK FOR -32768
	ld	c,a	; SAVE EXPONENT IN C
	dec	hl	; GET POINTER TO SIGN AND HO
	ld	a,(hl)	; GET SIGN AND HO
	xor	80h	; CHECK IF IT IS 200
	ld	b,6	; SET UP A COUNT TO CHECK IF THE REST OF
DINT1:	dec	hl	;  THE NUMBER IS ZERO, POINT TO NEXT BYTE
	or	(hl)	; IF ANY BITS ARE NON-ZERO, A WILL BE NON-ZERO
	dec	b	; ARE WE DONE?
	jp	nz,DINT1	; NO, CHECK THE NEXT LOWER ORDER BYTE
	or	a	; IS A NOW ZERO?
	ld	hl,80h*100h+0	; GET -32768 JUST IN CASE
	jp	nz,DIN05
	call	MAKINT	; A IS ZERO SO WE HAVE -32768
	jp	FRCDBL	; FORCE BACK TO DOUBLE
DIN05:	ld	a,c	; GET EXPONENT
	PUBLIC	DINT2
DINT2:	or	a	; CHECK FOR ZERO VALUE
	ret	z		; ***FIX 5.11***^1 -- ALALOW 0 IN DINT
	cp	0b8h	; ARE THERE ANY FRACTIONAL BITS?
	ret	nc		; NO, THE NUMBER IS ALREADY AN INTEGER

	PUBLIC	DINTFO
DINTFO:	push	af	; ENTRY FROM FOUT, CARRY IS ZERO IF WE COME
			;  HERE FROM FOUT
	call	MOVRF	; GET HO'S OF NUMBER IN REGISTERS FOR UNPACKING
	call	UNPACK	; UNPACK IT
	xor	(hl)	; GET ITS SIGN BACK
	dec	hl	; SET THE EXPONENT TO NORMALIZE CORRECTLY
	ld	(hl),0b8h
	push	af	; SAVE THE SIGN
	dec	hl
	ld	(hl),c	; GET UNPACKED HIGH BYTE
	call	m,DINTA	; SUBTRACT 1 FROM LO IF NUMBER IS NEGATIVE
	ld	a,(FAC-1)	; FETCH NEW HIGH MANTISSA BYTE
	ld	c,a	; AND PUT IN C
	ld	hl,FAC-1	; POINT TO THE HO OF THE FAC
	ld	a,0b8h	; GET HOW MANY BITS WE HAVE TO SHIFT OUT
	sub	b
	call	DSHFTR	; SHIFT THEM OUT!!
	pop	af	; GET THE SIGN BACK
	call	m,DROUNA	; IF NUMBER WAS NEGATIVE, ADD ONE
	xor	a	; PUT A ZERO IN THE EXTRA LO BYTE SO WHEN
	ld	(DFACLO-1),a	;  WE NORMALIZE, WE WILL SHIFT IN ZEROS
	pop	af	; IF WE WERE CALLED FROM FOUT, DON'T NORMALIZE,
	ret	nc		;  JUST RETURN
	jp	DNORML	; RE-FLOAT THE INTEGER

DINTA:	ld	hl,DFACLO	; SUBTRACT ONE FROM FAC, GET POINTER TO LO
DINTA1:	ld	a,(hl)	; GET A BYTE OF FAC
	dec	(hl)	; SUBTRACT ONE FROM IT
	or	a	; CONTINUE ONLY IF THE BYTE USED TO BE ZERO
	inc	hl	; INCREMENT POINTER TO NEXT BYTE
	jp	z,DINTA1	; CONTINUE IF NECESSARY
	ret		; ALL DONE
	PAGE
	SUBTTL	INTEGER ARITHMETIC ROUTINES
			; INTEGER MULTIPLY FOR MULTIPLY DIMENSIONED ARRAYS
			;  (DE):=(BC)*(DE)
			; OVERFLOW CAUSES A BS ERROR
			; ALTERS A,B,C,D,E
UMULT:	push	hl	; SAVE [H,L]
	ld	hl,0	; ZERO PRODUCT REGISTERS
	ld	a,b	; CHECK IF (BC) IS ZERO
	or	c	; IF SO, JUST RETURN, (HL) IS ALREADY ZERO
	jp	z,MULRET	; THIS IS DONE FOR SPEED
	ld	a,10h	; SET UP A COUNT
UMULT1:	add	hl,hl	; ROTATE (HL) LEFT ONE
	jp	c,BSERR	; CHECK FOR OVERFLOW, IF SO,
	ex	de,hl		;  BAD SUBSCRIPT (BS) ERROR
	add	hl,hl	; ROTATE (DE) LEFT ONE
	ex	de,hl
	jp	nc,UMULT2	; ADD IN (BC) IF HO WAS 1
	add	hl,bc
	jp	c,BSERR	; CHECK FOR OVERFLOW
UMULT2:	dec	a	; SEE IF DONE
	jp	nz,UMULT1
MULRET:	ex	de,hl		; RETURN THE RESULT IN [D,E]
	pop	hl	; GET BACK THE SAVED [H,L]
	ret


;
;	INTEGER ARITHMETIC CONVENTIONS
;
;INTEGER VARIABLES ARE 2 BYTE, SIGNED NUMBERS
;	THE LO BYTE COMES FIRST IN MEMORY
;
;CALLING CONVENTIONS:
;FOR ONE ARGUMENT FUNCTIONS:
;	THE ARGUMENT IS IN (HL), THE RESULT IS LEFT IN (HL)
;FOR TWO ARGUMENT OPERATIONS:
;	THE FIRST ARGUMENT IS IN (DE)
;	THE SECOND ARGUMENT IS IN (HL)
;	THE RESULT IS LEFT IN THE FAC AND IF NO OVERFLOW, (HL)
;IF OVERFLOW OCCURS, THE ARGUMENTS ARE CONVERTED TO SINGLE PRECISION
;WHEN INTEGERS ARE STORED IN THE FAC, THEY ARE STORED AT FACLO+0,1
;VALTYP(INTEGER)=2
;%


			; INTEGER SUBTRTACTION (HL):=(DE)-(HL)
			; ALTERS A,B,C,D,E,H,L
ISUB:	ld	a,h	; EXTEND THE SIGN OF (HL) TO B
	rla		; GET SIGN IN CARRY
	sbc	a,a
	ld	b,a
	call	INEGHL	; NEGATE (HL)
	ld	a,c	; GET A ZERO
	sbc	a,b	; NEGATE SIGN
	jp	IADDS	; GO ADD THE NUMBERS


			; INTEGER ADDITION (HL):=(DE)+(HL)
			; ALTERS A,B,C,D,E,H,L
IADD:	ld	a,h	; EXTEND THE SIGN OF (HL) TO B
	rla		; GET SIGN IN CARRY
	sbc	a,a
IADDS:	ld	b,a	; SAVE THE SIGN
	push	hl	; SAVE THE SECOND ARGUMENT IN CASE OF OVERFLOW
	ld	a,d	; EXTEND THE SIGN OF (DE) TO A
	rla		; GET SIGN IN CARRY
	sbc	a,a
	add	hl,de	; ADD THE TWO LO'S
	adc	a,b	; ADD THE EXTRA HO
	rrca		; IF THE LSB OF A IS DIFFERENT FROM THE MSB OF
	xor	h	;  H, THEN OVERFLOW OCCURED
	jp	p,CONIS1	; NO OVERFLOW, GET OLD (HL) OFF STACK AND WE
			;  ARE DONE, SAVE (HL) IN THE FAC ALSO
	push	bc	; OVERFLOW -- SAVE EXTENDED SIGN OF (HL)
	ex	de,hl		; GET (DE) IN (HL)
	call	CONSIH	; FLOAT IT
	pop	af	; GET SIGN OF (HL) IN A
	pop	hl	; GET OLD (HL) BACK
	call	PUSHF	; PUT FIRST ARGUMENT ON STACK
	ex	de,hl		; PUT SECOND ARGUMENT IN (DE) FOR FLOATR
	call	INEGAD	; FLOAT IT
	jp	FADDT	; ADD THE TWO NUMBERS USING SINGLE PRECISION


			; INTEGER MULTIPLICATION (HL):=(DE)*(HL)
			; ALTERS A,B,C,D,E,H,L
IMULT:	ld	a,h	; CHECK (HL) IF IS ZERO, IF SO
	or	l	;  JUST RETURN. THIS IS FOR SPEED.
	jp	z,MAKINT	; UPDATE FACLO TO BE ZERO AND RETURN
	push	hl	; SAVE SECOND ARGUMENT IN CASE OF OVERFLOW
	push	de	; SAVE FIRST ARGUMENT
	call	IMULDV	; FIX UP THE SIGNS
	push	bc	; SAVE THE SIGN OF THE RESULT
	ld	b,h	; COPY SECOND ARGUMENT INTO (BC)
	ld	c,l
	ld	hl,0	; ZERO (HL), THAT IS WHERE THE PRODUCT GOES
	ld	a,10h	; SET UP A COUNT
IMULT1:	add	hl,hl	; ROTATE PRODUCT LEFT ONE
	jp	c,IMULT5	; CHECK FOR OVERLFOW
	ex	de,hl		; ROTATE FIRST ARGUMENT LEFT ONE TO SEE IF
	add	hl,hl	;  WE ADD IN (BC) OR NOT
	ex	de,hl
	jp	nc,IMULT2	; DON'T ADD IN ANYTHING
	add	hl,bc	; ADD IN (BC)
	jp	c,IMULT5	; CHECK FOR OVERLFOW
IMULT2:	dec	a	; ARE WE DONE?
	jp	nz,IMULT1	; NO, DO IT AGAIN
	pop	bc	; WE ARE DONE, GET SIGN OF RESULT
	pop	de	; GET ORIGINAL FIRST ARGUMENT
IMLDIV:	ld	a,h	; ENTRY FROM IDIV, IS RESULT .GE. 32768?
	or	a
	jp	m,IMULT3	; IT IS, CHECK FOR SPECIAL CASE OF -32768
	pop	de	; RESULT IS OK, GET SECOND ARGUMENT OFF STACK
	ld	a,b	; GET THE SIGN OF RESULT IN A
	jp	INEGA	; NEGATE THE RESULT IF NECESSARY
IMULT3:	xor	80h	; IS RESULT 32768?
	or	l	; NOTE: IF WE GET HERE FROM IDIV, THE RESULT
	jp	z,IMULT4	;  MUST BE 32768, IT CANNOT BE GREATER
	ex	de,hl		; IT IS .GT. 32768, WE HAVE OVERFLOW
	defb	001	; "LXI B" OVER NEXT 2 BYTES
IMULT5:	pop	bc	; GET SIGN OF RESULT OFF STACK
	pop	hl	; GET THE ORIGINAL FIRST ARGUMENT
	call	CONSIH	; FLOAT IT
	pop	hl	; GET THE ORIGINAL SECOND ARGUMENT
	call	PUSHF	; SAVE FLOATED FIRST ARUMENT
	call	CONSIH	; FLOAT SECOND ARGUMENT
FMULTT:	pop	bc
	pop	de
			; GET FIRST ARGUMENT OFF STACK, ENTRY FROM POLYX
	jp	FMULT	; MULTIPLY THE ARGUMENTS USING SINGLE PRECISION
IMULT4:	ld	a,b	; IS RESULT +32768 OR -32768?
	or	a	; GET ITS SIGN
	pop	bc	; DISCARD ORIGINAL SECOND ARGUMENT
	jp	m,MAKINT	; THE RESULT SHOULD BE NEGATIVE, IT IS OK
	push	de	; IT IS POSITIVE, SAVE REMAINDER FOR MOD
	call	CONSIH	; FLOAT -32768
	pop	de	; GET MOD'S REMAINDER BACK
	jp	NEG	; NEGATE -32768 TO GET 32768, WE ARE DONE


			; INTEGER DIVISION (HL):=(DE)/(HL)
			; REMAINDER IS IN (DE), QUOTIENT IN (HL)
			; ALTERS A,B,C,D,E,H,L
IDIV:	ld	a,h	; CHECK FOR DIVISION BY ZERO
	or	l
	jp	z,DV0ERR	; WE HAVE DIVISION BY ZERO!!
	call	IMULDV	; FIX UP THE SIGNS
	push	bc	; SAVE THE SIGN OF THE RESULT
	ex	de,hl		; GET DENOMINATOR IN (HL)
	call	INEGHL	; NEGATE IT
	ld	b,h	; SAVE NEGATED DENOMINATOR IN (BC)
	ld	c,l
	ld	hl,0	; ZERO WHERE WE DO THE SUBTRACTION
	ld	a,11h	; SET UP A COUNT
	push	af	; SAVE IT
	or	a	; CLEAR CARRY
	jp	IDIV3	; GO DIVIDE
IDIV1:	push	af	; SAVE COUNT
	push	hl	; SAVE (HL) I.E. CURRENT NUMERATOR
	add	hl,bc	; SUBTRACT DENOMINATOR
	jp	nc,IDIV2	; WE SUBTRACTED TOO MUCH, GET OLD (HL) BACK
	pop	af	; THE SUBTRACTION WAS GOOD, DISCARD OLD (HL)
	scf		; NEXT BIT IN QUOTIENT IS A ONE
	defb	3eh	; "MVI A" OVER NEXT BYTE
IDIV2:	pop	hl	; IGNORE THE SUBTRACTION, WE COULDN'T DO IT
IDIV3:	ld	a,e	; SHIFT IN THE NEXT QUOTIENT BIT
	rla
	ld	e,a
	ld	a,d	; SHIFT THE HO
	rla
	ld	d,a
	ld	a,l	; SHIFT IN THE NEXT BIT OF THE NUMERATOR
	rla
	ld	l,a
	ld	a,h	; DO THE HO
	rla
	ld	h,a	; SAVE THE HO
	pop	af	; GET COUNT BACK
	dec	a	; ARE WE DONE?
	jp	nz,IDIV1	; NO, DIVIDE AGAIN
	ex	de,hl		; GET QUOTIENT IN (HL), REMAINDER IN (DE)
	pop	bc	; GET SIGN OF RESULT
	push	de	; SAVE REMAINDER SO STACK WILL BE ALRIGHT
	jp	IMLDIV	; CHECK FOR SPECIAL CASE OF 32768


			; GET READY TO MULTIPLY OR DIVIDE
			; ALTERS A,B,C,D,E,H,L
IMULDV:	ld	a,h	; GET SIGN OF RESULT
	xor	d
	ld	b,a	; SAVE IT IN B
	call	INEGH	; NEGATE SECOND ARGUMENT IF NECESARY
	ex	de,hl		; PUT (DE) IN (HL), FALL IN AND NEGATE FIRST
			;  ARGUMENT IF NECESSARY


			; NEGATE H,L
			; ALTERS A,C,H,L
INEGH:	ld	a,h	; GET SIGN OF (HL)
INEGA:	or	a	; SET CONDITION CODES
	jp	p,MAKINT	; WE DON'T HAVE TO NEGATE, IT IS POSITIVE
			; SAVE THE RESULT IN THE FAC FOR WHEN
			;  OPERATORS RETURN THROUGH HERE
INEGHL:	xor	a	; CLEAR A
	ld	c,a	; STORE A ZERO (WE USE THIS METHOD FOR ISUB)
	sub	l	; NEGATE LO
	ld	l,a	; SAVE IT
	ld	a,c	; GET A ZERO BACK
	sbc	a,h	; NEGATE HO
	ld	h,a	; SAVE IT
	jp	MAKINT	; ALL DONE, SAVE THE RESULT IN THE FAC
			;  FOR WHEN OPERATORS RETURN THROUGH HERE


			; INTEGER NEGATION
			; ALTERS A,B,C,D,E,H,L
INEG:	ld	hl,(FACLO)	; GET THE INTEGER
	call	INEGHL	; NEGATE IT
	ld	a,h	; GET THE HIGH ORDER
	xor	80h	; CHECK FOR SPECIAL CASE OF 32768
	or	l
	ret	nz		; IT DID NOT OCCUR, EVERYTHING IS FINE
	PUBLIC	INEG2
INEG2:	ex	de,hl		; WE HAVE IT, FLOAT 32768
	call	VALSNG	; CHANGE VALTYP TO "SINGLE PRECISION"
	xor	a	; GET A ZERO FOR THE HIGH ORDER
INEGAD:	ld	b,98h	; ENTRY FROM IADD, SET EXPONENT
	jp	FLOATR	; GO FLOAT THE NUMBER


			; MOD OPERATOR
			; (HL):=(DE)-(DE)/(HL)*(HL), (DE)=QUOTIENT
			; ALTERS A,B,C,D,E,H,L
IMOD:	push	de	; SAVE (DE) FOR ITS SIGN
	call	IDIV	; DIVIDE AND GET THE REMAINDER
	xor	a	; TURNOFF THE CARRY AND TRANFER
	add	a,d	; THE REMAINDER*2 WHICH IS IN [D,E]
	rra		; TO [H,L] DIVIDING BY TWO
	ld	h,a
	ld	a,e
	rra
	ld	l,a	;  ***WHG01*** FIX TO MOD OPERATOR
	call	VALINT	; SET VALTYP TO "INTEGER" IN CASE RESULT OF
			;  THE DIVISION WAS 32768
	pop	af	; GET THE SIGN OF THE REMAINDER BACK
	jp	INEGA	; NEGATE THE REMAINDER IF NECESSARY
	PAGE

	SUBTTL	DOUBLE PRECISION ARITHMETIC ROUTINES
;
;	DOUBLE PRECISION ARITHMETIC CONVENTIONS
;
;DOUBLE PRECISION NUMBERS ARE 8 BYTE QUANTITIES
;THE LAST 4 BYTES IN MEMORY ARE IN THE SAME FORMAT AS SINGLE PRECISION NUMBERS
;THE FIRST 4 BYTES ARE 32 MORE LOW ORDER BITS OF PRECISION
;THE LOWEST ORDER BYTE COMES FIRST IN MEMORY
;
;CALLING CONVENTIONS:
;FOR ONE ARGUMENT FUNCTIONS:
;	THE ARGUMENT IS IN THE FAC, THE RESULT IS LEFT IN THE FAC
;FOR TWO ARGUMENT OPERATIONS:
;	THE FIRST ARGUMENT IS IN THE FAC
;	THE SECOND ARGUMENT IS IN ARG-7,6,5,4,3,2,1,0  (NOTE: ARGLO=ARG-7)
;	THE RESULT IS LEFT IN THE FAC
;NOTE:	THIS ORDER IS REVERSED FROM INT AND SNG
;VALTYP(DOUBLE PRECISION)=10 OCTAL
;%


			; DOUBLE PRECISION SUBTRACTION FAC:=FAC-ARG
			; ALTERS ALL REGISTERS
DSUB:	ld	hl,ARG-1	; NEGATE THE SECOND ARGUMENT
	ld	a,(hl)	; GET THE HO AND SIGN
	xor	80h	; COMPLEMNT THE SIGN
	ld	(hl),a	; PUT IT BACK
			; FALL INTO DADD


			; DOUBLE PRECISION ADDITION FAC:=FAC+ARG
			; ALTERS ALL REGISTERS
DADD:	ld	hl,ARG	; GET POINTER TO EXPONENT OF FIRST ARGUMENT
	ld	a,(hl)	; CHECK IF IT IS ZERO
	or	a
	ret	z		; IT IS, RESULT IS ALREADY IN FAC
	ld	b,a	; SAVE EXPONENT FOR UNPACKING
	dec	hl	; POINT TO HO AND SIGN
	ld	c,(hl)	; GET HO AND SIGN FOR UNPACKING
	ld	de,FAC	; GET POINTER TO EXPONENT OF SECOND ARGUMENT
	ld	a,(de)	; GET EXPONENT
	or	a	; SEE IF IT IS ZERO
	jp	z,VMOVFA	; IT IS, MOVE ARG TO FAC AND WE ARE DONE
	sub	b	; SUBTRACT EXPONENTS TO GET SHIFT COUNT
	jp	nc,DADD2	; PUT THE SMALLER NUMBER IN FAC
	cpl		; NEGATE SHIFT COUNT
	inc	a
	push	af	; SAVE SHIFT COUNT
	ld	c,08h	; SWITCH FAC AND ARG, SET UP A COUNT
	inc	hl	; POINT TO ARG
	push	hl	; SAVE POINTER TO ARG
DADD1:	ld	a,(de)	; GET A BYTE OF THE FAC
	ld	b,(hl)	; GET A BYTE OF ARG
	ld	(hl),a	; PUT THE FAC BYTE IN ARG
	ld	a,b	; PUT THE ARG BYTE IN A
	ld	(de),a	; PUT THE ARG BYTE IN FAC
	dec	de	; POINT TO THE NEXT LO BYTE OF FAC
	dec	hl	; POINT TO THE NEXT LO BYTE OF ARG
	dec	c	; ARE WE DONE?
	jp	nz,DADD1	; NO, DO THE NEXT LO BYTE
	pop	hl	; GET THE HO BACK
	ld	b,(hl)	; GET THE EXPONENT
	dec	hl	; POINT TO THE HO AND SIGN
	ld	c,(hl)	; GET HO AND SIGN FOR UNPACKING
	pop	af	; GET THE SHIFT COUNT BACK
DADD2:	cp	39h	; ARE WE WITHIN 56 BITS?
	ret	nc		; NO, ALL DONE
	push	af	; SAVE SHIFT COUNT
	call	UNPACK	; UNPACK THE NUMBERS
	ld	hl,ARGLO-1	; POINT TO ARGLO-1
	ld	b,a	; SAVE SUBTRACTION FLAG
	ld	a,0
	ld	(hl),a	; CLEAR TEMPORARY LEAST SIG BYTE
	ld	(DFACLO-1),a	; CLEAR EXTRA BYTE
	pop	af	; GET SHIFT COUNT
	ld	hl,ARG-1	; POINT TO THE HO OF ARG
	call	DSHFTR	; SHIFT ARG RIGHT THE RIGHT NUMBER OF TIMES
	ld	a,(ARGLO-1)	; TRANSFER OVERFLOW BYTE
	ld	(DFACLO-1),a	; FROM ARG TO FAC
	ld	a,b
	or	a	; GET SUBTRACTION FLAG
	jp	p,DADD3	; SUBTRACT NUMBERS IF THEIR SIGNS ARE DIFFERENT
	call	DADDAA	; SIGNS ARE THE SAME, ADD THE NUMBERS
	jp	nc,DROUND	; ROUND THE RESULT IF NO CARRY
	ex	de,hl		; GET POINTER TO FAC IN (HL)
	inc	(hl)	; ADD 1 TO EXPONENT
	jp	z,OVFIN4
	call	DSHFRB	; SHIFT NUMBER RIGHT ONE, SHIFT IN CARRY
	jp	DROUND	; ROUND THE RESULT
DADD3:	
	defb	3eh	; "MVI A", SUBTRACT THE NUMBERS
	sbc	a,(hl)	; GET THE SUBTRACT INSTRUCTION IN A
	call	DADDA	; SUBTRACT THE NUMBERS
	ld	hl,FAC+1	; FIX [H,L] TO POINT TO SIGN FOR DNEGR
	call	c,DNEGR	; NEGATE THE RESULT IF IT WAS NEGATIVE
			; FALL INTO DNORML


			; NORMALIZE FAC
			; ALTERS A,B,C,D,H,L
DNORML:	xor	a	; CLEAR SHIFT COUNT
DNORM1:	ld	b,a	; SAVE SHIFT COUNT
	ld	a,(FAC-1)	; GET HO
	or	a	; SEE IF WE CAN SHIFT 8 LEFT
	jp	nz,DNORM5	; WE CAN'T, SEE IF NUMBER IS NORMALIZED
	ld	hl,DFACLO-1	; WE CAN, GET POINTER TO LO
	ld	c,08h	; SET UP A COUNT
DNORM2:	ld	d,(hl)	; GET A BYTE OF FAC
	ld	(hl),a	; PUT IN BYTE FROM LAST LOCATION, THE FIRST
			;  TIME THROUGH A IS ZERO
	ld	a,d	; PUT THE CURRENT BYTE IN A FOR NEXT TIME
	inc	hl	; INCREMENT POINTER TO NEXT HIGHER ORDER
	dec	c	; ARE WE DONE?
	jp	nz,DNORM2	; NO, DO THE NEXT BYTE
	ld	a,b	; SUBTRACT 8 FROM SHIFT COUNT
	sub	08h
	cp	0c0h	; HAVE WE SHIFTED ALL BYTES TO ZERO?
	jp	nz,DNORM1	; NO, TRY TO SHIFT 8 MORE
	jp	ZERO	; YES, THE NUMBER IS ZERO
DNORM3:	dec	b	; DECREMENT SHIFT COUNT
	ld	hl,DFACLO-1	; GET POINTER TO LO
	call	DSHFLC	; SHIFT THE FAC LEFT
	or	a	; SEE IF NUMBER IS NORMALIZED
DNORM5:	jp	p,DNORM3	; SHIFT FAC LEFT ONE IF IT IS NOT NORMALIZED
	ld	a,b	; GET THE SHIFT COUNT
	or	a	; SEE IF NO SHIFTING WAS DONE
	jp	z,DROUND	; NONE WAS, PROCEED TO ROUND THE NUMBER
	ld	hl,FAC	; GET POINTER TO EXPONENT
	add	a,(hl)	; UPDATE IT
	ld	(hl),a	; SAVE UPDATED EXPONENT
	jp	nc,ZERO	; UNDERFLOW, THE RESULT IS ZERO
	ret	z		; RESULT IS ALREADY ZERO, WE ARE DONE
			; FALL INTO DROUND AND ROUND THE RESULT


			; ROUND FAC
			; ALTERS A,B,H,L
DROUND:	ld	a,(DFACLO-1)	; GET EXTRA BYTE TO SEE IF WE HAVE TO ROUND
DROUNB:	or	a	; ENTRY FROM DDIV
	call	m,DROUNA	; ROUND UP IF NECESSARY
	ld	hl,FAC+1	; GET POINTER TO UNPACKED SIGN
	ld	a,(hl)	; GET SIGN
	and	80h	; ISOLATE SIGN BIT
	dec	hl	; POINT TO HO
	dec	hl
	xor	(hl)	; PACK SIGN AND HO
	ld	(hl),a	; PUT PACKED SIGN AND HO IN FAC
	ret		; WE ARE DONE



			; SUBROUTINE FOR ROUND: ADD ONE TO FAC
DROUNA:	ld	hl,DFACLO	; GET POINTER TO LO, ENTRY FROM DINT
	ld	b,7	; SET UP A COUNT
DRONA1:	inc	(hl)	; INCREMENT A BYTE
	ret	nz		; RETURN IF THERE WAS NO CARRY
	inc	hl	; INCREMENT POINTER TO NEXT HIGHER ORDER
	dec	b	; HAVE WE INCREMENTED ALL BYTES
	jp	nz,DRONA1	; NO, TRY THE NEXT ONE
	inc	(hl)	; YES, INCREMENT THE EXPONENT
	jp	z,OVFIN4
	dec	hl	; THE NUMBER OVERFLOWED ITS EXPONENT
	ld	(hl),80h	; PUT 200 IN HO
	ret		; ALL DONE


			; ADD OR SUBTRACT 2 DBL QUANTITIES
			; ALTERS A,C,D,E,H,L
DADDD:	ld	de,FBUFFR+27	; ENTRY FROM DDIV
	ld	hl,ARGLO	; ADD OR SUBTRACT FBUFFR+^D27 AND ARG
	jp	DADDS	; DO THE OPERATION

DADDAA:	defb	3eh	; "MVI A", ENTRY FROM DADD, DMULT
	adc	a,(hl)	; SETUP ADD INSTRUCTION FOR LOOP
DADDA:	ld	hl,ARGLO	; GET POINTER TO ARG, ENTRY FROM DADD
DADDFO:	ld	de,DFACLO	; GET POINTER TO FAC, ENTRY FROM FOUT
DADDS:	ld	c,7	; SET UP A COUNT
DADDS1:	
	ld	(DADDOP),a	; STORE THE ADD OR SUBTRACT INSTRUCTION
	xor	a	; CLEAR CARRY
DADDL:	ld	a,(de)	; GET A BYTE FROM RESULT NUMBER
DADDOP:	adc	a,(hl)	; THIS IS EITHER "ADC M" OR "SBB M"
	ld	(de),a	; SAVE THE CHANGED BYTE
	inc	de	; INCREMENT POINTERS TO NEXT HIGHER ORDER BYTE
	inc	hl
	dec	c	; ARE WE DONE?
	jp	nz,DADDL	; NO, DO THE NEXT HIGHER ORDER BYTE
	ret		; ALL DONE




			; NEGATE SIGNED NUMBER IN FAC
			; THIS IS USED BY DADD, DINT
			; ALTERS A,B,C,H,L
DNEGR:	ld	a,(hl)	; COMPLEMENT SIGN OF FAC
	cpl		; USE THE UNPACKED SIGN BYTE
	ld	(hl),a	; SAVE THE NEW SIGN
	ld	hl,DFACLO-1	; GET POINTER TO LO
	ld	b,08h	; SET UP A COUNT
	xor	a	; CLEAR CARRY AND GET A ZERO
	ld	c,a	; SAVE ZERO IN C
DNEGR1:	ld	a,c	; GET A ZERO
	sbc	a,(hl)	; NEGATE THE BYTE OF FAC
	ld	(hl),a	; UPDATE FAC
	inc	hl	; INCREMENT POINTER TO NEXT HIGHER ORDER BYTE
	dec	b	; ARE WE DONE?
	jp	nz,DNEGR1	; NO, NEGATE THE NEXT BYTE
	ret		; ALL DONE


			; SHIFT DBL FAC RIGHT ONE
			; A = SHIFT COUNT
			; ALTERS A,C,D,E,H,L
DSHFTR:	ld	(hl),c	; PUT THE UNPACKED HO BACK
	push	hl	; SAVE POINTER TO WHAT TO SHIFT
DSHFR1:	sub	08h	; SEE IF WE CAN SHIFT 8 RIGHT
	jp	c,DSHFR3	; WE CAN'T, CHECK IF WE ARE DONE
	pop	hl	; GET POINTER BACK
DSHFRM:	push	hl	; ENTRY FROM DMULT, SAVE POINTER TO HO
	ld	de,08h*100h+0	; SHIFT A ZERO INTO THE HO, SET UP A COUNT
DSHFR2:	ld	c,(hl)	; SAVE A BYTE OF FAC
	ld	(hl),e	; PUT THE LAST BYTE IN ITS PLACE
	ld	e,c	; SET UP E FOR NEXT TIME THROUGH THE LOOP
	dec	hl	; POINT TO NEXT LOWER ORDER BYTE
	dec	d	; ARE WE DONE?
	jp	nz,DSHFR2	; NO, DO THE NEXT BYTE
	jp	DSHFR1	; YES, SEE IF WE CAN SHIFT OVER 8 MORE
DSHFR3:	add	a,09h	; CORRECT SHIFT COUNT
	ld	d,a	; SAVE SHIFT COUNT IN D
DSHFR4:	xor	a	; CLEAR CARRY
	pop	hl	; GET POINTER TO HO
	dec	d	; ARE WE DONE?
	ret	z		; YES
DSHFRA:	push	hl	; NO, SAVE POINTER TO LO, ENTRY FROM DADD, DMULT
	ld	e,08h	; SET UP A COUNT, ROTATE FAC ONE LEFT
DSHFR5:	ld	a,(hl)	; GET A BYTE OF THE FAC
	rra		; ROTATE IT LEFT
	ld	(hl),a	; PUT THE UPDATED BYTE BACK
	dec	hl	; DECREMENT POINTER TO NEXT LOWER ORDER BYTE
	dec	e	; ARE WE DONE?
	jp	nz,DSHFR5	; NO, ROTATE THE NEXT LOWER ORDER BYTE
	jp	DSHFR4	; YES, SEE IF WE ARE DONE SHIFTING

			; ENTRY TO DSHFTR FROM DADD, DMULT
DSHFRB:	ld	hl,FAC-1	; GET POINTER TO HO OF FAC
	ld	d,1	; SHIFT RIGHT ONCE
	jp	DSHFRA	; GO DO IT


			; ROTATE FAC LEFT ONE
			; ALTERS A,C,H,L
DSHFLC:	ld	c,08h	; SET UP A COUNT
DSHFTL:	ld	a,(hl)
	rla		; ROTATE IT LEFT ONE
	ld	(hl),a	; UPDATE BYTE IN FAC
	inc	hl	; INCREMENT POINTER TO NEXT HIGHER ORDER BYTE
	dec	c	; ARE WE DONE?
	jp	nz,DSHFTL
	ret		; ALL DONE


			; DOUBLE PRECISION MULTIPLICATION FAC:=FAC*ARG
			; ALTERS ALL REGISTERS
DMULT:	call	SIGN	; CHECK IF WE ARE MULTIPLYING BY ZERO
	ret	z		; YES, ALL DONE, THE FAC IS ZERO
	ld	a,(ARG)	; MUST SEE IF ARG IS ZERO
	or	a
	jp	z,ZERO	; RETURN ZERO

	call	MULDVA	; ADD EXPONENTS AND TAKE CARE OF SIGNS
	call	DMULDV	; ZERO FAC AND PUT FAC IN FBUFFR
	ld	(hl),c	; PUT UNPACKED HO IN ARG
	inc	de	; GET POINTER TO LO OF ARG
	ld	b,7	; SET UP A COUNT
DMULT2:	ld	a,(de)	; GET THE BYTE OF ARG TO MULTIPLY BY
	inc	de	; INCREMENT POINTER TO NEXT HIGHER BYTE
	or	a	; CHECK IF WE ARE MULTIPLYING BY ZERO
	push	de	; SAVE POINTER TO ARG
	jp	z,DMULT5	; WE ARE
	ld	c,08h	; SET UP A COUNT
DMULT3:	push	bc	; SAVE COUNTERS
	rra		; ROTATE MULTIPLIER RIGHT
	ld	b,a	; SAVE IT
	call	c,DADDAA	; ADD IN OLD FAC IF BIT OF MULTIPIER WAS ONE
	call	DSHFRB	; ROTATE PRODUCT RIGHT ONE
	ld	a,b	; GET MULTIPLIER IN A
	pop	bc	; GET COUNTERS BACK
	dec	c	; ARE WE DONE WITH THIS BYTE OF ARG?
	jp	nz,DMULT3	; NO, MULTIPLY BY THE NEXT BIT OF THE MULTIPLIER
DMULT4:	pop	de	; YES, GET POINTER INTO ARG BACK
	dec	b	; ARE WE DONE?
	jp	nz,DMULT2	; NO, MULTIPLY BY NEXT HIGHER ORDER BY OF ARG
			; POINT IS TO RIGHT OF UNDERSTOOD ONE
	jp	DNORML	; ALL DONE, NORMALIZE AND ROUND RESULT
DMULT5:	ld	hl,FAC-1	; GET POINTER TO HO OF FAC
	call	DSHFRM	; SHIFT PRODUCT RIGHT ONE BYTE, WE ARE
	jp	DMULT4	;  MULTIPLYIING BY ZERO

			; CONSTANT FOR DIV10, DDIV10
TENTH:	defb	0cdh
	defb	0cch
	defb	0cch
	defb	0cch
	defb	0cch
	defb	0cch
	defb	4ch
	defb	7dh
DTEN:	defb	000	;  10D0
	defb	000
	defb	000
	defb	000
FTEN:	defb	000	;  10.0
	defb	000
	defb	20h
	defb	84h

			; DOUBLE PRECISION DIVIDE FAC BY 10
			; ALTERS ALL REGISTERS
DDIV10:			; DOUBLE PRECISION DIVIDE FAC BY 10
			; (FAC)=(FAC)*3/4*16/15*1/8
	ld	a,(FAC)	; MUST ASSURE OURSELVES WE CAN DO
	cp	41h	; 65 EXPONENT DECREMENTS W/O
	jp	nc,DD04	; REACHING ZERO
	ld	de,TENTH	; POINT TO .1D0
	ld	hl,ARGLO	; POINT TO ARG
	call	VMOVE
	jp	DMULT
DD04:	
	ld	a,(FAC-1)	; NEGATIVE NO?
	or	a
	jp	p,DD05
	and	7fh	; WANT ONLY POS. NOS.
	ld	(FAC-1),a
	ld	hl,NEG
	push	hl	; WILL NEGATE WHEN FINISHED
DD05:	
	call	$DECF1	; DIVIDE FAC BY 2
	ld	de,DFACLO
	ld	hl,ARGLO
	call	VMOVE
	call	$DECF1	; DIVIDE FAC BY 2
	call	DADD	; (FAC)=(FAC)+(ARG)
	ld	de,DFACLO
	ld	hl,ARGLO
	call	VMOVE	; (ARG)=(FAC)
	ld	a,15
DD10:	push	af	; SAVE LOOP COUNTER
	call	$DECA4	; (ARG)=(ARG)/16
	call	$PSARG	; PUSH ARG ON THE STACK
	call	DADD	; (FAC)=(FAC)+(ARG)
	ld	hl,ARG-1
	call	$PPARG	; POP ARG OFF THE STACK
	pop	af	; FETCH LOOP COUNTER
	dec	a
	jp	nz,DD10
	call	$DECF1
	call	$DECF1
	call	$DECF1
	ret
$DECF1:	ld	hl,FAC
	dec	(hl)	; (FAC)=(FAC)/2
	ret	nz
	jp	ZERO	; UNDERFLOW
$DECA4:			; (ARG)=(ARG)/16
	ld	hl,ARG
	ld	a,4
DC4:	dec	(hl)
	ret	z
	dec	a
	jp	nz,DC4
	ret
$PSARG:			; PUSH DOUBLE PRECISION ARG ON THE STACK
	pop	de	; GET OUR RETURN ADDRESS OFF THE STACK
	ld	a,4
	ld	hl,ARGLO
PSA10:	ld	c,(hl)	; FETCH BYTE
	inc	hl
	ld	b,(hl)
	inc	hl
	push	bc
	dec	a	; THROUGH?
	jp	nz,PSA10
	push	de
	ret
$PPARG:			; POP ARG OFF THE STACK
	pop	de	; GET OUR RETURN ADDRESS OFF THE STACK
	ld	a,4
	ld	hl,ARG
PPA10:	pop	bc
	ld	(hl),b
	dec	hl
	ld	(hl),c
	dec	hl
	dec	a
	jp	nz,PPA10
	push	de
	ret
			; DOUBLE PRECISION DIVISION FAC:=FAC/ARG
			; ALTERS ALL REGISTERS
DDIV:	ld	a,(ARG)	; CHECK FOR DIVISION BY ZERO
	or	a	; GET THE EXPONENT OF ARG
	jp	z,INTDV0
	ld	a,(FAC)	; IF FAC=0 THEN ANS IS ZERO
	or	a
	jp	z,ZERO
	call	MULDVS	; SUBTRACT EXPONENTS AND CHECK SIGNS
	inc	(hl)	; MULDIV DIFFERENT FOR TRUANS=0
	inc	(hl)	; MUST CORRECT FOR INCORRECT EXP CALC
	jp	z,OVFIN4
	call	DMULDV	; ZERO FAC AND PUT FAC IN FBUFFR
	ld	hl,FBUFFR+34	; GET POINTER TO THE EXTRA HO BYTE WE WILL USE
	ld	(hl),c	; ZERO IT
	ld	b,c	; ZERO FLAG TO SEE WHEN WE START DIVIDING
DDIV1:	
	defb	3eh	; "MVI A", SUBTRACT ARG FROM FBUFFR
	sbc	a,(hl)	; GET SUBTRACT INSTRUCTION
	call	DADDD	; DO THE SUBTRACTION
	ld	a,(de)	; SUBTRACT FROM EXTRA HO BYTE
	sbc	a,c	; HERE C=0
	ccf		; CARRY=1 IF SUBTRACTION WAS GOOD
	jp	c,DDIV2	; WAS IT OK?
	defb	3eh	; "MVI A" NO, ADD FBUFFR BACK IN
	adc	a,(hl)	; GET ADD INSTRUCTION
	call	DADDD	; DO THE ADDITION
	xor	a	; CLEAR CARRY
	defb	0dah	; "JC" OVER NEXT TWO BYTES
DDIV2:	ld	(de),a	; STORE THE NEW HIGHEST ORDER BYTE
	inc	b	; INCREMENT FLAG TO SHOW WE COULD DIVIDE
	ld	a,(FAC-1)	; CHECK IF WE ARE DONE DIVIDING
	inc	a	; SET SIGN FLAG WITHOUT AFFECTING CARRY
	dec	a
	rra		; PUT CARRY IN MSB FOR DROUND
	jp	m,DROUNB	; WE ARE DONE, WE HAVE 57 BITS OF ACCURACY
	rla		; GET OLD CARRY BACK WHERE IT BELONGS
	ld	hl,DFACLO	; GET POINTER TO LO OF FAC
	ld	c,7	; SET UP A COUNT, SHIFT FAC LEFT ONE
	call	DSHFTL	; SHIFT IN THE NEXT BIT IN THE QUOTIENT
	ld	hl,FBUFFR+27	; GET POINTER TO LO IN FBUFFR
	call	DSHFLC	; SHIFT DIVIDEND ONE LEFT
	ld	a,b	; IS THIS THE FIRST TIME AND WAS THE
	or	a	;  SUBTRACTION NOT GOOD? (B WILL GET
	jp	nz,DDIV1	;  CHANGED ON THE FIRST OR SECOND SUBTRACTION)
	ld	hl,FAC	; YES, SUBTRACT ONE FROM EXPONENT TO CORRECT
	dec	(hl)	;  SCALING
	jp	nz,DDIV1	; CONTINUE DIVIDING IF NO UNDERFLOW
	jp	ZERO	; UNDERFLOW


			; TRANSFER FAC TO FBUFFR FOR DMULT AND DDIV
			; ALTERS A,B,C,D,E,H,L
DMULDV:	
	ld	a,c	; PUT UNPACKED HO BACK IN ARG
	ld	(ARG-1),a
	dec	hl	; POINT TO HO OF FAC
	ld	de,FBUFFR+33	; POINT TO END OF FBUFFR
	ld	bc,7*100h+0	; SET UP A COUNT
			; TO FBUFFR
DMLDV1:	ld	a,(hl)	; GET A BYTE FROM FAC
	ld	(de),a	; PUT IT IN FBUFFR
	ld	(hl),c	; PUT A ZERO IN FAC
	dec	de	; POINT TO NEXT BYTE IN FBUFFR
	dec	hl	; POINT TO NEXT LOWER ORDER BYTE IN FAC
	dec	b	; ARE WE DONE?
	jp	nz,DMLDV1	; NO, TRANSFER THE NEXT BYTE
	ret		; ALL DONE



			; DOUBLE PRECISION MULTIPLY THE FAC BY 10
			; ALTERS ALL REGISTERS
DMUL10:	call	VMOVAF	; SAVE THE FAC IN ARG
			; VMOVAF EXITS WITH (DE)=FAC+1
	ex	de,hl		; GET THE POINTER INTO THE FAC IN (HL)
	dec	hl	; POINT TO THE EXPONENT
	ld	a,(hl)	; GET THE EXPONENT
	or	a	; IS THE NUMBER ZERO?
	ret	z		; YES, ALL DONE
	add	a,2	; MULTIPLY FAC BY 4 BY ADDING 2 TO THE EXPONENT
	jp	c,OVFIN4
	ld	(hl),a	; SAVE THE NEW EXPONENT
	push	hl	; SAVE POINTER TO FAC
	call	DADD	; ADD IN THE ORIGINAL FAC TO GET 5 TIMES FAC
	pop	hl	; GET THE POINTER TO FAC BACK
	inc	(hl)	; ADD ONE TO EXPONENT TO GET 10 TIMES FAC
	ret	nz		; ALL DONE IF OVERFLOW DID NOT OCCUR
	jp	OVFIN4
			; MULTIPLY FAC BY 10
	PAGE
	SUBTTL	FLOATING POINT INPUT ROUTINE
			; ALTERS ALL REGISTERS
			; THE NUMBER IS LEFT IN FAC
			; AT ENTRY, (HL) POINTS TO THE FIRST CHARACTER IN A TEXT BUFFER.
			; THE FIRST CHARACTER IS ALSO IN A. WE PACK THE DIGITS INTO THE FAC
			; AS AN INTEGER AND KEEP TRACK OF WHERE THE DECIMAL POINT IS.
			; C IS 377 IF WE HAVE NOT SEEN A DECIMAL POINT, 0 IF WE HAVE.
			; B IS THE NUMBER OF DIGITS AFTER THE DECIMAL POINT.
			; AT THE END, B AND THE EXPONENT (IN E) ARE USED TO DETERMINE HOW MANY
			; TIMES WE MULTIPLY OR DIVIDE BY TEN TO GET THE CORRECT NUMBER.
	PUBLIC	FINDBL
FINDBL:	call	ZERO	; ZERO THE FAC
	call	VALDBL	; FORCE TO DOUBLE PRECISION
	defb	0f6h	; "ORI" OVER "XRA A" SO FRCINT IS NOT CALLED
FIN:	
	xor	a	; FORCE CALL TO FRCINT
	EXTRN	FINOVC
	ld	bc,FINOVC	; WHEN DONE STORE OVERFLOW FLAG
	push	bc	; INTO STROVC AND GO TO NORMAL OVERFLOW MODE
	push	af	; SET UP ONCE ONLY OVERFLOW MODE
	ld	a,1
	ld	(FLGOVC),a
	pop	af
FINCHR:	ex	de,hl		; SAVE THE TEXT POINTER IN (DE)
	ld	bc,0ffh+0	; CLEAR FLAGS: B=DECIMAL PLACE COUNT
			; C="." FLAG
	ld	h,b	; ZERO (HL)
	ld	l,b
	call	z,MAKINT	; ZERO FAC, SET VALTYP TO "INTEGER"
	ex	de,hl		; GET THE TEXT POINTER BACK IN (HL) AND
			;  ZEROS IN (DE)
	ld	a,(hl)	; RESTORE CHAR FROM MEMORY
	cp	'&'
	EXTRN	OCTCNS
	jp	z,OCTCNS
			; RESTORE [A]
			; IF WE ARE CALLED BY VAL OR INPUT OR READ, THE SIGNS MAY NOT BE CRUNCHED
	cp	'-'	; SEE IF NUMBER IS NEGATIVE
	push	af	; SAVE SIGN
	jp	z,FIN1	; IGNORE MINUS SIGN
	cp	'+'	; IGNORE A LEADING SIGN
	jp	z,FIN1
	dec	hl	; SET CHARACTER POINTER BACK ONE
FIN1:	
			; HERE TO CHECK FOR A DIGIT, A DECIMAL POINT, "E" OR "D"
FINC:	call	CHRGTR	; GET THE NEXT CHARACTER OF THE NUMBER
	jp	c,FINDIG	; WE HAVE A DIGIT
	cp	'.'	; CHECK FOR A DECIMAL POINT
	jp	z,FINDP	; WE HAVE ONE, I GUESS
	cp	65h	; LOWER CASE "E"
	jp	z,FINC1
	cp	'E'	; CHECK FOR A SINGLE PRECISION EXPONENT
FINC1:	
	jp	nz,NOTE	; NO
	push	hl	; SAVE TEXT PTR
	call	CHRGTR	; GET NEXT CHAR
	cp	'L'+20h	; SEE IF LOWER CASE "L"
	jp	z,WUZELS	; IF SO POSSIBLE ELSE
	cp	'L'	; IS THIS REALLY AN "ELSE"?
	jp	z,WUZELS	; WAS ELSE
	cp	'Q'+20h	; SEE IF LOWER CASE "Q"
	jp	z,WUZELS	; IF SO POSSIBLE "EQV"
	cp	'Q'	; POSSIBLE "EQV"
WUZELS:	pop	hl	; RESTORE [H,L]
	jp	z,WUZ	; IT WAS JUMP!
	ld	a,(VALTYP)	; IF DOUBLE DON'T DOWNGRADE TO SINGLE
	cp	08h	; SET CONDITION CODES
	jp	z,FINEX1
	ld	a,0	; MAKE A=0 SO NUMBER IS A SINGLE
	jp	FINEX1
WUZ:	
	ld	a,(hl)	; RESTORE ORIGINAL CHAR
NOTE:	
	cp	'%'	; TRAILING % (RSTS-11 COMPATIBILITY)
	jp	z,FININT	; MUST BE INTEGER.
	cp	'#'	; FORCE DOUBLE PRECISION?
	jp	z,FINDBF	; YES, FORCE IT & FINISH UP.
	cp	'!'	; FORCE SINGLE PREC.
	jp	z,FINSNF
	cp	64h	; LOWER CASE "D"
	jp	z,FINEX1
	cp	'D'	; CHECK FOR A DOUBLE PRECISION EXPONENT
	jp	nz,FINE	; WE DON'T HAVE ONE, THE NUMBER IS FINISHED
FINEX1:	or	a	; DOUBLE PRECISION NUMBER -- TURN OFF ZERO FLAG
FINEX:	call	FINFRC	; FORCE THE FAC TO BE SNG OR DBL
	call	CHRGTR	; GET THE FIRST CHARACTER OF THE EXPONENT
	call	MINPLS	; EAT SIGN OF EXPONENT
			; HERE TO GET THE NEXT DIGIT OF THE EXPONENT
FINEC:	call	CHRGTR	; GET THE NEXT CHARATER
	jp	c,FINEDG	; PACK THE NEXT DIGIT INTO THE EXPONENT
	inc	d	; IT WAS NOT A DIGIT, PUT THE CORRECT SIGN ON
	jp	nz,FINE	;  THE EXPONENT, IT IS POSITIVE
	xor	a	; THE EXPONENT IS NEGATIVE
	sub	e	; NEGATE IT
	ld	e,a	; SAVE IT AGAIN
			; HERE TO FINISH UP THE NUMBER
FINE:	push	hl	; SAVE THE TEXT POINTER
	ld	a,e	; FIND OUT HOW MANY TIMES WE HAVE TO MULTIPLY
	sub	b	;  OR DIVIDE BY TEN
	ld	e,a	; SAVE NEW EXPONENT IN E
			; HERE TO MULTIPLY OR DIVIDE BY TEN THE CORRECT NUMBER OF TIMES
			; IF THE NUMBER IS AN INT, A IS 0 HERE.
FINE2:	call	p,FINMUL	; MULTIPLY IF WE HAVE TO
	call	m,FINDIV	; DIVIDE IF WE HAVE TO
	jp	nz,FINE2	; MULTIPLY OR DIVIDE AGAIN IF WE ARE NOT DONE
			; HERE TO PUT THE CORRECT SIGN ON THE NUMBER
	pop	hl	; GET THE TEXT POINTER
	pop	af	; GET THE SIGN
	push	hl	; SAVE THE TEXT POINTER AGAIN
	call	z,VNEG	; NEGATE IF NECESSARY
FINE2C:	pop	hl	; GET THE TEXT POINTER IN (HL)
	call	GETYPR	; WE WANT -32768 TO BE AN INT, BUT UNTIL NOW
			;  IT WOULD BE A SNG
	ret	pe		; IT IS NOT SNG, SO IT IS NOT -32768
	push	hl	; WE HAVE A SNG, SAVE TEXT POINTER
	ld	hl,POPHRT	; GET ADDRESS THAT POP'S H OFF STACK BECAUSE
	push	hl	;  CONIS2 DOES FUNNY THINGS WITH THE STACK
	call	CONIS2	; CHECK IF WE HAVE -32768
	ret		; WE DON'T, POPHRT IS STILL ON THE STACK SO
			;  WE CAN JUST RETURN

			; HERE TO CHECK IF WE HAVE SEEN 2 DECIMAL POINTS AND SET THE DECIMAL
			;  POINT FLAG
FINDP:	call	GETYPR	; SET CARRY IF WE DON'T HAVE A DOUBLE
	inc	c	; SET THE FLAG
	jp	nz,FINE	; WE HAD 2 DECIMAL POINTS, NOW WE ARE DONE
	call	c,FINFRC	; THIS IS THE FIRST ONE, CONVERT FAC TO SNG
			;  IF WE DON'T ALREADY HAVE A DOUBLE
	jp	FINC	; CONTINUE LOOKING FOR DIGITS

FININT:	call	CHRGTR
	pop	af	; GET SIGN OFF THE STACK
	push	hl	; SAVE TEXT POINTER
	ld	hl,POPHRT	; ADDRESS POP (HL) AND RETURN
	push	hl
	ld	hl,FRCINT	; ADDRESS TO FORCE INTEGER
	push	hl	; WILL WANT TO FORCE ONCE D.P. DONE
	push	af	; PUT SIGN BACK ON THE STACK
	jp	FINE	; ALL DONE
FINDBF:	or	a	; SET NON-ZERO TO FORCE DOUBLE PREC
FINSNF:	call	FINFRC	; FORCE THE TYPE
	call	CHRGTR	; READ AFTER TERMINATOR
	jp	FINE	; ALL DONE

			; FORCE THE FAC TO BE SNG OR DBL
			; IF THE ZERO FLAG IS ON, THEN FORCE THE FAC TO BE SNG
			; IF THE ZERO FLAG IS OFF, FORCE THE FAC TO BE DBL
FINFRC:	push	hl	; SAVE TEXT POINTER
	push	de	; SAVE EXPONENT INFORMATION
	push	bc	; SAVE DECIMAL POINT INFORMATION
	push	af	; SAVE WHAT WE WANT THE FAC TO BE
	call	z,FRCSNG	; CONVERT TO SNG IF WE HAVE TO
	pop	af	; GET TYPE FLAG BACK
	call	nz,FRCDBL	; CONVERT TO DBL IF WE HAVE TO
	pop	bc	; GET DECIMAL POINT INFORMATION BACK
	pop	de	; GET EXPONENT INFORMATION BACK
	pop	hl	; GET TEXT POINTER BACK
	ret		; ALL DONE

			; THIS SUBROUTINE MULIPLIES BY TEN ONCE.
			; IT IS A SUBROUTINE BECAUSE IT SAVES BYTES WHEN WE CHECK IF A IS ZERO
			; ALTERS ALL REGISTERS
FINMUL:	ret	z		; RETURN IF EXPONENT IS ZERO, ENTRY FROM FOUT
FINMLT:	push	af	; SAVE EXPONENT, ENTRY FROM FOUT
	call	GETYPR	; SEE WHAT KIND OF NUMBER WE HAVE
	push	af	; SAVE THE TYPE
	call	po,MUL10	; WE HAVE A SNG, MULTIPLY BY 10.0
	pop	af	; GET THE TYPE BACK
	call	pe,DMUL10	; WE HAVE A DBL, MULTIPLY BY 10D0
	pop	af	; GET EXPONENT
DCRART:	dec	a	; DECREASE IT
	ret		; ALL DONE

			; THIS SUBROUTINE DIVIDES BY TEN ONCE.
			; IT IS USED BY FIN, FOUT
			; ALTERS A,B,C
FINDIV:	push	de	; SAVE D,E
	push	hl	; SAVE H,L
	push	af	; WE HAVE TO DIVIDE -- SAVE COUNT
	call	GETYPR	; SEE WHAT KIND OF NUMBER WE HAVE
	push	af	; SAVE THE TYPE
	call	po,DIV10	; WE HAVE A SNG NUMBER
	pop	af	; GET THE TYPE BACK
	call	pe,DDIV10	; WE HAVE A DBL NUMBER
	pop	af	; GET COUNT BACK
	pop	hl	; GET H,L BACK
	pop	de	; GET D,E BACK
	inc	a	; UPDATE IT
	ret

			; HERE TO PACK THE NEXT DIGIT OF THE NUMBER INTO THE FAC
			; WE MULTIPLY THE FAC BY TEN AND ADD IN THE NEXT DIGIT
FINDIG:	
	push	de	; SAVE EXPONENT INFORMATION
	ld	a,b	; INCREMENT DECIMAL PLACE COUNT IF WE ARE
	adc	a,c	;  PAST THE DECIMAL POINT
	ld	b,a
	push	bc	; SAVE DECIMAL POINT INFORMATION
	push	hl	; SAVE TEXT POINTER
	ld	a,(hl)	; GET THE DIGIT
	sub	'0'	; CONVERT IT TO ASCII
	push	af	; SAVE THE DIGIT
	call	GETYPR	; SEE WHAT KIND OF A NUMBER WE HAVE
	jp	p,FINDGV	; WE DO NOT HAVE AN INTEGER
			; HERE TO PACK THE NEXT DIGIT OF AN INTEGER
	ld	hl,(FACLO)	; WE HAVE AN INTEGER, GET IT IN (HL)
	ld	de,3277+0	; SEE IF WE WILL OVERFLOW
	call	DCOMPR	; COMPAR RETURNS WITH CARRY ON IF
	jp	nc,FINDG2	;  (HL) .LT. (DE), SO THE NUMBER IS TOO BIG
	ld	d,h	; COPY (HL) INTO (DE)
	ld	e,l
	add	hl,hl	; MULTIPLY (HL) BY 2
	add	hl,hl	; MULTIPLY (HL) BY 2, (HL) NOW IS 4*(DE)
	add	hl,de	; ADD IN OLD (HL) TO GET 5*(DE)
	add	hl,hl	; MULTIPLY BY 2 TO GET TEN TIMES THE OLD (HL)
	pop	af	; GET THE DIGIT
	ld	c,a	; SAVE IT SO WE CAN USE DAD, B IS ALREADY ZERO
	add	hl,bc	; ADD IN THE NEXT DIGIT
	ld	a,h	; CHECK FOR OVERFLOW
	or	a	; OVERFLOW OCCURED IF THE MSB IS ON
	jp	m,FINDG1	; WE HAVE OVERFLOW!!
	ld	(FACLO),hl	; EVERYTHING IS FINE, STORE THE NEW NUMBER
FINDGE:	pop	hl	; ALL DONE, GET TEXT POINTER BACK
	pop	bc	; GET DECIMAL POINT INFORMATION BACK
	pop	de	; GET EXPONENT INFORMATION BACK
	jp	FINC	; GET THE NEXT CHARACTER
			; HERE TO HANDLE 32768, 32769
FINDG1:	ld	a,c	; GET THE DIGIT
	push	af	; PUT IT BACK ON THE STACK
			; HERE TO CONVERT THE INTEGER DIGITS TO SINGLE PRECISION DIGITS
FINDG2:	call	CONSI	; CONVERT THE INTEGER TO SINGLE PRECISION
	scf		; DO NOT TAKE THE FOLLOWING JUMP
			; HERE TO DECIDE IF WE HAVE A SINGLE OR DOUBLE PRECISION NUMBER
FINDGV:	jp	nc,FINDGD	; FALL THROUGH IF VALTYP WAS 4 I.E. SNG PREC
	ld	bc,0094h*256+0074h
	ld	de,0024h*256+0000h	; GET 1000000, DO WE HAVE 7 DIGITS ALREADY?
	call	FCOMP	; IF SO, FAC .GE. 1000000
	jp	p,FINDG3	; WE DO, CONVERT TO DOUBLE PRECISION
	call	MUL10	; MULTIPLY THE OLD NUMBER BY TEN
	pop	af	; GET THE NEXT DIGIT
	call	FINLOG	; PACK IT INTO THE FAC
	jp	FINDGE	; GET FLAGS OFF STACK AND WE ARE DONE
			; HERE TO CONVERT A 7 DIGIT SINGLE PRECISION NUMBER TO DOUBLE PRECISION
FINDG3:	call	CONDS	; CONVERT SINGLE TO DOUBLE PRECISION
			; HERE TO PACK IN THE NEXT DIGIT OF A DOUBLE PRECISION NUMBER
FINDGD:	call	DMUL10	; MULTIPLY THE FAC BY 10
	call	VMOVAF	; SAVE THE FAC IN ARG
	pop	af	; GET THE NEXT DIGIT
	call	FLOAT	; CONVERT THE DIGIT TO SINGLE PRECISION
	call	CONDS	; NOW, CONVERT THE DIGIT TO DOUBLE PRECISION
	call	DADD	; ADD IN THE DIGIT
	jp	FINDGE	; GET THE FLAGS OFF THE STACK AND WE ARE DONE

			; SUBROUTINE FOR FIN, LOG
FINLOG:	call	PUSHF	; SAVE FAC ON STACK
	call	FLOAT	; CONVERT A TO A FLOATING POINT NUMBER
FADDT:	pop	bc
	pop	de
			; GET PREVIOUS NUMBER OFF STACK
	jp	FADD	; ADD IT IN

			; HERE WE PACK IN THE NEXT DIGIT OF THE EXPONENT
			; WE MUTIPLY THE OLD EXPONENT BY TEN AND ADD IN THE NEXT DIGIT
			; NOTE: EXPONENT OVERFLOW IS NOT CHECKED FOR
FINEDG:	ld	a,e	; EXPONENT DIGIT -- MULTIPLY EXPONENT BY 10
	cp	0ah	; CHECK THAT THE EXPONENT DOES NOT OVERFLOW
			; IF IT DID, E COULD GET GARBAGE IN IT.
	jp	nc,FINEDO	; WE ALREADY HAVE TWO DIGITS
	rlca		; FIRST BY 4
	rlca
	add	a,e	; ADD 1 TO MAKE 5
	rlca		; NOW DOUBLE TO GET 10
	add	a,(hl)	; ADD IT IN
	sub	'0'	; SUBTRACT OFF ASCII CODE, THE RESULT IS
			;  POSITIVE ON LENGTH=2 BECAUSE OF THE
			;  ABOVE CHECK
	ld	e,a	; STORE EXPONENT
	defb	0fah	; "JM" OVER THE NEXT 2 BYTES
FINEDO:	ld	e,127	; AN EXPONENT LIKE THIS WILL SAFELY CAUSE
			;  OVERFLOW OR UNDERFLOW
	jp	FINEC	; CONTINUE
OVFIN1:	or	a	; CLEAR CARRY
	jp	OVFINT	; GO PRINT OVERFLOW
OVFIN9:	pop	af	; GET STACK RIGHT
OVFIN2:	push	hl
	ld	hl,FAC-1	; POINT (HL) TO SIGN BYTE
	call	GETYPR
	jp	po,OVF2A	; SP PROCEED AS NORMAL
	ld	a,(ARG-1)
	jp	OVF2B
OVF2A:	
	ld	a,c
OVF2B:	
	xor	(hl)	; SIGN IN HIGH BIT OF (A)
	rla		; SIGN IN CARRY
	pop	hl
	jp	OVFINT
OVF2C:	ld	a,(FAC+1)	; THIS ENTRY IS USED BY CONSD
	jp	OVFI4B	; WHEN D.P. EXP TOO LARGE FOR S.P.
OVFIN5:	pop	af	; NEED TO DO 3 POP'S THEN OVERFLOW
OVFIN6:	pop	af
OVFIN7:	pop	af
OVFIN3:	ld	a,(FAC-1)
	rla
	jp	OVFINT
OVFIN8:	pop	af	; DO A POP THEN FALL INTO OVFIN4
OVFIN4:	ld	a,(FAC+1)	; GET SIGN BYTE
	cpl		; SIGN WAS STORED COMPLEMENTED
OVFI4B:	rla		; SIGN TO CARRY
	jp	OVFINT
INTDV1:	ld	a,c
	jp	INTDV2
INTDV0:	push	hl	; GET ARG SIGN BYTE
	push	de
	ld	hl,DFACLO
	ld	de,INFM	; ALL ONE'S
	call	MOVE
	ld	a,(INFM)	; 377
	ld	(DFACLO+2),a	; (PREVIOUSLY 177)
	call	GETYPR
	jp	po,INDV0A	; NOT D.P. LOAD ARG SIGN
	ld	a,(FAC-1)
	jp	INDV0B
INDV0A:	
	ld	a,(ARG-1)
INDV0B:	pop	de
	pop	hl
INTDV2:	rla		; TO CARRY
	ld	hl,DIVMSG	; GET MESSAGE ADDRESS
	ld	(OVERRI),hl	; STORE SO OVFINT WILL PICK UP
OVFINT:			; ANSI OVERFLOW ROUTINE
	push	hl
	push	bc
	push	de
	push	af	; SAVE MACHINE STATUS
	push	af	; AGAIN
	EXTRN	ONELIN
	ld	hl,(ONELIN)	; TRAPPING ERRORS?
	ld	a,h
	or	l
	jp	nz,OVFPRT	; JUMP PRINT IF TRAPPING
			; OTHERWISE +INFINITY
	ld	a,(FLGOVC)	; PRINT INDICATOR FLAG
	or	a	; PRINT IF 0,1;SET TO 2 IF 1
	jp	z,OV1A	; GO PRINT
	cp	1
	jp	nz,OV1B
	ld	a,2
	ld	(FLGOVC),a
OV1A:	
	ld	hl,(OVERRI)	; ADDRESS OF OVERFLOW MESSAGE
	call	STRPRN	; PRINT
	ld	(TTYPOS),a	; SET TTY POSITION TO CHAR 0
			; SET TTY POSITION TO CHAR 0
	ld	a,0dh
	call	CALTTY
	ld	a,0ah
	call	CALTTY	; CARRIAGE RETURN,LINE FEED
OV1B:	
OVFPRT:	pop	af	; GET PLUS,MINUS INDICATION BACK
	ld	hl,FACLO	; MUST NOW PUT RIGHT INFINITY
			; INTO THE FAC
	ld	de,INFP
	jp	nc,OVFINA
	ld	de,INFM	; MINUS INFINITY
OVFINA:	call	MOVE	; MOVE INTO FAC
	call	GETYPR
	jp	po,OVFINB	; SP ALL OK
	ld	hl,DFACLO
	ld	de,INFM	; ALL ONES
	call	MOVE
OVFINB:	
	ld	hl,(ONELIN)	; TRAPPING ERRORS?
	ld	a,h
	or	l
	jp	z,NOODTP	; JUMP IF NOT TRAPPING
	ld	hl,(OVERRI)
	ld	de,OVRMSG
	call	DCOMPR
	ld	hl,OVRMSG
	ld	(OVERRI),hl
	jp	z,OVERR
	jp	DV0ERR
NOODTP:	
	pop	af
	ld	hl,OVRMSG	; PUT "OVRMSG" ADDRESS IN OVERRI
	ld	(OVERRI),hl	; IN CASE THIS WAS A DIV BY 0
	pop	de
	pop	bc
	pop	hl	; ALL RESTORED
	ret		; CONTINUE PROCESSING
INFP:	defb	0ffh
	defb	0ffh
	defb	7fh
	defb	0ffh
INFM:	defb	0ffh
	defb	0ffh
	defb	0ffh
	defb	0ffh



	PAGE
	SUBTTL	FLOATING POINT OUTPUT ROUTINE
			; ENTRY TO LINPRT
INPRT:	push	hl	; SAVE LINE NUMBER
	ld	hl,INTXT	; PRINT MESSAGE
	call	STROUT
	pop	hl	; FALL INTO LINPRT


			; PRINT THE 2 BYTE NUMBER IN H,L
			; ALTERS ALL REGISTERS
LINPRT:	
	ld	bc,STROUI
	push	bc
	PUBLIC	LINOUT
LINOUT:	call	MAKINT	; PUT THE LINE NUMBER IN THE FAC AS AN INTEGER
	xor	a	; SET FORMAT TO FREE FORMAT
	call	FOUINI	; SET UP THE SIGN
	or	(hl)	; TURN OFF THE ZERO FLAG
	jp	FOUT2	; CONVERT THE NUMBER INTO DIGITS
	EXTRN	STROUI

			; FLOATING OUTPUT OF FAC
			; ALTERS ALL REGISTERS
			; THE ORIGINAL CONTENTS OF THE FAC IS LOST
;
;	OUTPUT THE VALUE IN THE FAC ACCORDING TO THE FORMAT SPECIFICATIONS
;	IN A,B,C
;	ALL REGISTERS ARE ALTERED
;	THE ORIGINAL CONTENTS OF THE FAC IS LOST
;
;	THE FORMAT IS SPECIFIED IN A, B AND C AS FOLLOWS:
;	THE BITS OF A MEAN THE FOLLOWING:
;BIT 7	0 MEANS FREE FORMAT OUTPUT, I.E. THE OTHER BITS OF A MUST BE ZERO,
;	TRAILING ZEROS ARE SUPPRESSED, A NUMBER IS PRINTED IN FIXED OR FLOATING
;	POINT NOTATION ACCORDING TO ITS MAGNITUDE, THE NUMBER IS LEFT
;	JUSTIFIED IN ITS FIELD, B AND C ARE IGNORED.
;	1 MEANS FIXED FORMAT OUTPUT, I.E. THE OTHER BITS OF A ARE CHECKED FOR
;	FORMATTING INFORMATION, THE NUMBER IS RIGHT JUSTIFIED IN ITS FIELD,
;	TRAILING ZEROS ARE NOT SUPPRESSED.  THIS IS USED FOR PRINT USING.
;BIT 6	1 MEANS GROUP THE DIGITS IN THE INTEGER PART OF THE NUMBER INTO GROUPS
;	OF THREE AND SEPARATE THE GROUPS BY COMMAS
;	0 MEANS DON'T PRINT THE NUMBER WITH COMMAS
;BIT 5	1 MEANS FILL THE LEADING SPACES IN THE FIELD WITH ASTERISKS ("*")
;BIT 4	1 MEANS OUTPUT THE NUMBER WITH A FLOATING DOLLAR SIGN ("$")
;BIT 3	1 MEANS PRINT THE SIGN OF A POSITIVE NUMBER AS A PLUS SIGN ("+")
;	INSTEAD OF A SPACE
;BIT 2	1 MEANS PRINT THE SIGN OF THE NUMBER AFTER THE NUMBER
;BIT 1	UNUSED
;BIT 0	1 MEANS PRINT THE NUMBER IN FLOATING POINT NOTATION I.E. "E NOTATION"
;	IF THIS BIT IS ON, THE COMMA SPECIFICATION (BIT 6) IS IGNORED.
;	0 MEANS PRINT THE NUMBER IN FIXED POINT NOTATION.  NUMBERS .GE. 1E16
;	CANNOT BE PRINTED IN FIXED POINT NOTATION.
;
;	B AND C TELL HOW BIG THE FIELD IS:
;B   =	THE NUMBER OF PLACES IN THE FIELD TO THE LEFT OF THE DECIMAL POINT
;	(B DOES NOT INCLUDE THE DECIMAL POINT)
;C   =	THE NUMBER OF PLACES IN THE FIELD TO THE RIGHT OF THE DECIMAL POINT
;	(C INCLUDES THE DECIMAL POINT)
;	B AND C DO NOT INCLUDE THE 4 POSITIONS FOR THE EXPONENT IF BIT 0 IS ON
;	FOUT ASSUMES B+C .LE. 24 (DECIMAL)
;	IF THE NUMBER IS TOO BIG TO FIT IN THE FIELD, A PERCENT SIGN ("%") IS
;	PRINTED AND THE FIELD IS EXTENDED TO HOLD THE NUMBER.
;&


			; ENTRY TO PRINT THE FAC IN FREE FORMAT
FOUT:	xor	a	; SET FORMAT FLAGS TO FREE FORMATED OUTPUT
			; ENTRY TO PRINT THE FAC USING THE FORMAT SPECIFICATIONS IN A, B AND C
PUFOUT:	call	FOUINI	; SAVE THE FORMAT SPECIFICATION IN A AND PUT
			; A SPACE FOR POSITIVE NUMBERS IN THE BUFFER
	and	08h	; CHECK IF POSITIVE NUMBERS GET A PLUS SIGN
	jp	z,FOUT1	; THEY DON'T
	ld	(hl),'+'	; THEY DO, PUT IN A PLUS SIGN
FOUT1:	ex	de,hl		; SAVE BUFFER POINTER
	call	VSIGN	; GET THE SIGN OF THE FAC
	ex	de,hl		; PUT THE BUFFER POINTER BACK IN (HL)
	jp	p,FOUT2	; IF WE HAVE A NEGATIVE NUMBER, NEGATE IT
	ld	(hl),'-'	;  AND PUT A MINUS SIGN IN THE BUFFER
	push	bc	; SAVE THE FIELD LENGTH SPECIFICATION
	push	hl	; SAVE THE BUFFER POINTER
	call	VNEG	; NEGATE THE NUMBER
	pop	hl	; GET THE BUFFER POINTER BACK
	pop	bc	; GET THE FIELD LENGTH SPECIFICATIONS BACK
	or	h	; TURN OFF THE ZERO FLAG, THIS DEPENDS ON THE
			;  FACT THAT FBUFFR IS NEVER ON PAGE 0.
FOUT2:	inc	hl	; POINT TO WHERE THE NEXT CHARACTER GOES
	ld	(hl),'0'	; PUT A ZERO IN THE BUFFER IN CASE THE NUMBER
			;  IS ZERO (IN FREE FORMAT) OR TO RESERVE SPACE
			;  FOR A FLOATING DOLLAR SIGN (FIXED FORMAT)
	ld	a,(TEMP3)	; GET THE FORMAT SPECIFICATION
	ld	d,a	; SAVE IT FOR LATER
	rla		; PUT THE FREE FORMAT OR NOT BIT IN THE CARRY
	ld	a,(VALTYP)	; GET THE VALTYP, VNEG COULD HAVE CHANGED THIS
			;  SINCE -32768 IS INT AND 32768 IS SNG.
	jp	c,FOUTFX	; THE MAN WANTS FIXED FORMATED OUTPUT
			; HERE TO PRINT NUMBERS IN FREE FORMAT
	jp	z,FOUTZR	; IF THE NUMBER IS ZERO, FINISH IT UP
	cp	4	; DECIDE WHAT KIND OF A VALUE WE HAVE
	jp	nc,FOUFRV	; WE HAVE A SNG OR DBL
			; HERE TO PRINT AN INTEGER IN FREE FORMAT
	ld	bc,0	; SET THE DECIMAL POINT COUNT AND COMMA COUNT
			;  TO ZERO
	call	FOUTCI	; CONVERT THE INTEGER TO DECIMAL
			; FALL INTO FOUTZS AND ZERO SUPPRESS THE THING


			; ZERO SUPPRESS THE DIGITS IN FBUFFR
			; ASTERISK FILL AND ZERO SUPPRESS IF NECESSARY
			; SET UP B AND CONDITION CODES IF WE HAVE A TRAILING SIGN
FOUTZS:	ld	hl,FBUFFR+1	; GET POINTER TO THE SIGN
	ld	b,(hl)	; SAVE THE SIGN IN B
	ld	c,' '	; DEFAULT FILL CHARACTER TO A SPACE
	ld	a,(TEMP3)	; GET FORMAT SPECS TO SEE IF WE HAVE TO
	ld	e,a	;  ASTERISK FILL. SAVE IT
	and	20h
	jp	z,FOTZS1	; WE DON'T
	ld	a,b	; WE DO, SEE IF THE SIGN WAS A SPACE
	cp	c	; ZERO FLAG IS SET IF IT WAS
	ld	c,'*'	; SET FILL CHARACTER TO AN ASTERISK
	jp	nz,FOTZS1	; SET THE SIGN TO AN ASTERISK IF IT WAS A SPACE
	ld	a,e	; GET FORMAT SPECS AGAIN
	and	4	; SEE IF SIGN IS TRAILING
	jp	nz,FOTZS1	; IF SO DON'T ASTERISK FILL
	ld	b,c	; B HAS THE SIGN, C THE FILL CHARACTER
FOTZS1:	ld	(hl),c	; FILL IN THE ZERO OR THE SIGN
	call	CHRGTR	; GET THE NEXT CHARACTER IN THE BUFFER
			; SINCE THERE ARE NO SPACES, "CHRGET" IS
			;  EQUIVALENT TO "INX H"/"MOV A,M"
	jp	z,FOTZS4	; IF WE SEE A REAL ZERO, IT IS THE END OF
			;  THE NUMBER, AND WE MUST BACK UP AND PUT
			;  IN A ZERO. CHRGET SETS THE ZERO FLAG ON
			;  REAL ZEROS OR COLONS, BUT WE WON'T SEE
			;  ANY COLONS IN THIS BUFFER.
	cp	'E'	; BACK UP AND PUT IN A ZERO IF WE SEE
	jp	z,FOTZS4	; AN "E" OR A "D" SO WE CAN PRINT 0 IN
	cp	'D'	; FLOATING POINT NOTATION WITH THE C FORMAT ZERO
	jp	z,FOTZS4
	cp	'0'	; DO WE HAVE A ZERO?
	jp	z,FOTZS1	; YES, SUPPRESS IT
	cp	2ch	; 54="," DO WE HAVE A COMMA?
	jp	z,FOTZS1	; YES, SUPPRESS IT
	cp	'.'	; ARE WE AT THE DECIMAL POINT?
	jp	nz,FOTZS2	; NO, I GUESS NOT
FOTZS4:	dec	hl	; YES, BACK UP AND PUT A ZERO BEFORE IT
	ld	(hl),'0'
FOTZS2:	ld	a,e	; GET THE FORMAT SPECS TO CHECK FOR A FLOATING
	and	10h	;  DOLLAR SIGN
	jp	z,FOTZS3	; WE DON'T HAVE ONE
	dec	hl	; WE HAVE ONE, BACK UP AND PUT IN THE DOLLAR
	ld	(hl),CURNCY	;  SIGN
FOTZS3:	ld	a,e	; DO WE HAVE A TRAILING SIGN?
	and	4
	ret	nz		; YES, RETURN; NOTE THE NON-ZERO FLAG IS SET
	dec	hl	; NO, BACK UP ONE AND PUT THE SIGN BACK IN
	ld	(hl),b	; PUT IN THE SIGN
	ret		; ALL DONE


			; HERE TO INITIALLY SET UP THE FORMAT SPECS AND PUT IN A SPACE FOR THE
			; SIGN OF A POSITIVE NUMBER
FOUINI:	ld	(TEMP3),a	; SAVE THE FORMAT SPECIFICATION
	ld	hl,FBUFFR+1	; GET A POINTER INTO FBUFFR
			; WE START AT FBUFFR+1 IN CASE THE NUMBER WILL
			;  OVERFLOW ITS FIELD, THEN THERE IS ROOM IN
			;  FBUFFR FOR THE PERCENT SIGN.
	ld	(hl),' '	; PUT IN A SPACE
	ret		; ALL DONE


			; HERE TO PRINT A SNG OR DBL IN FREE FORMAT
FOUFRV:	
			; THE FOLLOWING CODE DOWN TO FOUFRF: IS ADDED TO ADDRESS THE
			; ANSI STANDARD OF PRINTING NUMBERS IN FIXED FORMAT RATHER THAN
			; SCIENTIFIC NOTATION IF THEY CAN BE AS ACCURATELY RPRESENTED
			; IN FIXED FORMAT

	call	PUSHF	; SAVE IN CASE NEEDED FOR 2ED PASS
	ex	de,hl		; SAVE BUFFER POINTER IN (HL)
	ld	hl,(DFACLO)
	push	hl	; SAVE FOR D.P.
	ld	hl,(DFACLO+2)
	push	hl
	ex	de,hl		; BUFFER POINTER BACK TO (HL)
	push	af	; SAVE IN CASE NEEDED FOR SECOND PASS
	xor	a	; (A)=0
	ld	(FANSII),a	; INITIALIZE FANSII FLAG
	pop	af	; GET PSW RIGHT
	push	af	; SAVE PSW
	call	FOUFRF	; FORMAT NUMBER
	ld	b,'E'	; WILL SEARCH FOR SCIENTIFIC NOTN.
	ld	c,0	; DIGIT COUNTER
FU1:			; GET ORIGINAL FBUFFER POINTER
	push	hl	; SAVE IN CASE WE NEED TO LOOK FOR "D"
	ld	a,(hl)	; FETCH UP FIRST CHARACTER
FU2:	cp	b	; SCIENTIFIC NOTATION?
	jp	z,FU4	; IF SO, JUMP
	cp	3ah	; IF CARRY NOT SET NOT A DIGIT
	jp	nc,FU2A
	cp	30h	; IF CARRY SET NOT A DIGIT
	jp	c,FU2A
	inc	c	; INCREMENTED DIGITS TO PRINT
FU2A:	inc	hl	; POINT TO NEXT BUFFER CHARACTER
	ld	a,(hl)	; FETCH NEXT CHARACTER
	or	a	; 0(BINARY) AT THE END OF CHARACTERS
	jp	nz,FU2	; CONTINUE SEARCH IF NOT AT END
	ld	a,'D'	; NOW TO CHECK TO SEE IF SEARCHED FOR D
	cp	b
	ld	b,a	; IN CASE NOT YET SEARCHED FOR
	pop	hl	; NOW TO CHECK FOR "D"
	ld	c,0	; ZERO DIGIT COUNT
	jp	nz,FU1	; GO SEARCH FOR "D" IF NOT DONE SO
FU3:	pop	af	; POP ORIGINAL PSW
	pop	bc
	pop	de
			; GET DFACLO-DFACLO+3
	ex	de,hl		; (DE)=BUF PTR,(HL)=DFACLO
	ld	(DFACLO),hl
	ld	h,b
	ld	l,c
	ld	(DFACLO+2),hl
	ex	de,hl
	pop	bc
	pop	de
			; GET ORIG FAC OFF STACK
	ret		; COMPLETE
FU4:			; PRINT IS IN SCIENTIFIC NOTATION , IS THIS BEST?
	push	bc	; SAVE TYPE,DIGIT COUNT
	ld	b,0	; EXPONENT VALUE (IN BINARY)
	inc	hl	; POINT TO NEXT CHARACTER OF EXP.
	ld	a,(hl)	; FETCH NEXT CHARACTER OF EXPONENT
FU5:	cp	'+'	; IS EXPONENT POSITIVE?
	jp	z,FU8	; IF SO NO BETTER PRINTOUT
	cp	'-'	; MUST BE NEGATIVE!
	jp	z,FU5A	; MUST PROCESS THE DIGITS
	sub	'0'	; SUBTRACT OUT ASCII BIAS
	ld	c,a	; DIGIT TO C
	ld	a,b	; FETCH OLD DIGIT
	add	a,a	; *2
	add	a,a	; *4
	add	a,b	; *5
	add	a,a	; *10
	add	a,c	; ADD IN NEW DIGIT
	ld	b,a	; BACK OUT TO EXPONENT ACCUMULATOR
	cp	10h	; 16 D.P. DIGITS FOR MICROSOFT FORMAT
	jp	nc,FU8	; IF SO STOP TRYING
FU5A:	inc	hl	; POINT TO NEXT CHARACTER
	ld	a,(hl)	; FETCH UP
	or	a	; BINARY ZERO AT END
	jp	nz,FU5	; CONTINUE IF NOT AT END
	ld	h,b	; SAVE EXPONENT
	pop	bc	; FETCH TYPE, DIGIT COUNT
	ld	a,b	; DETERMINE TYPE
	cp	'E'	; SINGLE PRECISION?
	jp	nz,FU7	; NO -GO PROCESS AS DOUBLE PRECISION
	ld	a,c	; DIGIT COUNT
	add	a,h	; ADD EXPONENT VALUE
	cp	09h
	pop	hl	; POP OLD BUFFER POINTER
	jp	nc,FU3	; CAN'T DO BETTER
FU6:	ld	a,80h
	ld	(FANSII),a
	jp	FU9	; DO FIXED POINT PRINTOUT
FU7:	ld	a,h	; SAVE EXPONENT
	add	a,c	; TOTAL DIGITS NECESSARY
	cp	12h	; MUST PRODUCE CARRY TO USE FIXED POINT
	pop	hl	; GET STACK RIGHT
	jp	nc,FU3
	jp	FU6	; GO RINT IN FIXED POINT
FU8:	pop	bc
	pop	hl	; GET ORIGINAL BUFFER PTR BACK
	jp	FU3
FU9:	pop	af	; GET ORIGINAL PSW OFF STACK
	pop	bc
	pop	de
			; GET DFACLO-DFACLO+3
	ex	de,hl		; (DE)=BUFFER PTR,(HL)=DFACLO
	ld	(DFACLO),hl
	ld	h,b
	ld	l,c
	ld	(DFACLO+2),hl
	ex	de,hl
	pop	bc
	pop	de
			; GET ORIGINAL FAC BACK
	call	MOVFR	; MOVE TO FAC
	inc	hl	; BECAUSE WHEN WE ORIGINALLY ENTERED
			; FOUFRV THE (HL) POINTED TO A CHAR.
			; PAST THE SIGN AND THE PASS THROUGH
			; THIS CODE LEAVES (HL) POINTING TO
			; THE SIGN. (HL) MUST POINT PAST SIGN!
FOUFRF:	
	cp	5	; SET CC'S FOR Z80
	push	hl	; SAVE THE BUFFER POINTER
	sbc	a,0	; MAP 4 TO 6 AND 10 TO 20
	rla		; THIS CALCULATES HOW MANY DIGITS WE WILL
	ld	d,a	;  PRINT
	inc	d
	call	FOUTNV	; NORMALIZE THE FAC SO ALL SIGNIFICANT DIGITS
			;  ARE IN THE INTEGER PART
	ld	bc,3*100h+0	; B = DECIMAL POINT COUNT
			; C = COMMA COUNT
			; SET COMMA COUNT TO ZERO AND DECIMAL POINT
			;  COUNT FOR E NOTATION

	push	af	; SAVE FOR NORMAL CASE
	ld	a,(FANSII)	; SEE IF FORCED FIXED OUTPUT
	or	a	; SET CONDITION CODES CORRECTLY
	jp	p,FOFV5A	; DO NORMAL THING
	pop	af
	add	a,d
	jp	FOUFV6	; FIXED OUTPUT
FOFV5A:	pop	af	; NORMAL ROUTE
	add	a,d	; SEE IF NUMBER SHOULD BE PRINTED IN E NOTATION
	jp	m,FOFRS1	; IT SHOULD, IT IS .LT. .01
	inc	d	; CHECK IF IT IS TOO BIG
	cp	d
	jp	nc,FOFRS1	; IT IS TOO BIG, IT IS .GT. 10^D-1
FOUFV6:	inc	a	; IT IS OK FOR FIXED POINT NOTATION
	ld	b,a	; SET DECIMAL POINT COUNT
	ld	a,2	; SET FIXED POINT FLAG, THE EXPONENT IS ZERO
			;  IF WE ARE USING FIXED POINT NOTATION
FOFRS1:	sub	2	; E NOTATION: ADD D-2 TO ORIGINAL EXPONENT
			; RESTORE EXP IF NOT D.P.
	pop	hl	; GET THE BUFFER POINTER BACK
	push	af	; SAVE THE EXPONENT FOR LATER
	call	FOUTAN	; .01 .LE. NUMBER .LT. .1?
	ld	(hl),'0'	; YES, PUT ".0" IN BUFFER
	call	z,INXHRT
	call	FOUTCV	; CONVERT THE NUMBER TO DECIMAL DIGITS
			; HERE TO SUPPRESS THE TRAILING ZEROS
FOFRS2:	dec	hl	; MOVE BACK TO THE LAST CHARACTER
	ld	a,(hl)	; GET IT AND SEE IF IT WAS ZERO
	cp	'0'
	jp	z,FOFRS2	; IT WAS, CONTINUE SUPPRESSING
	cp	'.'	; HAVE WE SUPPRESSED ALL THE FRACTIONAL DIGITS?
	call	nz,INXHRT	; YES, IGNORE THE DECIMAL POINT ALSO
	pop	af	; GET THE EXPONENT BACK
	jp	z,FOUTDN	; WE ARE DONE IF WE ARE IN FIXED POINT NOTATION
			; FALL IN AND PUT THE EXPONENT IN THE BUFFER


			; HERE TO PUT THE EXPONENT AND "E" OR "D" IN THE BUFFER
			; THE EXPONENT IS IN A, THE CONDITION CODES ARE ASSUMED TO BE SET
			; CORRECTLY.
FOFLDN:	push	af	; SAVE THE EXPONENT
	call	GETYPR	; SET CARRY FOR SINGLE PRECISION
	ld	a,22h	; [A]="D"/2
	adc	a,a	; MULTIPLY BY 2 AND ADD CARRY
	ld	(hl),a	; SAVE IT IN THE BUFFER
	inc	hl	; INCREMENT THE BUFFER POINTER
			; PUT IN THE SIGN OF THE EXPONENT
	pop	af	; GET THE EXPONENT BACK
	ld	(hl),'+'	; A PLUS IF POSITIVE
	jp	p,FOUCE1
	ld	(hl),'-'	; A MINUS IF NEGATIVE
	cpl		; NEGATE EXPONENT
	inc	a
			; CALCULATE THE TWO DIGIT EXPONENT
FOUCE1:	ld	b,'0'-1	; INITIALIZE TEN'S DIGIT COUNT
FOUCE2:	inc	b	; INCREMENT DIGIT
	sub	0ah	; SUBTRACT TEN
	jp	nc,FOUCE2	; DO IT AGAIN IF RESULT WAS POSITIVE
	add	a,'0'+0ah	; ADD BACK IN TEN AND CONVERT TO ASCII
			; PUT THE EXPONENT IN THE BUFFER
	inc	hl
	ld	(hl),b	; PUT TEN'S DIGIT OF EXPONENT IN BUFFER
	inc	hl	; WHEN WE JUMP TO HERE, A IS ZERO
	ld	(hl),a	; PUT ONE'S DIGIT IN BUFFER
FOUTZR:	inc	hl	; INCREMENT POINTER, HERE TO FINISH UP
			;  PRINTING A FREE FORMAT ZERO
FOUTDN:	ld	(hl),0	; PUT A ZERO AT THE END OF THE NUMBER
	ex	de,hl		; SAVE THE POINTER TO THE END OF THE NUMBER
			;  IN (DE) FOR FFXFLV
	ld	hl,FBUFFR+1	; GET A POINTER TO THE BEGINNING
	ret		; ALL DONE




			; HERE TO PRINT A NUMBER IN FIXED FORMAT
FOUTFX:	inc	hl	; MOVE PAST THE ZERO FOR THE DOLLAR SIGN
	push	bc	; SAVE THE FIELD LENGTH SPECIFICATIONS
	cp	4	; CHECK WHAT KIND OF VALUE WE HAVE
	ld	a,d	; GET THE FORMAT SPECS
	jp	nc,FOUFXV	; WE HAVE A SNG OR A DBL
			; HERE TO PRINT AN INTEGER IN FIXED FORMAT
	rra		; CHECK IF WE HAVE TO PRINT IT IN FLOATING
	jp	c,FFXIFL	;  POINT NOTATION
			; HERE TO PRINT AN INTEGER IN FIXED FORMAT-FIXED POINT NOTATION
	ld	bc,6*100h+3+0	; SET DECIMAL POINT COUNT TO 6 AND
			;  COMMA COUNT TO 3
	call	FOUICC	; CHECK IF WE DON'T HAVE TO USE THE COMMAS
	pop	de	; GET THE FIELD LENGTHS
	ld	a,d	; SEE IF WE HAVE TO PRINT EXTRA SPACES BECAUSE
	sub	5	;  THE FIELD IS TOO BIG
	call	p,FOTZER	; WE DO, PUT IN ZEROS, THEY WILL LATER BE
			;  CONVERTED TO SPACES OR ASTERISKS BY FOUTZS
	call	FOUTCI	; CONVERT THE NUMBER TO DECIMAL DIGITS
FOUTTD:	ld	a,e	; DO WE NEED A DECIMAL POINT?
	or	a
	call	z,DCXHRT	; WE DON'T, BACKSPACE OVER IT.
	dec	a	; GET HOW MANY TRAILING ZEROS TO PRINT
	call	p,FOTZER	; PRINT THEM
			; IF WE DO HAVE DECIMAL PLACES, FILL THEM UP
			;  WITH ZEROS
			; FALL IN AND FINISH UP THE NUMBER


			; HERE TO FINISH UP A FIXED FORMAT NUMBER
FOUTTS:	push	hl	; SAVE BUFFER POINTER
	call	FOUTZS	; ZERO SUPPRESS THE NUMBER
	pop	hl	; GET THE BUFFER POINTER BACK
	jp	z,FFXIX1	; CHECK IF WE HAVE A TRAILING SIGN
	ld	(hl),b	; WE DO, PUT THE SIGN IN THE BUFFER
	inc	hl	; INCREMENT THE BUFFER POINTER
FFXIX1:	ld	(hl),0	; PUT A ZERO AT THE END OF THE NUMBER


			; HERE TO CHECK IF A FIXED FORMAT-FIXED POINT NUMBER OVERFLOWED ITS
			; FIELD LENGTH
			; D = THE B IN THE FORMAT SPECIFICATION
			; THIS ASSUMES THE LOCATION OF THE DECIMAL POINT IS IN TEMP2
	ld	hl,FBUFFR	; GET A POINTER TO THE BEGINNING
FOUBE1:	inc	hl	; INCREMENT POINTER TO THE NEXT CHARACTER
FOUBE5:	ld	a,(TEMP2)	; GET THE LOCATION OF THE DECIMAL POINT
			; SINCE FBUFFR IS ONLY 35 (DECIMAL) LONG, WE
			;  ONLY HAVE TO LOOK AT THE LOW ORDER TO SEE
			;  IF THE FIELD IS BIG ENOUGH
	sub	l	; FIGURE OUT HOW MUCH SPACE WE ARE TAKING
	sub	d	; IS THIS THE RIGHT AMOUNT OF SPACE TO TAKE?
	ret	z		; YES, WE ARE DONE, RETURN FROM FOUT
	ld	a,(hl)	; NO, WE MUST HAVE TOO MUCH SINCE WE STARTED
			;  CHECKING FROM THE BEGINNING OF THE BUFFER
			;  AND THE FIELD MUST BE SMALL ENOUGH TO FIT IN
			;  THE BUFFER. GET THE NEXT CHARACTER IN
			;  THE BUFFER.
	cp	' '	; IF IT IS A SPACE OR AN ASTERISK, WE CAN
	jp	z,FOUBE1	;  IGNORE IT AND MAKE THE FIELD SHORTER WITH
	cp	'*'	;  NO ILL EFFECTS
	jp	z,FOUBE1
	dec	hl	; MOVE THE POINTER BACK ONE TO READ THE
			;  CHARACTER WITH CHRGET
	push	hl	; SAVE THE POINTER

			; HERE WE SEE IF WE CAN IGNORE THE LEADING ZERO BEFORE A DECIMAL POINT.
			; THIS OCCURS IF WE SEE THE FOLLOWING: (IN ORDER)
			;  +,- A SIGN (EITHER "-" OR "+") [OPTIONAL]
			;  $ A DOLLAR SIGN [OPTIONAL]
			;  0 A ZERO [MANDATORY]
			;  . A DECIMAL POINT [MANDATORY]
			;  0-9 ANOTHER DIGIT [MANDATORY]
			; IF YOU SEE A LEADING ZERO, IT MUST BE THE ONE BEFORE A DECIMAL POINT
			; OR ELSE FOUTZS WOULD HAVE SUPPRESSED IT, SO WE CAN JUST "INX H"
			; OVER THE CHARACTER FOLLOWING THE ZERO, AND NOT CHECK FOR THE
			; DECIMAL POINT EXPLICITLY.
FOUBE2:	push	af	; PUT THE LAST CHARACTER ON THE STACK. THE
			;  ZERO FLAG IS SET. THE FIRST TIME THE ZERO
			;  ZERO FLAG IS NOT SET.
	ld	bc,FOUBE2	; GET ADDRESS WE GO TO IF WE SEE A CHARACTER
	push	bc	;  WE ARE LOOKING FOR
	call	CHRGTR	; GET THE NEXT CHARACTER
	cp	'-'	; SAVE IT AND GET THE NEXT CHARACTER IF IT IS
	ret	z		;  A MINUS SIGN, A PLUS SIGN OR A DOLLAR SIGN
	cp	'+'
	ret	z
	cp	CURNCY
	ret	z
	pop	bc	; IT ISN'T, GET THE ADDRESS OFF THE STACK
	cp	'0'	; IS IT A ZERO?
	jp	nz,FOUBE4	; NO, WE CAN NOT GET RID OF ANOTHER CHARACTER
	inc	hl	; SKIP OVER THE DECIMAL POINT
	call	CHRGTR	; GET THE NEXT CHARACTER
	jp	nc,FOUBE4	; IT IS NOT A DIGIT, WE CAN'T SHORTEN THE FIELD
	dec	hl	; WE CAN!!! POINT TO THE DECIMAL POINT
	defb	001	; "LXI B" OVER THE NEXT 2 BYTES
FOUBE3:	dec	hl	; POINT BACK ONE CHARACTER
	ld	(hl),a	; PUT THE CHARACTER BACK

			; IF WE CAN GET RID OF THE ZERO, WE PUT THE CHARACTERS ON THE STACK
			; BACK INTO THE BUFFER ONE POSITION IN FRONT OF WHERE THEY ORIGINALLY
			; WERE. NOTE THAT THE MAXIMUM NUMBER OF STACK LEVELS THIS USES IS
			; THREE -- ONE FOR THE LAST ENTRY FLAG, ONE FOR A POSSIBLE SIGN,
			; AND ONE FOR A POSSIBLE DOLLAR SIGN. WE DON'T HAVE TO WORRY ABOUT
			; THE FIRST CHARACTER BEING IN THE BUFFER TWICE BECAUSE THE POINTER
			; WHEN FOUT EXITS WILL BE POINTING TO THE SECOND OCCURANCE.
	pop	af	; GET THE CHARACTER OFF THE STACK
	jp	z,FOUBE3	; PUT IT BACK IN THE BUFFER IF IT IS NOT THE
			;  LAST ONE
	pop	bc	; GET THE BUFFER POINTER OFF THE STACK
	jp	FOUBE5	; SEE IF THE FIELD IS NOW SMALL ENOUGH
			; HERE IF THE NUMBER IS TOO BIG FOR THE FIELD
FOUBE4:	pop	af	; GET THE CHARACTERS OFF THE STACK
	jp	z,FOUBE4	; LEAVE THE NUMBER IN THE BUFFER ALONE
	pop	hl	; GET THE POINTER TO THE BEGINNING OF THE
			;  NUMBER MINUS 1
	ld	(hl),'%'	; PUT IN A PERCENT SIGN TO INDICATE THE NUMBER
			;  WAS TOO LARGE FOR THE FIELD
	ret		; ALL DONE -- RETURN FROM FOUT


			; HERE TO PRINT A SNG OR DBL IN FIXED FORMAT
FOUFXV:	push	hl	; SAVE THE BUFFER POINTER
	rra		; GET FIXED OR FLOATING NOTATION FLAG IN CARRY
	jp	c,FFXFLV	; PRINT THE NUMBER IN E-NOTATION
	jp	z,FFXSFX	; WE HAVE A SNG
			; HERE TO PRINT A DBL IN FIXED FORMAT--FIXED POINT NOTATION
	ld	de,FFXDXM	; GET POINTER TO 1D16
	call	DCOMPD	; WE CAN'T PRINT A NUMBER .GE. 10^16 IN FIXED
			;  POINT NOTATION
	ld	d,10h	; SET D = NUMBER OF DIGITS TO PRINT FOR A DBL
	jp	m,FFXSDC	; IF THE FAC WAS SMALL ENOUGH, GO PRINT IT
			; HERE TO PRINT IN FREE FORMAT WITH A PERCENT SIGN A NUMBER .GE. 10^16
FFXSDO:	pop	hl	; GET THE BUFFER POINTER OFF THE STACK
	pop	bc	; GET THE FIELD SPECIFICATION OFF THE STACK
	call	FOUT	; PRINT THE NUMBER IN FREE FORMAT
	dec	hl	; POINT TO IN FRONT OF THE NUMBER
	ld	(hl),'%'	; PUT IN THE PERCENT SIGN
	ret		; ALL DONE--RETURN FROM FOUT

			; HERE TO PRINT A SNG IN FIXED FORMAT--FIXED POINT NOTATION
FFXSFX:	ld	bc,00b6h*256+000eh
	ld	de,001bh*256+00cah	; GET 1E16, CHECK IF THE NUMBER IS TOO BIG
	call	FCOMP
	jp	p,FFXSDO	; IT IS, PRINT IT IN FREE FORMAT WITH A % SIGN
	ld	d,6	; D = NUMBER OF DIGITS TO PRINT IN A SNG

			; HERE TO ACTUALLY PRINT A SNG OR DBL IN FIXED FORMAT
FFXSDC:	call	SIGN	; SEE IF WE HAVE ZERO
	call	nz,FOUTNV	; IF NOT, NORMALIZE THE NUMBER SO ALL DIGITS TO
			;  BE PRINTED ARE IN THE INTEGER PART
	pop	hl	; GET THE BUFFER POINTER
	pop	bc	; GET THE FIELD LENGTH SPECS
	jp	m,FFXXVS	; DO DIFFERENT STUFF IF EXPONENT IS NEGATIVE
			; HERE TO PRINT A NUMBER WITH NO FRACTIONAL DIGITS
FFXSD2:	push	bc	; SAVE THE FIELD LENGTH SPECS AGAIN
	ld	e,a	; SAVE THE EXPONENT IN E
	ld	a,b	; WE HAVE TO PRINT LEADING ZEROS IF THE FIELD
	sub	d	;  HAS MORE CHARACTERS THAN THERE ARE DIGITS
	sub	e	;  IN THE NUMBER.
			; IF WE ARE USING COMMAS, A MAY BE TOO BIG.
			; THIS DOESN'T MATTER BECAUSE FOUTTS WILL FIND
			;  THE CORRECT BEGINNING. THERE IS ROOM IN
			;  FBUFFR BECAUSE THE MAXIMUM VALUE B CAN BE IS
			;  24 (DECIMAL) SO D+C .LE. 16 (DECIMAL) SINCE
			;  FAC .LT. 10^16.
			; SO WE NEED 8 MORE BYTES FOR ZEROS. 4 COME
			;  SINCE WE WILL NOT NEED TO PRINT AN EXPONENT.
			;  FBUFFR ALSO CONTAINS AN EXTRA 4 BYTES FOR
			;  THIS CASE.
			; (IT WOULD TAKE MORE THAN 4 BYTES TO CHECK FOR
			;  THIS.)
	call	p,FOTZER	; FOUTZS WILL LATER SUPPRESS THEM
	call	FOUTCD	; SETUP DECIMAL POINT AND COMMA COUNT
	call	FOUTCV	; CONVERT THE NUMBER TO DECIMAL DIGITS
	or	e	; PUT IN DIGITS AFTER THE NUMBER IF IT
			;  IS BIG ENOUGH, HERE A=0
	call	nz,FOTZEC	; THERE CAN BE COMMAS IN THESE ZEROS
	or	e	; MAKE SURE WE GET A DECIMAL POINT FOR FOUTTS
	call	nz,FOUTED
	pop	de	; GET THE FIELD LENGTH SPECS
	jp	FOUTTD	; GO CHECK THE SIZE, ZERO SUPPRESS, ETC. AND
			;  FINISH THE NUMBER

			; HERE TO PRINT A SNG OR DBL THAT HAS FRACTIONAL DIGITS
FFXXVS:	ld	e,a	; SAVE THE EXPONENT
	ld	a,c	; DIVIDE BY TEN THE RIGHT NUMBER OF TIMES SO
	or	a	;  THE RESULT WILL BE ROUNDED CORRECTLY AND
	call	nz,DCRART	;  HAVE THE CORRECT NUMBER OF SIGNIFICANT
	add	a,e	;  DIGITS
FFXXS2:	jp	m,FFXXV8	; FOR LATER CALCULATIONS, WE WANT A ZERO IF THE
	xor	a	;  RESULT WAS NOT NEGATIVE
FFXXV8:	push	bc	; SAVE THE FIELD SPECS
	push	af	; SAVE THIS NUMBER FOR LATER
FFXXV2:	call	m,FINDIV	; THIS IS THE DIVIDE LOOP
	jp	m,FFXXV2
	pop	bc	; GET THE NUMBER WE SAVED BACK IN B
	ld	a,e	; WE HAVE TWO CASES DEPENDING ON WHETHER THE
	sub	b	;  THE NUMBER HAS INTEGER DIGITS OR NOT
	pop	bc	; GET THE FILED SPECS BACK
	ld	e,a	; SAVE HOW MANY DECIMAL PLACES BEFORE THE
	add	a,d	;  THE NUMBER ENDS
FFXXS4:	ld	a,b	; GET THE "B" FIELD SPEC
	jp	m,FFXXV3
			; HERE TO PRINT NUMBERS WITH INTEGER DIGITS
	sub	d	; PRINT SOME LEADING ZEROS IF THE FIELD IS
	sub	e	;  BIGGER THAN THE NUMBER OF DIGITS WE WILL
FFXXS6:	call	p,FOTZER	;  PRINT
	push	bc	; SAVE FIELD SPEC
	call	FOUTCD	; SET UP DECIMAL POINT AND COMMA COUNT
	jp	FFXXV6	; CONVERT THE DIGITS AND DO THE TRIMMING UP

			; HERE TO PRINT A NUMBER WITHOUT INTEGER DIGITS
FFXXV3:	call	FOTZER	; PUT ALL ZEROS BEFORE THE DECIMAL POINT
	ld	a,c	; SAVE C
	call	FOUTDP	; PUT IN A DECIMAL POINT
	ld	c,a	; RESTORE C
	xor	a	; DECIDE HOW MANY ZEROS TO PRINT BETWEEN THE
	sub	d	;  DECIMAL POINT AND THE FIRST DIGIT WE WILL
	sub	e	;  PRINT.
	call	FOTZER	; PRINT THE ZEROS
	push	bc	; SAVE EXPONENT AND THE "C" IN THE FIELD SPEC
	ld	b,a	; ZERO THE DECIMAL PLACE COUNT
	ld	c,a	; ZERO THE COMMA COUNT
FFXXV6:	call	FOUTCV	; CONVERT THE NUMBER TO DECIMAL DIGITS
	pop	bc	; GET THE FIELD SPECS BACK
	or	c	; CHECK IF WE HAVE TO PRINT ANY ZEROS AFTER
			;  THE LAST DIGIT
	jp	nz,FFXXV7	; CHECK IF THERE WERE ANY DECIMAL PLACES AT ALL
			; E CAN NEVER BE 200, (IT IS NEGATIVE) SO IF
			;  A=0 HERE, THERE IS NO WAY WE WILL CALL FOTZER
	ld	hl,(TEMP2)	; THE END OF THE NUMBER IS WHERE THE DP IS
FFXXV7:	add	a,e	; PRINT SOME MORE TRAILING ZEROS
	dec	a
	call	p,FOTZER
	ld	d,b	; GET THE "B" FIELD SPEC IN D FOR FOUTTS
	jp	FOUTTS	; FINISH UP THE NUMBER


			; HERE TO PRINT AN INTEGER IN FIXED FORMAT--FLOATING POINT NOTATION
FFXIFL:	push	hl	; SAVE THE BUFFER POINTER
	push	de	; SAVE THE FORMAT SPECS
	call	CONSI	; CONVERT THE INTEGER TO A SNG
	pop	de	; GET THE FORMAT SPECS BACK
	xor	a	; SET FLAGS TO PRINT THE NUMBER AS A SNG
			; FALL INTO FFXFLV


			; HERE TO PRINT A SNG OR DBL IN FIXED FORMAT-FLOTATING POINT NOTATION
FFXFLV:	jp	z,FFXSFL	; IF WE HAVE A SNG, SET THE RIGHT FLAGS
	ld	e,10h	; WE HAVE A DBL, GET HOW MANY DIGITS WE HAVE
	defb	001	; "LXI B" OVER THE NEXT TWO BYTES
FFXSFL:	ld	e,6	; WE HAVE A SNG, GET HOW MANY DIGITS WE PRINT
	call	SIGN	; SEE IF WE HAVE ZERO
FFXS03:	scf		; SET CARRY TO DETERMINE IF WE ARE PRINTING
			; ZERO. NOTE: THIS DEPENDS ON THE FACT THAT
			; FOUTNV EXITS WITH CARRY OFF
	call	nz,FOUTNV	; IF NOT, NORMALIZE THE NUMBER SO ALL DIGITS TO
			;  BE PRINTED ARE IN THE INTEGER PART
	pop	hl	; GET THE BUFFER POINTER BACK
	pop	bc	; GET THE FIELD LENGTH SPECS
	push	af	; SAVE THE EXPONENT
	ld	a,c	; CALCULATE HOW MANY SIGNIFICANT DIGITS WE MUST
	or	a	;  PRINT
	push	af	; SAVE THE "C" FIELD SPEC FOR LATER
	call	nz,DCRART
	add	a,b
	ld	c,a
	ld	a,d	; GET THE "A" FIELD SPEC
	and	4	; SEE IF THE SIGN IS A TRAILING SIGN
	cp	1	; SET CARRY IF A IS ZERO
	sbc	a,a	; SET D=0 IF WE HAVE A TRAILING SIGN,
	ld	d,a	;  D=377 IF WE DO NOT
	add	a,c
	ld	c,a	; SET C=NUMBER OF SIGNIFICANT DIGITS TO PRINT
	sub	e	; IF WE HAVE LESS THAN E, THEN WE MUST GET RID
	push	af	; SAVE COMPARISON # OF SIG DIGITS AND THE
			; # OF DIGITS WE WILL PRINT
	push	bc	; SAVE THE "B" FIELD SPEC AND # OF SIG DIGITS
FFXLV1:	call	m,FINDIV	;  OF SOME BY DIVIDING BY TEN AND ROUNDING
	jp	m,FFXLV1
	pop	bc	; GET "B" FIELD SPEC AND # OF SIG DIGITS BACK
	pop	af	; GET # OF TRAILING ZEROS TO PRINT
	push	bc	; SAVE THE "B" FIELD SPEC AND # OF SIG DIGITS
	push	af	; SAVE # OF TRAILING ZEROS TO PRINT
	jp	m,FFXLV3	; TAKE INTO ACCOUNT DIGITS THAT WERE
	xor	a	; DIVIDED OFF AT FFXLV1
FFXLV3:	cpl
	inc	a
	add	a,b	; SET THE DECIMAL PLACE COUNT
	inc	a
	add	a,d	; TAKE INTO ACCOUNT IF THE SIGN IS TRAILING
	ld	b,a	;  OR NOT
	ld	c,0	; SET COMMA COUNT TO ZERO, THE COMMA SPEC IS
			;  IGNORED.
	call	FOUTCV	; CONVERT THE NUMBER TO DECIMAL DIGITS
	pop	af	; GET NUMBER TRAILING ZEROS TO PRINT
			; IF THE FIELD LENGTH IS LONGER THAN THE # OF DIGITS
			; WE CAN PRINT
	call	p,FOTZNC	; THE DECIMAL POINT COULD COME OUT IN HERE
	call	FOUTED	; IN CASE D.P. IS LAST ON LIST
	pop	bc	; GET # OF SIG DIGITS AND "B" FIELD SPAC BACK
	pop	af	; GET THE "C" FIELD SPEC BACK
	jp	nz,FFXLV4	; IF NON-ZERO PROCEED
	call	DCXHRT	; SEE IF D.P. THERE
	ld	a,(hl)	; FETCH TO MAKE SURE D.P.
	cp	'.'	; IF NOT MUST BE ZERO
	call	nz,INXHRT	; IF NOT MUST LEAVE AS IS
	ld	(TEMP2),hl	; NEED D.P. LOCATION IN TEMP2
FFXLV4:			;  SO IGNORE IT.
	pop	af	; GET THE EXPONENT BACK
	jp	c,FFXLV2	; EXPONENT=0 IF THE NUMBER IS ZERO
	add	a,e	; SCALE IT CORRECTLY
	sub	b
	sub	d
FFXLV2:	push	bc	; SAVE THE "B" FIELD SPEC
	call	FOFLDN	; PUT THE EXPONENT IN THE BUFFER
	ex	de,hl		; GET THE POINTER TO THE END IN (HL)
			;  IN CASE WE HAVE A TRAILING SIGN
	pop	de	; GET THE "B" FIELD SPEC IN D, PUT ON A
	jp	FOUTTS	;  POSSIBLE TRAILING SIGN AND WE ARE DONE


			; NORMALIZE THE NUMBER IN THE FAC SO ALL THE DIGITS ARE IN THE INTEGER
			; PART. RETURN THE BASE 10 EXPONENT IN A
			; D,E ARE LEFT UNALTERED
FOUTNV:	push	de	; SAVE (DE)
	xor	a	; ZERO THE EXPONENT
	push	af	; SAVE IT
	call	GETYPR	; GET TYPE OF NUMBER TO BE PRINTED
	jp	po,FOUNDB	; NOT DOUBLE, DO NORMAL THING
FORBIG:	ld	a,(FAC)	; GET EXPONENT
	cp	91h	; IS IT .LT.1D5?
	jp	nc,FOUNDB	; NO, DONT MULTPLY
	ld	de,TENTEN	; MULTIPLY BY 1D10
	ld	hl,ARGLO	; MOVE INTO ARG
	call	VMOVE	; PUT IN ARG
	call	DMULT	; MULTIPLY BY IT
	pop	af	; GET ORIG EXPONENT OFF STACK
	sub	10	; GET PROPER OFFSET FOR EXPONENT
	push	af	; SAVE EXPONENT BACK
	jp	FORBIG	; FORCE IT BIGGER IF POSSIBLE
FOUNDB:	call	FOUNVC	; IS THE FAC TOO BIG OR TOO SMALL?
FOUNV1:	call	GETYPR	; SEE WHAT KIND OF VALUE WE HAVE SO WE
			;  CAN SEE IF THE FAC IS BIG ENOUGH
	jp	pe,FOUNV4	; WE HAVE A DBL
	ld	bc,0091h*256+0043h
	ld	de,004fh*256+00f9h	; GET 99999.95 TO SEE IF THE FAC IS BIG
	call	FCOMP	;  ENOUGH YET
	jp	FOUNV5	; GO DO THE CHECK
FOUNV4:	ld	de,FOUTDL	; GET POINTER TO 999,999,999,999,999.5
	call	DCOMPD	; SEE IF THE NUMBER IS STILL TOO SMALL
FOUNV5:	jp	p,FOUNV3	; IT ISN'T ANY MORE, WE ARE DONE
	pop	af	; IT IS, MULTIPLY BY TEN
	call	FINMLT
	push	af	; SAVE THE EXPONENT AGAIN
	jp	FOUNV1	; NOW SEE IF IT IS BIG ENOUGH
FOUNV2:	pop	af	; THE FAC IS TOO BIG, GET THE EXPONENT
	call	FINDIV	; DIVIDE IT BY TEN
	push	af	; SAVE THE EXPONENT AGAIN
	call	FOUNVC	; SEE IF THE FAC IS SMALL ENOUGH
FOUNV3:	pop	af	; WE ARE DONE, GET THE EXPONENT BACK
	or	a	; CLEAR CARRY
	pop	de	; GET (DE) BACK
	ret		; ALL DONE

			; HERE TO SEE IF THE FAC IS SMALL ENOUGH YET
FOUNVC:	call	GETYPR	; SEE WHAT TYPE NUMBER WE HAVE
	jp	pe,FONVC1	; WE HAVE A DBL
	ld	bc,0094h*256+0074h
	ld	de,0023h*256+00f8h	; GET 999999.5 TO SEE IF THE FAC IS TOO BIG
	call	FCOMP
	jp	FONVC2	; GO DO THE CHECK
FONVC1:	ld	de,FOUTDU	; GET POINTER TO 9,999,999,999,999,999.5
	call	DCOMPD	; SEE IF THE NUMBER IS TOO BIG
FONVC2:	pop	hl	; GET THE RETURN ADDRESS OFF THE STACK
	jp	p,FOUNV2	; THE NUMBER IS TOO BIG, DIVIDE IT BY TEN
	jp	(hl)		; IT ISN'T TOO BIG, JUST RETURN


			; HERE TO PUT SOME ZEROS IN THE BUFFER
			; THE COUNT IS IN A, IT CAN BE ZERO, BUT THE ZERO FLAG MUST BE SET
			; ONLY (HL) AND A ARE ALTERED
			; WE EXIT WITH A=0
FOTZER:	or	a	; THIS IS BECAUSE FFXXV3 CALL US WITH THE
			;  CONDITION CODES NOT SET UP
FOTZR1:	ret	z		; RETURN IF WE ARE DONE
	dec	a	; WE ARE NOT DONE, SO DECREMENT THE COUNT
	ld	(hl),'0'	; PUT A ZERO IN THE BUFFER
	inc	hl	; UPDATE THE BUFFER POINTER
	jp	FOTZR1	; GO SEE IF WE ARE NOW DONE


			; HERE TO PUT ZEROS IN THE BUFFER WITH COMMAS OR A DECIMAL POINT IN THE
			; MIDDLE. THE COUNT IS IN A, IT CAN BE ZERO, BUT THE ZERO FLAG MUST BE
			; SET. B THE DECIMAL POINT COUNT AND C THE COMMA COUNT ARE UPDATED
			; A,B,C,H,L ARE ALTERED
FOTZNC:	jp	nz,FOTZEC	; ENTRY AFTER A "CALL FOUTCV"
FOTZRC:	ret	z		; RETURN IF WE ARE DONE
	call	FOUTED	; SEE IF WE HAVE TO PUT A COMMA OR A DECIMAL
			;  POINT BEFORE THIS ZERO
FOTZEC:	ld	(hl),'0'	; PUT A ZERO IN THE BUFFER
	inc	hl	; UPDATE THE BUFFER POINTER
	dec	a	; DECREMENT THE ZERO COUNT
	jp	FOTZRC	; GO BACK AND SEE IF WE ARE DONE


			; HERE TO PUT A POSSIBLE COMMA COUNT IN C, AND ZERO C IF WE ARE NOT
			; USING THE COMMA SPECIFICATION
FOUTCD:	ld	a,e	; SETUP DECIMAL POINT COUNT
	add	a,d
	inc	a
	ld	b,a
	inc	a	; SETUP COMMA COUNT
FOTCD1:	sub	3	; REDUCE [A] MOD 3
	jp	nc,FOTCD1
	add	a,5	; ADD 3 BACK IN AND ADD 2 MORE FOR
			; SCALING
FOUTCC:	ld	c,a	; SAVE A POSSIBLE COMMA COUNT
FOUICC:	ld	a,(TEMP3)	; GET THE FORMAT SPECS
	and	40h	; LOOK AT THE COMMA BIT
	ret	nz		; WE ARE USING COMMAS, JUST RETURN
	ld	c,a	; WE AREN'T, ZERO THE COMMA COUNT
	ret		; ALL DONE


			; HERE TO PUT DECIMAL POINTS AND COMMAS IN THEIR CORRECT PLACES
			; THIS SUBROUTINE SHOULD BE CALLED BEFORE THE NEXT DIGIT IS PUT IN THE
			; BUFFER. B=THE DECIMAL POINT COUNT, C=THE COMMA COUNT
			; THE COUNTS TELL HOW MANY MORE DIGITS HAVE TO GO IN BEFORE THE COMMA
			; OR DECIMAL POINT GO IN. THE COMMA OR DECIMAL POINT THEN GOES BEFORE
			; THE LAST DIGIT IN THE COUNT. FOR EXAMPLE, IF THE DECIMAL POINT SHOULD
			; COME AFTER THE FIRST DIGIT, THE DECIMAL POINT COUNT SHOULD BE 2.
FOUTAN:			; SAVE FOR LATER
	dec	b
	jp	p,FOUTE1	; PROCESS AS NORMAL
	ld	(TEMP2),hl	; SAVE LOCATION OF DECIMAL POINT
	ld	(hl),'.'	; PUT IN D.P.
FOUTD1:	inc	hl	; POINT TO NEXT BUFFER POSTION
	ld	(hl),'0'
	inc	b
	jp	nz,FOUTD1
	inc	hl	; POINT TO NEXT AVAILABLE BUFFER LOCATION
	ld	c,b
	ret
FOUTED:	dec	b	; TIME FOR D.P.?
FOUTE1:	
	jp	nz,FOUED1	; NO, CHECK FOR THE COMMA
			; ENTRY TO PUT A DECIMAL POINT IN THE BUFFER
FOUTDP:	ld	(hl),'.'	; YES, PUT THE DECIMAL POINT IN
	ld	(TEMP2),hl	; SAVE THE LOCATION OF THE DECIMAL POINT
	inc	hl	; INCREMENT THE BUFFER POINTER
	ld	c,b	; PUT ZERO IN C SO WE WON"T PRINT ANY COMMAS
	ret		;  AFTER THE DECIMAL POINT. ALL DONE
			; HERE TO SEE IF IT IS TIME TO PRINT A COMMA
FOUED1:	dec	c	; IS IT TIME?
	ret	nz		; NOPE, WE CAN RETURN
	ld	(hl),2ch	; ","=54, YES, PUT A COMMA IN THE BUFFER
	inc	hl	; INCREMENT THE BUFFER POINTER
	ld	c,3	; RESET THE COMMA COUNT SO WE WILL PRINT A
	ret		;  COMMA AFTER THREE MORE DIGITS. ALL DONE


			; HERE TO CONVERT A SNG OR DBL NUMBER THAT HAS BEEN NORMALIZED TO
			; DECIMAL DIGITS. THE DECIMAL POINT COUNT AND COMMA COUNT ARE IN B AND
			; C RESPECTIVELY. (HL) POINTS TO WHERE THE FIRST DIGIT WILL GO.
			; THIS EXITS WITH A=0. (DE) IS LEFT UNALTERED.
FOUTCV:	push	de	; SAVE (DE)
	call	GETYPR	; SEE WHAT KIND OF A NUMBER WE HAVE
	jp	po,FOUTCS	; WE HAVE A SNG
			; HERE TO CONVERT A DOUBLE PRECISION NUMBER TO DECIMAL DIGITS
	push	bc	; SAVE THE DECIMAL POINT AND COMMA COUNTS
	push	hl	; SAVE THE BUFFER POINTER
	call	VMOVAF	; MOVE THE FAC INTO ARG
	ld	hl,DHALF	; GET POINTER TO .5D0
	call	VMOVFM	; MOVE THE CONSTANT INTO THE FAC
	call	DADD	; ADD .5 TO THE ORIGINAL NUMBER TO ROUND IT
	xor	a	; CLEAR THE CARRY
	call	DINTFO	; TAKE THE INTEGER PART OF THE NUMBER
			; THE NUMBER IS NOT NORMALIZED AFTERWARDS
	pop	hl	; GET THE BUFFER POINTER BACK
	pop	bc	; GET THE COMMA AND DECIMAL POINT COUNTS BACK
	ld	de,FODTBL	; GET A POINTER TO THE DBL POWER OF TEN TABLE
	ld	a,0ah	; CONVERT TEN DIGITS, THE OTHERS WILL BE
			;  CONVERTED AS SNG'S AND INT'S
			; BECAUSE WE BRACKETED THE NUMBER A
			; POWER OF TEN LESS IN MAGNITUDE AND
			; SINGLE PRECISION CONVERSION CAN HANDLE
			; A MAGNITUDE OF TEN LARGER
			; HERE TO CONVERT THE NEXT DIGIT
FOUCD1:	call	FOUTED	; SEE IF WE HAVE TO PUT IN A DP OR COMMA
	push	bc	; SAVE DP AND COMMA INFORMATION
	push	af	; SAVE DIGIT COUNT
	push	hl	; SAVE BUFFER POINTER
	push	de	; SAVE POWER OF TEN POINTER
			; HERE TO DIVIDE FOR THE NEXT DIGIT
	ld	b,'0'-1	; SET UP THE COUNT FOR THE DIGIT
FOUCD2:	inc	b	; INCREMENT THE DIGIT COUNT
	pop	hl	; GET THE POINTER TO THE POWER OF TEN
	push	hl	; SAVE IT AGAIN
	defb	3eh	; "MVI A", GET THE INSTRUCTION TO SUBTRACT
	sbc	a,(hl)	;  THE POWER OF TEN
	call	DADDFO	; GO SUBTRACT THEM
	jp	nc,FOUCD2	; IF THE NUMBER WAS NOT LESS THAN THE POWER OF
			;  TEN, SUBTRACT AGAIN
	pop	hl	; WE ARE DONE SUBTRACTING, BUT WE DID IT ONCE
			;  TOO OFTEN, SO ADD BACK IN THE POWER OF TEN
			; GET THE POINTER TO THE POWER OF TEN
	defb	3eh	; "MVI A", GET THE INSTRUCTION TO ADD THE
	adc	a,(hl)	;  POWER OF TEN AND THE NUMBER
	call	DADDFO	; ADD THE TWO NUMBERS
	ex	de,hl		; PUT THE POWER OF TEN POINTER IN (DE). IT IS
			;  UPDATED FOR THE NEXT POWER OF TEN
	pop	hl	; GET THE BUFFER POINTER BACK
	ld	(hl),b	; PUT THE DIGIT INTO THE BUFFER
	inc	hl	; INCREMENT THE BUFFER POINTER
	pop	af	; GET THE DIGIT COUNT BACK
	pop	bc	; GET THE DECIMAL POINT AND COMMA COUNTS
	dec	a	; HAVE WE PRINTED THE LAST DIGIT?
	jp	nz,FOUCD1	; NO, GO DO THE NEXT ONE
	push	bc	; YES, CONVERT REMAINING DIGITS USING SINGLE
	push	hl	;  PRECISION, THIS IS FASTER, MOVE THE NUMBER
	ld	hl,DFACLO	;  THAT IS LEFT INTO THE SNG FAC
	call	MOVFM
	jp	FOUCDC	; GO TO IT!!

			; HERE TO CONVERT A SINGLE PRECISION NUMBER TO DECIMAL DIGITS
FOUTCS:	push	bc	; SAVE THE DECIMAL POINT AND COMMA COUNTS
	push	hl	; SAVE THE BUFFER POINTER
	call	FADDH	; ROUND NUMBER TO NEAREST INTEGER
	ld	a,1	; MAKE A NON-ZERO, SINCE NUMBER IS POSITIVE
			;  AND NON-ZERO, ROUND WILL EXIT WITH THE HO
			;  IN A, SO THE MSB WILL ALWAYS BE ZERO AND
			;  ADDING ONE WILL NEVER CAUSE A TO BE ZERO
	call	QINT	; GET INTEGER PART IN C,D,E
	call	MOVFR	; SAVE NUMBER IN FAC
FOUCDC:	pop	hl	; GET THE BUFFER POINTER BACK
	pop	bc	; GET THE DECIMAL POINT AND COMMA COUNTS BACK
	xor	a	; CLEAR CARRY, THE CARRY IS OUR FLAG TO
			;  CALCULATE TWO DIGITS
	ld	de,FOSTBL	; GET POINTER TO POWER OF TEN TABLE
			; HERE TO CALCULATE THE NEXT DIGIT OF THE NUMBER
FOUCS1:	ccf		; COMPLEMENT FLAG THAT TELLS WHEN WE ARE DONE
	call	FOUTED	; SEE IF A COMMA OR DP GOES BEFORE THIS DIGIT
	push	bc	; SAVE COMMA AND DECIMAL POINT INFORMATION
	push	af	; SAVE CARRY I.E. DIGIT COUNT
	push	hl	; SAVE CHARACTER POINTER
	push	de	; SAVE POWER OF TEN POINTER
	call	MOVRF	; GET NUMBER IN C,D,E
	pop	hl	; GET POWER OF TEN POINTER
	ld	b,'0'-1	; B = NEXT DIGIT TO BE PRINTED
FOUCS2:	inc	b	; ADD ONE TO DIGIT
	ld	a,e	; SUBTRACT LO
	sub	(hl)
	ld	e,a
	inc	hl	; POINT TO NEXT BYTE OF POWER OF TEN
	ld	a,d	; SUBTRACT MO
	sbc	a,(hl)
	ld	d,a
	inc	hl
	ld	a,c	; SUBTRACT HO
	sbc	a,(hl)
	ld	c,a
	dec	hl	; POINT TO BEGINNING OF POWER OF TEN
	dec	hl
	jp	nc,FOUCS2	; SUBTRACT AGAIN IF RESULT WAS POSITIVE
	call	FADDA	; IT WASN'T, ADD POWER OF TEN BACK IN
	inc	hl	; INCREMENT POINTER TO NEXT POWER OF TEN
	call	MOVFR	; SAVE C,D,E IN FAC
	ex	de,hl		; GET POWER OF TEN POINTER IN (DE)
	pop	hl	; GET BUFFER POINTER
	ld	(hl),b	; PUT CHARACTER IN BUFFER
	inc	hl	; INCREMENT BUFFER POINTER
	pop	af	; GET DIGIT COUNT (THE CARRY) BACK
	pop	bc	; GET COMMA AND DP INFORMATION BACK
	jp	c,FOUCS1	; CALCULATE NEXT DIGIT IF WE HAVE NOT DONE 2
	inc	de	; WE HAVE, INCREMENT POINTER TO CORRECT PLACE
	inc	de	;  IN THE INTEGER POWER OF TEN TABLE
	ld	a,4	; GET THE DIGIT COUNT
	jp	FOUCI1	; COMPUTE THE REST OF THE DIGITS LIKE INTEGERS
			; NOTE THAT THE CARRY IS OFF

			; HERE TO CONVERT AN INTEGER INTO DECIMAL DIGITS
			; THIS EXITS WITH A=0. (DE) IS LEFT UNALTERED.
FOUTCI:	push	de	; SAVE (DE)
	ld	de,FOITBL	; GET POINTER TO THE INTEGER POWER OF TEN TABLE
	ld	a,5	; SET UP A DIGIT COUNT, WE HAVE TO CALCULATE 5
			;  DIGITS BECAUSE THE MAX POS INTEGER IS 32768
			; HERE TO CALCULATE EACH DIGIT
FOUCI1:	call	FOUTED	; SEE IF A COMMA OR DP GOES BEFORE THE DIGIT
	push	bc	; SAVE COMMA AND DECIMAL POINT INFORMATION
	push	af	; SAVE DIGIT COUNT
	push	hl	; SAVE BUFFER POINTER
	ex	de,hl		; GET THE POWER OF TEN POINTER IN (HL)
	ld	c,(hl)	; PUT THE POWER OF TEN ON THE STACK
	inc	hl
	ld	b,(hl)
	push	bc
	inc	hl	; INCREMENT THE PWR OF TEN PTR TO NEXT POWER
	ex	(sp),hl		; GET THE POWER OF TEN IN (HL) AND PUT THE
			;  POINTER ON THE STACK
	ex	de,hl		; PUT THE POWER OF TEN IN (DE)
	ld	hl,(FACLO)	; GET THE INTEGER IN (HL)
	ld	b,'0'-1	; SET UP THE DIGIT COUNT, B=DIGIT TO BE PRINTED
FOUCI2:	inc	b	; INCREMENT THE DIGIT COUNT
	ld	a,l	; SUBTRACT (DE) FROM (HL)
	sub	e	; SUBTRACT THE LOW ORDERS
	ld	l,a	; SAVE THE NEW RESULT
	ld	a,h	; SUBTRACT THE HIGH ORDERS
	sbc	a,d
	ld	h,a	; SAVE THE NEW HIGH ORDER
	jp	nc,FOUCI2	; IF (HL) WAS .GE. (DE) THEN SUBTRACT AGAIN
	add	hl,de	; WE ARE DONE, BUT WE SUBTRACTED (DE) ONCE TOO
			;  OFTEN, SO ADD IT BACK IN
	ld	(FACLO),hl	; SAVE IN THE FAC WHAT IS LEFT
	pop	de	; GET THE POWER OF TEN POINTER BACK
	pop	hl	; GET THE BUFFER POINTER BACK
	ld	(hl),b	; PUT THE NEW DIGIT IN THE BUFFER
	inc	hl	; INCREMENT THE BUFFER POINTER TO NEXT DIGIT
	pop	af	; GET THE DIGIT COUNT BACK
	pop	bc	; GET THE COMMA AND DP INFORMATION BACK
	dec	a	; WAS THAT THE LAST DIGIT?
	jp	nz,FOUCI1	; NO, GO DO THE NEXT ONE
	call	FOUTED	; YES, SEE IF A DP GOES AFTER THE LAST DIGIT
	ld	(hl),a	; PUT A ZERO AT THE END OF THE NUMBER, BUT
			;  DON'T INCREMENT (HL) SINCE AN EXPONENT OR A
			;  TRAILING SIGN MAY BE COMMING
	pop	de	; GET (DE) BACK
	ret		; ALL DONE, RETURN WITH A=0


			; CONSTANTS USED BY FOUT
TENTEN:	defb	0	; 10000000000
	defb	0
	defb	0
	defb	0
	defb	0f9h
	defb	2
	defb	15h
	defb	0a2h
FOUTDL:	defb	0e1h	;  999,999,999,999,999.5
	defb	0ffh
	defb	9fh
	defb	31h
	defb	0a9h
	defb	5fh
	defb	63h
	defb	0b2h
FOUTDU:	defb	0feh	;  9,999,999,999,999,999.5
	defb	0ffh
	defb	003
	defb	0bfh
	defb	0c9h
	defb	1bh
	defb	0eh
	defb	0b6h
DHALF:	defb	000	;  .5D0
	defb	000
	defb	000
	defb	000
FHALF:	defb	000	;  .5E0
	defb	000
	defb	000
	defb	80h
FFXDXM:	defb	000	;  1D16
	defb	000
	defb	004
	defb	0bfh
	defb	0c9h
	defb	1bh
	defb	0eh
	defb	0b6h
			; DOUBLE PRECISION POWER OF TEN TABLE
FODTBL:	defb	000	;  1D15
	defb	80h
	defb	0c6h
	defb	0a4h
	defb	7eh
	defb	8dh
	defb	003
	defb	000	;  1D14
	defb	40h
	defb	7ah
	defb	10h
	defb	0f3h
	defb	5ah
	defb	000
	defb	000	;  1D13
	defb	0a0h
	defb	72h
	defb	4eh
	defb	18h
	defb	09h
	defb	000
	defb	000	;  1D12
	defb	10h
	defb	0a5h
	defb	0d4h
	defb	0e8h
	defb	000
	defb	000
	defb	000	;  1D11
	defb	0e8h
	defb	76h
	defb	48h
	defb	17h
	defb	000
	defb	000
	defb	000	;  1D10
	defb	0e4h
	defb	0bh
	defb	54h
	defb	002
	defb	000
	defb	000
	defb	000	;  1D9
	defb	0cah
	defb	9ah
	defb	3bh
	defb	000
	defb	000
	defb	000
	defb	000	;  1D8
	defb	0e1h
	defb	0f5h
	defb	005
	defb	000
	defb	000
	defb	000
	defb	80h	;  1D7
	defb	96h
	defb	98h
	defb	000
	defb	000
	defb	000
	defb	000
	defb	40h	;  1D6
	defb	42h
	defb	0fh
	defb	000
	defb	000
	defb	000
	defb	000
			; SINGLE PRECISION POWER OF TEN TABLE
FOSTBL:	defb	0a0h	;  1E5
	defb	86h
	defb	001
	defb	10h	;  1E4
	defb	27h
	defb	000
			; INTEGER POWER OF TEN TABLE
FOITBL:	defb	10h	;  10000
	defb	27h
	defb	0e8h	;  1000
	defb	003
	defb	64h	;  100
	defb	000
	defb	0ah	;  10
	defb	000
	defb	001	;  1
	defb	000
;
; OUTPUT ROUTINES FOR OCTAL AND HEX NUMBERS
;
	PUBLIC	FOUTO,FOUTH
FOUTO:	xor	a	; MAKE A=0, SET ZERO
	ld	b,a	; SAVE IN [B]
	defb	0c2h	; "JNZ" AROUND NEXT TWO BYTES
FOUTH:	ld	b,1	; SET HEX FLAG
	push	bc	; SAVE HEX/OCTAL FLAG
	EXTRN	FRQINT
	call	FRQINT	; GET DOUBLE BYTE INT IN [H,L]
	pop	bc	; GET BACK HEX/OCTAL FLAG
	ld	de,FBUFFR	; POINTER TO OUTPUT BUFFER IN [D,E]
	push	de	; SAVE SO WE CAN RETURN IT LATER
	xor	a	; GET SET TO HAVE FIRST DIGIT FOR OCTAL
	ld	(de),a	; CLEAR DIGIT SEEN FLAG
	dec	b	; SEE IF OCTAL
	inc	b	; IF SO, ZERO SET
	ld	c,6	; SIX DIGITS FOR OCTAL
	jp	z,OCTONE	; DO FIRST OCTAL DIGIT
	ld	c,4	; FOUR DIGIT FOR HEX

OUTHLP:	add	hl,hl	; SHIFT LEFT ONE BIT
	adc	a,a	; ADD IN THE SHIFTED BIT
OUTOLP:	add	hl,hl	; SHIFT LEFT ONE BIT
	adc	a,a
	add	hl,hl
	adc	a,a
OCTONE:	add	hl,hl	; ENTER HERE FOR FIRST OCTAL DIGIT
	adc	a,a
	or	a	; SEE IF WE GOT A ZERO DIGIT
	jp	nz,MAKDIG	; NO, MAKE A DIGIT
	ld	a,c	; GET DIGIT COUNTER
	dec	a	; WAS IT GOING TO GO TO ZERO (LAST DIG?)
	jp	z,MAKDIG	; IF SO, FORCE ONE ZERO DIGIT
	ld	a,(de)	; HAVE WE PRINTED A NON-ZERO DIGIT?
	or	a	; SET CC'S
	jp	z,NOLEAD	; NO, DONT PRINT THIS LEADING ZERO
	xor	a	; GET ZERO
MAKDIG:	add	a,'0'	; MAKE NUMERIC DIGIT
	cp	'9'+1	; IS IT A BIG HEX DIGIT? (A-F)
	jp	c,NOTHAL	; NO, DONT ADD OFFSET
	add	a,'A'-'9'-1	; ADD OFFSET
NOTHAL:	ld	(de),a	; SAVE DIGIT IN FBUFFR
	inc	de	; BUMP POINTER
	ld	(de),a	; SAVE HERE TO FLAG PRINTED SIG. DIG.
NOLEAD:	xor	a	; MAKE A ZERO
	dec	c	; ALL DONE PRINTING?
	jp	z,FINOHO	; YES, RETURN
	dec	b	; SEE IF HEX OR OCTAL
	inc	b	; TEST
	jp	z,OUTOLP	; WAS OCTAL
	jp	OUTHLP	; WAS HEX

FINOHO:	ld	(de),a	; STORE FINAL ZERO
	pop	hl	; GET POINTER TO FBUFFR
	ret		; ALL DONE.
	PAGE
	SUBTTL	EXPONENTIATION AND THE SQUARE ROOT FUNCTION
			; ALL DONE

			; SUBROUTINE FOR FPWR, ATN
PSHNEG:	ld	hl,NEG	; GET THE ADDRESS OF NEG
	ex	(sp),hl		; SWITCH RET ADDR AND ADDR OF NEG
	jp	(hl)		; RETURN, THE ADDRESS OF NEG IS ON THE STACK

			; SQUARE ROOT FUNCTION
			; WE USE SQR(X)=X^.5
SQR:	call	PUSHF	; SAVE ARG X
	ld	hl,FHALF	; GET 1/2
	call	MOVFM	; SQR(X)=X^.5

	jp	FPWRT	; SKIP OVER THE NEXT 3 BYTES
			; ENTRY FROM THE OPERATOR DISPATCH ROUTINES
	PUBLIC	FPWRQ
FPWRQ:	call	FRCSNG	; MAKE SURE THE FAC IS A SNG
FPWRT:	pop	bc
	pop	de
			; GET ARG IN REGISTERS, ENTRY TO FPWR IF
			;  ARGUMENT IS ON STACK. FALL INTO FPWR


			; EXPONENTIATION --- X^Y
			; N.B. 0^0=1
			; FIRST WE CHECK IF Y=0, IF SO, THE RESULT IS 1.
			; NEXT, WE CHECK IF X=0, IF SO, THE RESULT IS 0.
			; THEN WE CHECK IF X IS POSITIVE, IF NOT, WE CHECK THAT Y IS A
			; NEGATIVE INTEGER, AND WHETHER IT IS EVEN OR ODD. IF Y IS A NEGATIVE
			; INTEGER, WE NEGATE X. IF NOT, LOG WILL GIVE AN FC ERROR WHEN WE CALL
			; IT. IF X IS NEGATIVE AND Y IS ODD, WE PUSH THE ADDRESS OF NEG ON THE
			; STACK SO WE WILL RETURN TO IT AND GET A NEGATIVE RESULT. TO COMPUTE
			; THE RESULT WE USE X^Y=EXP(Y*LOG(X))
FPWR:	
	EXTRN	CLROVC
	ld	hl,CLROVC	; RETURN TO ROUTINE TO SET NORMAL
	push	hl	; OVERFLOW MODE
	ld	a,1
	ld	(FLGOVC),a	; SET UP ONCE ONLY OVERFLOW MODE
	call	SIGN	; SEE IF Y IS ZERO
	ld	a,b	; SEE IF X IS ZERO
	jp	z,EXP	; IT IS, RESULT IS ONE
	jp	p,POSEXP	; POSITIVE EXPONENT
	or	a	; IS IT ZERO TO MINUS POWER?
	jp	z,INTDV2	; GIVE DIV BY ZERO AND CONTINUE
POSEXP:	or	a
	jp	z,ZERO0	; IT IS, RESULT IS ZERO
	push	de
	push	bc
			; SAVE X ON STACK
	ld	a,c	; CHECK THE SIGN OF X
	or	7fh	; TURN THE ZERO FLAG OFF
	call	MOVRF	; GET Y IN THE REGISTERS
			; END INTFSW CONTITTONAL
	jp	p,FPWR1	; NO PROBLEMS IF X IS POSITIVE
	push	de
	push	bc
			; SAVE Y
	call	INT	; SEE IF Y IS AN INTEGER
	pop	bc
	pop	de
			; GET Y BACK
	push	af	; SAVE LO OF INT FOR EVEN AND ODD INFORMATION
	call	FCOMP	; SEE IF WE HAVE AN INTEGER
	pop	hl	; GET EVEN-ODD INFORMATION
	ld	a,h	; PUT EVEN-ODD FLAG IN CARRY
	rra
FPWR1:	pop	hl	; GET X BACK IN FAC
	ld	(FAC-1),hl	; STORE HO'S
	pop	hl	; GET LO'S OFF STACK
	ld	(FACLO),hl	; STORE THEM IN FAC
	call	c,PSHNEG	; NEGATE NUMBER AT END IF Y WAS ODD
	call	z,NEG	; NEGATE THE NEGATIVE NUMBER
	push	de
	push	bc
			; SAVE Y AGAIN
	call	LOG	; COMPUTE EXP(Y*LOG(X))
	pop	bc
	pop	de
			; IF X WAS NEGATIVE AND Y NOT AN INTEGER THEN
	call	FMULT	;  LOG WILL BLOW HIM OUT OF THE WATER
;	JMP	EXP
	PAGE
	SUBTTL	EXPONENTIAL FUNCTON
			; THE FUNCTION EXP(X) CALCULATES e^X WHERE e=2.718282
			;  THE TECHNIQUE USED IS TO EMPLOY A COUPLE
			;  OF FUNDAMENTAL IDENTITIES THAT ALLOWS US TO
			;  USE THE BASE 2 THROUGH THE DIFFICULT PORTIONS OF
			;  THE CALCULATION:

			;  (1)e^X=2^y WHERE y=X*LOG2(e) [LOG2(e) IS
			;  LOG BASE 2
			;  OF e ]

			;  (2) 2^y=2^[ INT(y)+(y-INT(y)]
			;  (3) IF Ny=INT(y) THEN
			;  2^(Ny+y-Ny)=[2^Ny]*[2^(y-Ny)]

			;  NOW, SINCE 2^Ny IS EASY TO COMPUTE (AN EXPONENT
			;  CALCULATION WITH MANTISSA BITS OF ZERO) THE DIFFICULT
			;  PORTION IS TO COMPUTE 2^(Y-Ny) WHERE 0.LE.(Y-Ny).LT.1
			;  THIS IS ACCOMPLISHED WITH A POLYNOMIAL APPROXIMATION
			;  TO 2^Z WHERE 0.LE.Z.LT.1 . ONCE THIS IS COMPUTED WE
			;  HAVE TO EFFECT THE MULTIPLY BY 2^Ny .
EXP:	ld	bc,0081h*256+0038h
	ld	de,00aah*256+003bh	; GET LOG2(e)
	call	FMULT	; y=FAC*LOG2(e)
	ld	a,(FAC)	; MUST SEE IF TOO LARGE
	cp	88h	; ABS .GT. 128?
	jp	nc,EXP100	; IF SO OVERFLOW
	cp	68h	; IF TOO SMALL ANSWER IS 1
	jp	c,EXP200
	call	PUSHF	; SAVE y
	call	INT	; DETERMINE INTEGER POWER OF 2
	add	a,81h	; INTEGER WAS RETURNED IN A
			; BIAS IS 201 BECAUSE BINARY
			; POINT IS TO LEFT OF UNDERSTOOD 1
	pop	bc
	pop	de	; RECALL y
	jp	z,EXP110	; OVERFLOW
	push	af	; SAVE EXPONENT
	call	FSUB	; FAC=y-INT(y)
	ld	hl,EXPBCN	; WILL USE HART 1302 POLY. EVAL NOW
	call	POLY	; COMPUTE 2^[y-INT(y)]
	pop	bc	; INTEGER POWER OF 2 EXPONENT
	ld	de,0+0
	ld	c,d	; NOW HAVE FLOATING REPRESENTATION
			; OF INT(y) IN (BCDE)
	jp	FMULT	; MULTIPLY BY 2^[y-INT(y)] AND RETURN
EXP100:	call	PUSHF
EXP110:	
	ld	a,(FAC-1)	; IF NEG. THEN JUMP TO ZERO
	or	a
	jp	p,EXP115	; OVERFLOW IF PLUS
	pop	af	; NEED STACK RIGHT
	pop	af
	jp	ZERO	; GO ZERO THE FAC
EXP115:	jp	OVFIN6	; OVERFLOW
EXP200:	ld	bc,0081h*256+0000h
	ld	de,0000h*256+0000h	; 1.
	call	MOVFR
	ret
;*************************************************************
;	Hart 1302 polynomial coefficients
;*************************************************************
EXPBCN:	defb	7	; DEGREE + 1
	defb	7ch	; .00020745577403-
	defb	88h
	defb	59h
	defb	74h
	defb	0e0h	; .00127100574569-
	defb	97h
	defb	26h
	defb	77h
	defb	0c4h	; .00965065093202+
	defb	1dh
	defb	1eh
	defb	7ah
	defb	5eh	; .05549656508324+
	defb	50h
	defb	63h
	defb	7ch
	defb	1ah	; .24022713817633-
	defb	0feh
	defb	75h
	defb	7eh
	defb	18h	; .69314717213716+
	defb	72h
	defb	31h
	defb	80h
	defb	000	; 1.0
	defb	0000
	defb	0000
	defb	81h
			; END INTFSW CONDITIONAL

	PAGE
	SUBTTL	POLYNOMIAL EVALUATOR AND THE RANDOM NUMBER GENERATOR
			; EVALUATE P(X^2)*X
			; POINTER TO DEGREE+1 IS IN (HL)
			; THE CONSTANTS FOLLOW THE DEGREE
			; CONSTANTS SHOULD BE STORED IN REVERSE ORDER, FAC HAS X
			; WE COMPUTE:
			;  C0*X+C1*X^3+C2*X^5+C3*X^7+...+C(N)*X^(2*N+1)
POLYX:	call	PUSHF	; SAVE X
	ld	de,FMULTT	; PUT ADDRESS OF FMULTT ON STACK SO WHEN WE
	push	de	;  RETURN WE WILL MULTIPLY BY X
POLYX2:	push	hl	; SAVE CONSTANT POINTER
	call	MOVRF	; SQUARE X
	call	FMULT
	pop	hl	; GET CONSTANT POINTER
			; FALL INTO POLY


			; POLYNOMIAL EVALUATOR
			; POINTER TO DEGREE+1 IS IN (HL), IT IS UPDATED
			; THE CONSTANTS FOLLOW THE DEGREE
			; CONSTANTS SHOULD BE STORED IN REVERSE ORDER, FAC HAS X
			; WE COMPUTE:
			;  C0+C1*X+C2*X^2+C3*X^3+...+C(N-1)*X^(N-1)+C(N)*X^N
POLY:	call	PUSHF	; SAVE X
	ld	a,(hl)	; GET DEGREE
	inc	hl	; INCREMENT POINTER TO FIRST CONSTANT
	call	MOVFM	; MOVE FIRST CONSTANT TO FAC
	defb	006	; "MVI B" OVER NEXT BYTE
POLY1:	pop	af	; GET DEGREE
	pop	bc
	pop	de
			; GET X
	dec	a	; ARE WE DONE?
	ret	z		; YES, RETURN
	push	de
	push	bc
			; NO, SAVE X
	push	af	; SAVE DEGREE
	push	hl	; SAVE CONSTANT POINTER
	call	FMULT	; EVALUATE THE POLY, MULTIPLY BY X
	pop	hl	; GET LOCATION OF CONSTANTS
	call	MOVRM	; GET CONSTANT
	push	hl	; STORE LOCATION OF CONSTANTS SO FADD AND FMULT
	call	FADD	;  WILL NOT SCREW THEM UP, ADD IN CONSTANT
	pop	hl	; MOVE CONSTANT POINTER TO NEXT CONSTANT
	jp	POLY1	; SEE IF DONE


			; PSUEDO-RANDOM NUMBER GENERATOR
			; IF ARG=0, THE LAST RANDOM NUMBER GENERATED IS RETURNED
			; IF ARG .LT. 0, A NEW SEQUENCE OF RANDOM NUMBERS IS STARTED
			;  USING THE ARGUMENT
			; TO FORM THE NEXT RANDOM NUMBER IN THE SEQUENCE, WE MULTIPLY THE
			; PREVIOUS RANDOM NUMBER BY A RANDOM CONSTANT, AND ADD IN ANOTHER
			; RANDOM CONSTANT. THEN THE HO AND LO BYTES ARE SWITCHED, THE
			; EXPONENT IS PUT WHERE IT WILL BE SHIFTED IN BY NORMAL, AND THE
			; EXPONENT IN THE FAC SET TO 200 SO THE RESULT WILL BE LESS THAN 1.
			; THIS IS THEN NORMALIZED AND SAVED FOR THE NEXT TIME.
			; THE HO AND LO BYTES WERE SWITCHED SO WE HAVE A RANDOM CHANCE OF
			; GETTING A NUMBER LESS THAN OR GREATER THAN .5
	PUBLIC	RNDCOP
RNDCOP:	defb	52h	; A COPY OF RNDX TO COPY AT RUN TIME
	defb	0c7h
	defb	4fh
	defb	80h
	PUBLIC	RNDMON
RNDMON:	call	CHRGTR
	PUBLIC	RNDMN2
RNDMN2:	push	hl	; SAVE TEXT POINTER FOR MONADIC RND
	ld	hl,FONE	; PRETEND ARG IS 1.0
	call	MOVFM
	call	RND	; PICK UP A RANDOM VALUE
	pop	hl	; GET BACK THE TEXT POINTER
	jp	VALSNG
RND:	call	SIGN	; GET SIGN OF ARG
	ld	hl,RNDCNT+1
	jp	m,RNDSTR	; START NEW SEQUENCE IF NEGATIVE
	ld	hl,RNDX	; GET LAST NUMBER GENERATED
	call	MOVFM
	ld	hl,RNDCNT+1
	ret	z		; RETURN LAST NUMBER GENERATED IF ZERO
	add	a,(hl)	; GET COUNTER INTO CONSTANTS
			; AND ADD ONE
	and	7
	ld	b,0
	ld	(hl),a
	inc	hl
	add	a,a
	add	a,a
	ld	c,a
	add	hl,bc
	call	MOVRM
	call	FMULT
	ld	a,(RNDCNT)
	inc	a
	and	3
	ld	b,0
	cp	1
	adc	a,b
	ld	(RNDCNT),a
	ld	hl,RNDTB2-4
	add	a,a
	add	a,a
	ld	c,a
	add	hl,bc
	call	FADDS
RND1:	call	MOVRF	; SWITCH HO AND LO BYTES,
	ld	a,e	; GET LO
	ld	e,c	; PUT HO IN LO BYTE
	xor	4fh
	ld	c,a	; PUT LO IN HO BYTE
	ld	(hl),80h	; MAKE RESULT POSITIVE
	dec	hl	; GET POINTER TO EXPONENT
	ld	b,(hl)	; PUT EXPONENT IN OVERFLOW POSITION
	ld	(hl),80h	; SET EXP SO RESULT WILL BE BETWEEN 0 AND 1
	ld	hl,RNDCNT-1
	inc	(hl)	; INCREMENT THE PERTUBATION COUNT
	ld	a,(hl)	; SEE IF ITS TIME
	sub	0abh
	jp	nz,NTPTRB
	ld	(hl),a	; ZERO THE COUNTER
	inc	c
	dec	d
	inc	e
NTPTRB:	call	NORMAL	; NORMALIZE THE RESULT
	ld	hl,RNDX	; SAVE RANDOM NUMBER GENERATED FOR NEXT
	jp	MOVMF	;  TIME
RNDSTR:	ld	(hl),a	; ZERO THE COUNTERS
	dec	hl
	ld	(hl),a
	dec	hl
	ld	(hl),a
	jp	RND1

			; STORAGE FOR RND
	defb	0
	PUBLIC	RNDCNT
RNDCNT:	defb	0
	defb	0
RNDTAB:	defb	35h
	defb	4ah
	defb	0cah
	defb	99h
	defb	39h
	defb	1ch
	defb	76h
	defb	98h
	defb	22h
	defb	95h
	defb	0b3h
	defb	98h
	defb	0ah
	defb	0ddh
	defb	47h
	defb	98h
	defb	53h
	defb	0d1h
	defb	99h
	defb	99h
	defb	0ah
	defb	1ah
	defb	9fh
	defb	98h
	defb	65h
	defb	0bch
	defb	0cdh
	defb	98h
	defb	0d6h
	defb	77h
	defb	3eh
	defb	98h
	PUBLIC	RNDX
RNDX:	defb	52h	; LAST RANDOM NUMBER GENERATED, BETWEEN 0 AND 1
	defb	0c7h
	defb	4fh
	defb	80h
RNDTB2:	defb	68h
	defb	0b1h
	defb	46h
	defb	68h
	defb	99h
	defb	0e9h
	defb	92h
	defb	69h
	defb	10h
	defb	0d1h
	defb	75h
	defb	68h
	PAGE
	SUBTTL	SINE, COSINE AND TANGENT FUNCTIONS
			; COSINE FUNCTION
			; IDEA: USE COS(X)=SIN(X+PI/2)
BOBTST	defl 0
COS:	ld	hl,PI2	; ADD PI/2 TO FAC
	call	FADDS
			; END INTFSW
			; FALL INTO SIN


			; SINE FUNCTION
			; IDEA: USE IDENTITIES TO GET FAC IN QUADRANTS I OR IV
			; THE FAC IS DIVIDED BY 2*PI AND THE INTEGER PART IS IGNORED BECAUSE
			; SIN(X+2*PI)=SIN(X). THEN THE ARGUMENT CAN BE COMPARED WITH PI/2 BY
			; COMPARING THE RESULT OF THE DIVISION WITH PI/2/(2*PI)=1/4.
			; IDENTITIES ARE THEN USED TO GET THE RESULT IN QUADRANTS I OR IV.
			; AN APPROXIMATION POLYNOMIAL IS THEN USED TO COMPUTE SIN(X).
SIN:	
	ld	a,(FAC)	; WILL SEE IF .LT.2^-10
			; AND IF SO SIN(X)=X
	cp	77h
	ret	c
			; SIN BY HART #3341
	ld	bc,007eh*256+0022h
	ld	de,00f9h*256+0083h	; WILL CALCULATE X=FAC/(2*PI)
	call	FMULT
	call	PUSHF	; SAVE X
	call	INT	; FAC=INT(X)
	pop	bc
	pop	de
			; FETCH X TO REGISTERS
	call	FSUB	; FAC=X-INT(X)
	ld	bc,007fh*256+0000h
	ld	de,0000h*256+0000h	; GET 1/4
	call	FCOMP	; FAC=FAC-1/4
	jp	m,SIN2A
	ld	bc,007fh*256+0080h
	ld	de,0000h*256+0000h	; -1/4
	call	FADD
	ld	bc,0080h*256+0080h
	ld	de,0000h*256+0000h	; -1/2
	call	FADD	; X=X-1/2
	call	SIGN
	call	p,NEG	; MAKE SURE IF QUADRANTS II,IV
			; WE WORK WITH 1/4-X
SIN2:	ld	bc,007fh*256+0000h
	ld	de,0000h*256+0000h	; 1/4
	call	FADD
	call	NEG
SIN2A:	ld	a,(FAC-1)	; MUST REDUCE TO [0,1/4]
	or	a	; SIGN IN PSW
	push	af	; SAVE FOR POSSIBLE NEG. AFTER CALC
	jp	p,SIN3
	xor	80h
	ld	(FAC-1),a	; NOW IN [0,1/4]
SIN3:	ld	hl,SINCON	; POINT TO HART COEFFICIENTS
	call	POLYX	; DO POLY EVAL
	pop	af	; NOW TO DO SIGN
	ret	p		; OK IF POS
	ld	a,(FAC-1)	; FETCH SIGN BYTE
	xor	80h	; MAKE NEG
	ld	(FAC-1),a	; REPLACE SIGN
	ret
			; END OF INTFSW COND

			; CONSTANTS FOR SIN, COS
P1B2PI:	defb	000	; 1/(2*PI)
	defb	000
	defb	000
	defb	000
	defb	83h
	defb	0f9h
	defb	22h
	defb	7eh
PI2:	defb	0dbh	;  PI/2
	defb	0fh
	defb	49h
	defb	81h
FR4:	defb	000	;  1/4
	defb	000
	defb	000
	defb	7fh
SINCON:			; HART ALGORITHM 3341 CONSTANTS
;NOTE THAT HART CONSTANTS HAVE BEEN SCALED BY A POWER OF 2
;THIS IS DUE TO RANGE REDUCTION AS A % OF 2*PI RATHER THAN PI/2
;WOULD NEED TO MULTIPLY ARGUMENT BY 4 BUT INSTEAD WE FACTOR THIS
;THRU THE CONSTANTS.
	defb	5	; DEGREE
	defb	0fbh	;  .1514851E-3
	defb	0d7h
	defb	1eh
	defb	86h
	defb	65h	;  -.4673767E-2
	defb	26h
	defb	99h
	defb	87h
	defb	58h	;  .7968968E-1
	defb	34h
	defb	23h
	defb	87h
	defb	0e1h	;  -.6459637
	defb	5dh
	defb	0a5h
	defb	86h
	defb	0dbh	;  1.570796
	defb	0fh
	defb	49h
	defb	83h

			; TANGENT FUNCTION
			; TAN(X)=SIN(X)/COS(X)
TAN:	call	PUSHF	; SAVE ARG
	call	SIN	;  TAN(X)=SIN(X)/COS(X)
	pop	bc	; GET X OFF STACK
	pop	hl	; PUSHF SMASHES (DE)
	call	PUSHF
	ex	de,hl		; GET LO'S WHERE THEY BELONG
	call	MOVFR
	call	COS
	jp	FDIVT

	PAGE
	SUBTTL	ARCTANGENT FUNCTION
			; IDEA: USE IDENTITIES TO GET ARG BETWEEN 0 AND 1 AND THEN USE AN
			; APPROXIMATION POLYNOMIAL TO COMPUTE ARCTAN(X)
ATN:	call	SIGN	; SEE IF ARG IS NEGATIVE
	call	m,PSHNEG	; IF ARG IS NEGATIVE, USE:
	call	m,NEG	;  ARCTAN(X)=-ARCTAN(-X)
	ld	a,(FAC)	; SEE IF FAC .GT. 1
	cp	81h
	jp	c,ATN2
	ld	bc,81h*100h+0	; GET THE CONSTANT 1
	ld	d,c
	ld	e,c	; COMPUTE RECIPROCAL TO USE THE IDENTITY:
	call	FDIV	;  ARCTAN(X)=PI/2-ARCTAN(1/X)
	ld	hl,FSUBS	; PUT FSUBS ON THE STACK SO WE WILL RETURN
	push	hl	;  TO IT AND SUBTRACT THE REULT FROM PI/2
ATN2:	ld	hl,ATNCON	; EVALUATE APPROXIMATION POLYNOMIAL
	call	POLYX
	ld	hl,PI2	; GET POINTER TO PI/2 IN CASE WE HAVE TO
	ret		;  SUBTRACT THE RESULT FROM PI/2

			; CONSTANTS FOR ATN
ATNCON:	defb	09h	; DEGREE
	defb	4ah	;  .002866226
	defb	0d7h
	defb	3bh
	defb	78h
	defb	002	;  -.01616574
	defb	6eh
	defb	84h
	defb	7bh
	defb	0feh	;  .04290961
	defb	0c1h
	defb	2fh
	defb	7ch
	defb	74h	;  -.07528964
	defb	31h
	defb	9ah
	defb	7dh
	defb	84h	;  .1065626
	defb	3dh
	defb	5ah
	defb	7dh
	defb	0c8h	;  -.142089
	defb	7fh
	defb	91h
	defb	7eh
	defb	0e4h	;  .1999355
	defb	0bbh
	defb	4ch
	defb	7eh
	defb	6ch	;  -.3333315
	defb	0aah
	defb	0aah
	defb	7fh
	defb	000	;  1.0
	defb	000
	defb	000
	defb	81h

	end

